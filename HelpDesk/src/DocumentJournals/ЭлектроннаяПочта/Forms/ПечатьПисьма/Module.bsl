&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Параметры.Свойство("Письмо") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Письмо = Параметры.Письмо;
	
	ТекстHTML = ВстроеннаяПочтаСервер.СформироватьТекстHTMLДляПисьма(Письмо, УникальныйИдентификатор);
	
	ОбработатьТекстHTMLСервер(ТекстHTML);

КонецПроцедуры

&НаКлиенте
Процедура Печать(Команда)
	
	Элементы.ТекстHTML.Документ.execCommand("Print"); 
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)		
	Если ТаблицаВложений.Количество()<>0 Тогда
		РаботаСHTML.ПередатьОбъектыСтраницыНаКлиента(ТекстHTML,ТаблицаВложений);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОбработатьТекстHTMLСервер(ТекстHTML)
	СимвНачала = СтрНайти(ТекстHTML,"##<");
	Если СимвНачала <> 0 Тогда
		СимвКонца = СтрНайти(ТекстHTML,">##",НаправлениеПоиска.СНачала,СимвНачала);
		АдресТаблицыВложений = Сред(ТекстHTML,СимвНачала+3,СимвКонца-СимвНачала-3);
		ТекстПисьма = СтрЗаменить(ТекстHTML,"##<"+АдресТаблицыВложений+">##","");
		
		Если ЭтоАдресВременногоХранилища(АдресТаблицыВложений) Тогда
			ДанныеТаблицаВложений = ПолучитьИзВременногоХранилища(АдресТаблицыВложений);
			
			Если ТипЗнч(ДанныеТаблицаВложений) = Тип("Строка") Тогда
				ТаблицаВложенийССервера = ЗначениеИзСтрокиВнутр(ДанныеТаблицаВложений);
				Если ТипЗнч(ТаблицаВложенийССервера) = Тип("ТаблицаЗначений") Тогда
					ТаблицаВложений.Загрузить(ТаблицаВложенийССервера);
					УдалитьИзВременногоХранилища(АдресТаблицыВложений);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры