
&НаКлиенте
Процедура Сформировать(Команда)
	
	ОчиститьСообщения();
	Если Не ПроверитьЗаполнение() Тогда 
		Возврат;
	КонецЕсли;	
	
	СформироватьНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура СформироватьНаСервере()
	
	ОтчетОбъект = РеквизитФормыВЗначение("Отчет");
	ОтчетОбъект.Сформировать(Результат, Документ, ТипыСвязей, КоличествоУровней);
	
КонецПроцедуры	

&НаКлиенте
Процедура ОграничиватьКоличествоУровнейПриИзменении(Элемент)
	
	Если Не ОграничиватьКоличествоУровней Тогда 
		КоличествоУровней = 0;
	Иначе	
		КоличествоУровней = 1;
	КонецЕсли;	
	
	Элементы.КоличествоУровней.Доступность = ОграничиватьКоличествоУровней;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.КоличествоУровней.Доступность = ОграничиватьКоличествоУровней;
	
	Если Параметры.Свойство("Документ") И ЗначениеЗаполнено(Параметры.Документ) Тогда 
		Документ = Параметры.Документ;
		СформироватьНаСервере();
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	ПроверяемыеРеквизиты.Добавить("Документ");
	Если ОграничиватьКоличествоУровней Тогда 
		ПроверяемыеРеквизиты.Добавить("КоличествоУровней");
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СписокВыбора = Новый СписокЗначений;
	СписокВыбора.Добавить("Справочник.ВходящиеДокументы", 	НСтр("ru = 'Входящий документ'"));
	СписокВыбора.Добавить("Справочник.ИсходящиеДокументы",  НСтр("ru = 'Исходящий документ'"));
	СписокВыбора.Добавить("Справочник.ВнутренниеДокументы", НСтр("ru = 'Внутренний документ'"));
	СписокВыбора.Добавить("Справочник.Мероприятия", НСтр("ru = 'Мероприятие'"));
	СписокВыбора.Добавить("Справочник.Проекты", НСтр("ru = 'Проект'"));
	СписокВыбора.Добавить("Справочник.Файлы", НСтр("ru = 'Файл'"));
	СписокВыбора.Добавить("Справочник.ШаблоныВнутреннихДокументов", НСтр("ru = 'Шаблон внутреннего документа'"));
	СписокВыбора.Добавить("Справочник.ШаблоныВходящихДокументов", НСтр("ru = 'Шаблон входящего документа'"));
	СписокВыбора.Добавить("Справочник.ШаблоныИсходящихДокументов", НСтр("ru = 'Шаблон исходящего документа'"));
	СписокВыбора.Добавить("Документ.ВходящееПисьмо", НСтр("ru = 'Входящее письмо'"));
	СписокВыбора.Добавить("Документ.ИсходящееПисьмо", НСтр("ru = 'Исходящее письмо'"));
	СписокВыбора.Добавить("Документ.ВходящееСообщениеМЭДО", НСтр("ru = 'Входящее сообщение МЭДО'"));
	СписокВыбора.Добавить("Документ.ИсходящееСообщениеМЭДО", НСтр("ru = 'Исходящее сообщение МЭДО'"));
	
	ВыбранныйЭлемент = Неопределено;

	
	ПоказатьВыборИзСписка(Новый ОписаниеОповещения("ДокументНачалоВыбораЗавершение", ЭтотОбъект, Новый Структура("Элемент", Элемент)), СписокВыбора, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументНачалоВыбораЗавершение(ВыбранныйЭлемент1, ДополнительныеПараметры) Экспорт
	
	Элемент = ДополнительныеПараметры.Элемент;
	
	
	ВыбранныйЭлемент = ВыбранныйЭлемент1;
	Если ВыбранныйЭлемент = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	ИмяТипа = СтрЗаменить(ВыбранныйЭлемент.Значение, "Справочник.", "СправочникСсылка.");
	ИмяТипа = СтрЗаменить(ИмяТипа, "Документ.", "ДокументСсылка.");
	ОписаниеТипа = Новый ОписаниеТипов(ИмяТипа);
	ТекущаяСтрока = ОписаниеТипа.ПривестиЗначение(Документ);
	
	ПараметрыФормы = Новый Структура("ТекущаяСтрока", ТекущаяСтрока);
	ОткрытьФорму(ВыбранныйЭлемент.Значение + ".ФормаВыбора", ПараметрыФормы, Элемент);

КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	Если ТипЗнч(Настройки) = Тип("Соответствие") 
		И Настройки.Получить("ОграничиватьКоличествоУровней") <> Неопределено Тогда 
		Элементы.КоличествоУровней.Доступность = Настройки["ОграничиватьКоличествоУровней"];
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ТипыСвязейОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТипыСвязей.Очистить();
	ТипыСвязей.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.ТипыСвязей");
	
КонецПроцедуры


&НаКлиенте
Процедура ТипыСвязейНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОчиститьСообщения();
	Если Не ЗначениеЗаполнено(Документ) Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Поле ""Документ"" не заполнено'"),,"Документ",,);
		Возврат;			
	КонецЕсли;	
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Документ", Документ);
	ПараметрыФормы.Вставить("ВыбранныеТипыСвязей", ТипыСвязей);
	
	ВыбранныеТипыСвязей = Неопределено;

	
	ОткрытьФорму("Отчет.СтруктураСвязейДокумента.Форма.ВыборТиповСвязи", ПараметрыФормы, ЭтотОбъект,,,, Новый ОписаниеОповещения("ТипыСвязейНачалоВыбораЗавершение", ЭтотОбъект), РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

&НаКлиенте
Процедура ТипыСвязейНачалоВыбораЗавершение(Результат1, ДополнительныеПараметры) Экспорт
	
	ВыбранныеТипыСвязей = Результат1;
	Если ТипЗнч(ВыбранныеТипыСвязей) <> Тип("СписокЗначений") Тогда 
		Возврат;
	КонецЕсли;	
	
	ТипыСвязей.ЗагрузитьЗначения(ВыбранныеТипыСвязей.ВыгрузитьЗначения());
	
КонецПроцедуры


