
Функция ПредставлениеПроектаЗадачи(Проект, ПроектнаяЗадача, ВыводитьЗадачаПроектаНеУказана = Неопределено) Экспорт 
	
	ПредставлениеПроектаЗадачи = "";
	
	Если ЗначениеЗаполнено(ПроектнаяЗадача) Тогда 
		ПредставлениеПроектаЗадачи = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1 (%2)'"),
			Строка(Проект),
			Строка(ПроектнаяЗадача));
	ИначеЕсли ЗначениеЗаполнено(Проект) Тогда 
		
		Если ВыводитьЗадачаПроектаНеУказана = Неопределено Тогда 
			ВыводитьЗадачаПроектаНеУказана = РаботаСПроектами.ЕстьПроектныеЗадачи(Проект);
		КонецЕсли;	
			
		Если ВыводитьЗадачаПроектаНеУказана Тогда 
			ПредставлениеПроектаЗадачи = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '%1 (%2)'"),
				Строка(Проект),
				"Задача проекта не указана");
		Иначе
			ПредставлениеПроектаЗадачи = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '%1'"),
				Строка(Проект));
		КонецЕсли;
			
	КонецЕсли;	
	
	Возврат ПредставлениеПроектаЗадачи;
	
КонецФункции	

//Осуществляет поиск задачи в дереве задач
//Параметры:КоллекцияЗадачОдногоУровня - список задач одного уровня дерева
//			ИскомаяЗадача - задач, которую необходимо найти
//			Индекс = значение индекса найденной задачи в дереве. Если задача не найдена, 
//						значение параметра не изменяется.
Процедура НайтиЗадачуВДеревеПоСсылке(КоллекцияЗадачОдногоУровня, ИскомаяЗадача, Индекс) Экспорт
	
	Если ТипЗнч(Индекс) = Тип("Число") И Индекс > -1 Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Задача Из КоллекцияЗадачОдногоУровня Цикл
		Если Задача.Ссылка = ИскомаяЗадача Тогда
			Индекс = Задача.ПолучитьИдентификатор();
		Иначе
			НайтиЗадачуВДеревеПоСсылке(Задача.ПолучитьЭлементы(), ИскомаяЗадача, Индекс);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры
