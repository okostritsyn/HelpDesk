////////////////////////////////////////////////////////////////////////////////
// Подсистема "Контактная информация".
//
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Событие ПриИзменении в колонке Представление таблицы контактной информации
Процедура ПредставлениеПриИзменении(Форма, Элемент) Экспорт
	
	ДанныеСтроки = ПолучитьСтрокуДополнительныхЗначений(Форма, Элемент);
	Если (ДанныеСтроки <> Неопределено) И (ДанныеСтроки.ТипНомер = 2) Тогда
		Значение = Элемент.ТекстРедактирования;
		ЗаполнитьПоляВЗаписиПоПредставлениюТелефон(Значение, ДанныеСтроки.ЗначенияПолей);
	КонецЕсли;
	Если (ДанныеСтроки <> Неопределено) И (ДанныеСтроки.ТипНомер = 1) и Элемент.ТекстРедактирования = "" Тогда
		ДанныеСтроки.ЗначенияПолей.Очистить()
	КонецЕсли;	
	
КонецПроцедуры

// Событие НачалоВыбора в колонке Представление таблицы контактной информации
Процедура ПредставлениеНачалоВыбора(Форма, Элемент, Модифицированность, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ДанныеСтроки = ПолучитьСтрокуДополнительныхЗначений(Форма, Элемент);
	Если (ДанныеСтроки = Неопределено) И (ДанныеСтроки.ТипНомер = 0) Тогда
		Возврат;
	КонецЕсли;
	
	Если ДанныеСтроки.ТипНомер = 1 Тогда
		ИмяФормыРедактирования =  "ОбщаяФорма.ВводАдреса";
	Иначе
		ИмяФормыРедактирования =  "ОбщаяФорма.ВводТелефона";
	КонецЕсли;
	
	ПараметрыДляФормы = Новый Структура;
	ПараметрыДляФормы.Вставить("ЗначенияПолей",                ДанныеСтроки.ЗначенияПолей);
	ПараметрыДляФормы.Вставить("Вид",                          ДанныеСтроки.Вид);
	ПараметрыДляФормы.Вставить("БылиВнесеныИзменения",         Ложь);
	ПараметрыДляФормы.Вставить("Представление",                Элемент.ТекстРедактирования);
	ПараметрыДляФормы.Вставить("РедактированиеТолькоВДиалоге", Не Элемент.РедактированиеТекста);
	ПараметрыДляФормы.Вставить("АдресТолькоРоссийский",        ДанныеСтроки.ТолькоРоссийский);
	
	Результат = ОткрытьФормуМодально(ИмяФормыРедактирования, ПараметрыДляФормы);
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		Форма[Элемент.Имя]   = Результат.Представление;
		ДанныеСтроки.ЗначенияПолей = Результат.ЗначенияПолей;
		Модифицированность      = Истина;
	КонецЕсли;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Возвращает строку дополнительных значений по имени реквизита.
//
// Параметры:
//	Форма - Форма - передаваемая форма.
//	Элемент - ДанныеФормыСтруктураСКоллекцией - данные формы.
//
// Возвращаемое значение - Неопределено или СтрокаКоллекции - строка коллекции.
//
Функция ПолучитьСтрокуДополнительныхЗначений(Форма, Элемент)
	
	Отбор = Новый Структура("ИмяРеквизита", Элемент.Имя);
	Строки = Форма.КонтактнаяИнформацияОписаниеДополнительныхРеквизитов.НайтиСтроки(Отбор);
	
	Возврат ?(Строки.Количество() = 0, Неопределено, Строки[0]);
	
КонецФункции

// Заполняет по полю Представление остальные поля в записи для телефона.
//
// Параметры:
//	Представление - Строка - представление телефона.
//	СписокПолей - ДанныеФормыКоллекция - список полей.
//
Процедура ЗаполнитьПоляВЗаписиПоПредставлениюТелефон(Представление, СписокПолей)
	
	СтрокаТелефона = СокрЛП(Представление);
	СписокПолей.Очистить();
	КодСтраны     = "";
	КодГорода     = "";
	НомерТелефона = "";
	Добавочный    = "";
	Комментарий   = "";
	
	// вырежем добавочный номер с комментарием
	ПозицияДобавочный = СтрНайти(ВРЕГ(СтрокаТелефона), "ДОБ.");
	Если ПозицияДобавочный <> 0 Тогда
		ДобавочныйСКомментарием = СокрЛП(Сред(СтрокаТелефона, ПозицияДобавочный + 4));
		
		СтрокаТелефона = СокрЛП(Лев(СтрокаТелефона, ПозицияДобавочный - 1));
		
		Если Прав(СтрокаТелефона, 1) = "," Тогда
			СтрокаТелефона = Лев(СтрокаТелефона, СтрДлина(СтрокаТелефона)-1);
		КонецЕсли;
		
		ПозицияДобавочный = СтрНайти(ВРЕГ(ДобавочныйСКомментарием), ", ");
		
		Если ПозицияДобавочный <> 0 Тогда
			Добавочный = СокрЛП(Лев(ДобавочныйСКомментарием, ПозицияДобавочный - 1));
			Комментарий = СокрЛП(Сред(ДобавочныйСКомментарием, ПозицияДобавочный + 2));
		Иначе
			Добавочный = ДобавочныйСКомментарием;
		КонецЕсли;
		
	КонецЕсли;
	
	// вырежем код города
	ПозицияОткрывающаясяСкобка = СтрНайти(СтрокаТелефона, "(");
	Если ПозицияОткрывающаясяСкобка <> 0 Тогда
		КодСтраны = СокрЛП(Лев(СтрокаТелефона, ПозицияОткрывающаясяСкобка - 1));
		
		СтрокаТелефона = СокрЛП(Сред(СтрокаТелефона, ПозицияОткрывающаясяСкобка + 1));
		ПозицияЗакрывающаясяСкобка = СтрНайти(СтрокаТелефона, ")");
		
		Если ПозицияЗакрывающаясяСкобка <> 0 Тогда
			КодГорода = СокрЛП(Лев(СтрокаТелефона, ПозицияЗакрывающаясяСкобка - 1));
			СтрокаТелефона = СокрЛП(Сред(СтрокаТелефона, ПозицияЗакрывающаясяСкобка + 1));
		КонецЕсли;
	КонецЕсли;
	
	ПозицияЗапятая = СтрНайти(СтрокаТелефона, ", ");
	// Если добавочного номера нет - ориентируемся по номеру телефона и комментарию
	Если ПозицияДобавочный = 0 И ПозицияЗапятая <> 0 Тогда
		// вырежем комментарий
		НомерТелефона = СокрЛП(Лев(СтрокаТелефона, ПозицияЗапятая - 1));
		Комментарий = СокрЛП(Сред(СтрокаТелефона, ПозицияЗапятая + 2));
	Иначе
		// все оставшееся это номер
		НомерТелефона = СтрокаТелефона;
	КонецЕсли;
	
	// Поправим представление
	Представление = СформироватьПредставлениеТелефона(КодСтраны, КодГорода, НомерТелефона, Добавочный, Комментарий);
	СписокПолей.Добавить(КодСтраны,     "КодСтраны");
	СписокПолей.Добавить(КодГорода,     "КодГорода");
	СписокПолей.Добавить(НомерТелефона, "НомерТелефона");
	СписокПолей.Добавить(Добавочный,    "Добавочный");
	СписокПолей.Добавить(Комментарий,   "Комментарий");
	
КонецПроцедуры

// Формирует строковое представление телефона.
//
// Параметры:
//	КодСтраны - Строка - код страны.
//	КодГорода - Строка - код города.
//	НомерТелефона - Строка - номер телефона.
// 	Добавочный - Строка - добавочный номер.
//	Комментарий - Строка - комментарий.
//
// Возвращаемое значение - Строка - представление телефона.
//
Функция СформироватьПредставлениеТелефона(КодСтраны, КодГорода, НомерТелефона, Добавочный, Комментарий) Экспорт
	
	Представление = СокрЛП(КодСтраны);
	
	Если Не ПустаяСтрока(КодГорода) Тогда
		Представление = Представление + ?(ПустаяСтрока(Представление), "", " ") + "(" + СокрЛП(КодГорода) + ")";
	КонецЕсли;
	
	Если Не ПустаяСтрока(НомерТелефона) Тогда
		Представление = Представление + ?(ПустаяСтрока(Представление), "", " ") + СокрЛП(НомерТелефона);
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(Добавочный) Тогда
		Представление = Представление + ?(ПустаяСтрока(Представление), "", ", ") + "доб. " + СокрЛП(Добавочный);
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(Комментарий) Тогда
		Представление = Представление + ?(ПустаяСтрока(Представление), "", ", ") + СокрЛП(Комментарий);
	КонецЕсли;
	
	Возврат Представление;
	
КонецФункции
