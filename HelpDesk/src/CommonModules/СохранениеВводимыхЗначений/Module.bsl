// Функция формирует таблицу значений с наименованиями элементов формы
// и их значениями по переданной структуре. Передаваемая структура
// должна содержать соответствия ключ - имя поля, значение - значение
// реквизита формы, связанное с этим полем.
//
Функция СформироватьТаблицуСохраняемыхЭлементов(знач СохраняемыеЭлементы) Экспорт
	
	СписокСохраняемыхПолей = Новый ТаблицаЗначений;
	СписокСохраняемыхПолей.Колонки.Добавить("ИмяПоляЭлемента");
	СписокСохраняемыхПолей.Колонки.Добавить("ЗначениеРеквизита");
	
	Для Каждого СохраняемыйЭлемент Из СохраняемыеЭлементы Цикл
		НоваяСтрока = СписокСохраняемыхПолей.Добавить();
		НоваяСтрока.ИмяПоляЭлемента = СохраняемыйЭлемент.Ключ;
		НоваяСтрока.ЗначениеРеквизита = СохраняемыйЭлемент.Значение;
	КонецЦикла;
	
	Возврат СписокСохраняемыхПолей;
	
КонецФункции

// Обновляет информацию по истории выбора новым значением, а так же
// обновляет список выбора по всем элементам
//
// Параметры
// ЭтотОбъект      - УправляемаяФорма - форма, на элементы которой, навешивается механизм
// ЭлементыСЗначениями - специально сформированная таблица значений
//                 содержащая сведения с именами элементов и их текущими значениями
// ДополнительнаяИдентификация - строка - дополнительная идентификация, необходимая
//                 для поддержки уникальности адресации элементов\
//
Процедура ОбновитьСпискиВыбора(знач ЭтотОбъект,
                               знач СохраняемыеЭлементы,
                               знач ДополнительнаяИдентификация = "") Экспорт
	
	Префикс = ?(ЗначениеЗаполнено(ДополнительнаяИдентификация), ДополнительнаяИдентификация + ".", "");
	
	Для Каждого ЭлементТаблицы Из СохраняемыеЭлементы Цикл
		Если ЗначениеЗаполнено(ЭлементТаблицы.ЗначениеРеквизита) Тогда
			ДобавитьЗначениеВИсториюВвода(
			              Префикс+ЭлементТаблицы.ИмяПоляЭлемента,
			              ЭлементТаблицы.ЗначениеРеквизита);
		КонецЕсли;
	КонецЦикла;
	
	ЗаполнитьСписокВыбора(ЭтотОбъект, СохраняемыеЭлементы, ДополнительнаяИдентификация);
	
КонецПроцедуры

// Заполняет списки выбора элементов форм считывая сохраненные 
// 
// Параметры
// ЭтотОбъект      - УправляемаяФорма - форма, на элементы которой, навешивается механизм
// ЭлементыСЗначениями - специально сформированная таблица значений
//                 содержащая сведения с именами элементов и их текущими значениями
// ДополнительнаяИдентификация - строка - дополнительная идентификация, необходимая
//                 для поддержки уникальности адресации элементов\
//
Процедура ЗаполнитьСписокВыбора(ЭтотОбъект,
                                знач ЭлементыСЗначениями,
                                знач ДополнительнаяИдентификация = "") Экспорт
	
	Элементы = ЭтотОбъект.Элементы;
	
	Префикс = ?(ЗначениеЗаполнено(ДополнительнаяИдентификация), ДополнительнаяИдентификация + ".", "");
	
	Для Каждого ЭлементТЗ Из ЭлементыСЗначениями Цикл
		НаименованиеЭлементаФормы = ЭлементТЗ.ИмяПоляЭлемента;
		
		Если ЗначениеЗаполнено(НаименованиеЭлементаФормы) Тогда
			СписокИсторииВвода = ПолучитьСписокЗначенийИсторииВвода(Префикс+ЭлементТЗ.ИмяПоляЭлемента);
			
			Если СписокИсторииВвода <> Неопределено Тогда
				СписокВыбора = Элементы.Найти(НаименованиеЭлементаФормы).СписокВыбора;

				Элементы.Найти(НаименованиеЭлементаФормы).КнопкаСпискаВыбора = 
				          ?(СписокИсторииВвода.Количество() > 0, Истина, Ложь);

				СписокВыбора.Очистить();
				Для Каждого ЭлементСпискаИсторииВыбора Из СписокИсторииВвода Цикл
					НовыйЭлемент = СписокВыбора.Добавить();
					НовыйЭлемент.Значение = ЭлементСпискаИсторииВыбора.Значение;
				КонецЦикла;
			КонецЕсли; // СписокИсторииВвода <> Неопределено
		КонецЕсли; // ЗначениеЗаполнено(НаименованиеЭлементаФормы)
	КонецЦикла; // Для Каждого ЭлементТЗ Из ЭлементыСЗначениями Цикл
	
КонецПроцедуры

// Добавляет значение для истории ввода по идентификатору в конец списка
// Если запись уже существует - она переносится в конец
// Если записей больше чем 15 то первая запись удаляется
//
// Параметры:
// Идентификатор - Строка - идентификатор по которому будет вестись история ввода
// Значение      - значение добавляемое к истории
//
Процедура ДобавитьЗначениеВИсториюВвода(Идентификатор, Значение)
	
	МассивВведенныхЗначений = ПолучитьНастройкуСистемногоХранилища(Идентификатор,"МеханизмСохраненияПоследнихВыбранныхЗначений");
	
	Если МассивВведенныхЗначений = Неопределено Тогда
		МассивВведенныхЗначений = Новый Массив;
	КонецЕсли;
	
	Для Индекс = 0 по МассивВведенныхЗначений.ВГраница() Цикл
		Если МассивВведенныхЗначений.Получить(Индекс) = Значение Тогда
			МассивВведенныхЗначений.Удалить(Индекс);
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	МассивВведенныхЗначений.Вставить(0, Значение);
	
	Пока МассивВведенныхЗначений.Количество() > 15 Цикл
		МассивВведенныхЗначений.Удалить(МассивВведенныхЗначений.ВГраница());
	КонецЦикла;
	
	СохранитьНастройкуСистемногоХранилища(Идентификатор, МассивВведенныхЗначений, "МеханизмСохраненияПоследнихВыбранныхЗначений");
	
КонецПроцедуры

// Возвращает список ранее введенных значений. В качестве ссылки на пользователя
// берется значение ПараметрСеанса.ТекущийПользователь
// 
// Параметры:
// Идентификатор - Строка - идентификатор элемента, по которому необходимо вернуть
//                 список вводимых значений
// Возвращаемое значение:
// СписокЗначений - список из значений истории ввода
//
Функция ПолучитьСписокЗначенийИсторииВвода(Идентификатор)
	
	МассивВведенныхЗначений = ПолучитьНастройкуСистемногоХранилища(Идентификатор,"МеханизмСохраненияПоследнихВыбранныхЗначений");
	
	Если МассивВведенныхЗначений = Неопределено Тогда
		МассивВведенныхЗначений = Новый Массив;
	КонецЕсли;
	
	СписокИстории = Новый СписокЗначений;
	
	Для Индекс = 0 по МассивВведенныхЗначений.ВГраница() Цикл
		
		Ссылка = МассивВведенныхЗначений.Получить(Индекс);
		
		НужноДобавить = Истина;
		Если ТипЗнч(Ссылка) = Тип("СправочникСсылка.Пользователи") Тогда
			НужноДобавить = Не ОбщегоНазначения.ПолучитьЗначениеРеквизита(Ссылка, "Недействителен");
		КонецЕсли;	
		
		Если НужноДобавить Тогда
			СписокИстории.Добавить(Ссылка);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат СписокИстории;
	
КонецФункции

// Сохраняет настройку в системное хранилище
//
Функция СохранитьНастройкуСистемногоХранилища(знач КлючОбъекта,
                                              знач Настройка,
                                              знач КлючНастроек = Неопределено,
                                              знач ОписаниеНастроек = Неопределено,
                                              знач ИдентификаторПользователя = Неопределено) Экспорт
	
	ХранилищеСистемныхНастроек.Сохранить(КлючОбъекта, КлючНастроек, Настройка, ОписаниеНастроек, ИдентификаторПользователя);
	
КонецФункции

// Получает настройку из системного хранилища
//
Функция ПолучитьНастройкуСистемногоХранилища(знач КлючОбъекта,
                                             знач КлючНастроек = Неопределено,
                                             знач ОписаниеНастроек = Неопределено,
                                             знач ИдентификаторПользователя = Неопределено) Экспорт
											 
	Результат = ХранилищеСистемныхНастроек.Загрузить(КлючОбъекта, КлючНастроек, ОписаниеНастроек, ИдентификаторПользователя);
	
	Возврат Результат;
	
КонецФункции


// Загружает список выбора элемента формы из хранилища системных настроек
//
Процедура ЗагрузитьСписокВыбора(Форма, ИмяСписка) Экспорт
	СписокВыбора = ХранилищеСистемныхНастроек.Загрузить(Форма.ИмяФормы, ИмяСписка);
	Если ТипЗнч(СписокВыбора) = Тип("СписокЗначений") Тогда
		Форма.Элементы[ИмяСписка].СписокВыбора.ЗагрузитьЗначения(СписокВыбора.ВыгрузитьЗначения());
	КонецЕсли;
	Форма.Элементы[ИмяСписка].КнопкаСпискаВыбора = (Форма.Элементы[ИмяСписка].СписокВыбора.Количество() > 0);
КонецПроцедуры

// Созраняет список выбора элемента формы в хранилище системных настроек
//
Процедура СохранитьСписокВыбора(ИмяФормы, ИмяСписка, СписокВыбора) Экспорт
	ХранилищеСистемныхНастроек.Сохранить(ИмяФормы, ИмяСписка, СписокВыбора);
КонецПроцедуры


