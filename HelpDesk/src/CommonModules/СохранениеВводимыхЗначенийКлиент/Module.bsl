// Обновляет список выбора элемента формы и сохраняет его в хранилище системных настроек
//
Процедура ОбновитьСписокВыбора(Форма, ИмяПоляСоСпискомВыбора, ХранитьЭлементов = 20) Экспорт
	
	СписокВыбора = Форма.Элементы[ИмяПоляСоСпискомВыбора].СписокВыбора;
	Значение = Форма.Элементы[ИмяПоляСоСпискомВыбора].ТекстРедактирования;
	Если Не ЗначениеЗаполнено(Значение) Тогда
		Возврат;
	КонецЕсли;
	ЭлементСпискаЗначений = СписокВыбора.НайтиПоЗначению(Значение);
	Если ЭлементСпискаЗначений <> Неопределено Тогда
		СписокВыбора.Удалить(ЭлементСпискаЗначений);
	КонецЕсли;
	СписокВыбора.Вставить(0, Значение);
	КоличествоСтрок = СписокВыбора.Количество();
	Пока КоличествоСтрок > ХранитьЭлементов Цикл
		СписокВыбора.Удалить(КоличествоСтрок - 1);
		КоличествоСтрок = КоличествоСтрок - 1;
	КонецЦикла;
	СохранениеВводимыхЗначений.СохранитьСписокВыбора(Форма.ИмяФормы, ИмяПоляСоСпискомВыбора, СписокВыбора);
	Форма.Элементы[ИмяПоляСоСпискомВыбора].КнопкаСпискаВыбора = (Форма.Элементы[ИмяПоляСоСпискомВыбора].СписокВыбора.Количество() > 0);
	
КонецПроцедуры

// Очищает список выбора элемента формы и сохраняет его в хранилище системных настроек
//
Процедура ОчиститьСписокВыбора(Форма, ИмяПоляСоСпискомВыбора) Экспорт
	
	СписокВыбора = Форма.Элементы[ИмяПоляСоСпискомВыбора].СписокВыбора;
	СписокВыбора.Очистить();
	СохранениеВводимыхЗначений.СохранитьСписокВыбора(Форма.ИмяФормы, ИмяПоляСоСпискомВыбора, СписокВыбора);
	Форма.Элементы[ИмяПоляСоСпискомВыбора].КнопкаСпискаВыбора = (Форма.Элементы[ИмяПоляСоСпискомВыбора].СписокВыбора.Количество() > 0);
	
КонецПроцедуры
