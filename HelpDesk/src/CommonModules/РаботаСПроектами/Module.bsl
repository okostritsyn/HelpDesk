
// Заполняет коды СДР всех задач проекта 
Процедура ЗаполнитьКодыСДРПроектныхЗадач(Проект) Экспорт
	
	// Коды СДР обычных задач
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПроектныеЗадачи.Ссылка КАК Ссылка,
		|	ПроектныеЗадачи.НомерЗадачиВУровне КАК НомерЗадачиВУровне
		|ИЗ
		|	Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
		|ГДЕ
		|	ПроектныеЗадачи.Родитель = ЗНАЧЕНИЕ(Справочник.ПроектныеЗадачи.ПустаяСсылка)
		|	И ПроектныеЗадачи.Владелец = &Проект
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерЗадачиВУровне";
	Запрос.УстановитьПараметр("Проект", Проект);
	РезультатВыполнения = Запрос.Выполнить();	
	Если РезультатВыполнения.Пустой() Тогда
		Возврат;
	КонецЕсли;
	Выборка = РезультатВыполнения.Выбрать();
	Счетчик = 1;
	Пока Выборка.Следующий() Цикл
		ЗаблокироватьДанныеДляРедактирования(Выборка.Ссылка);
		ЗадачаОбъект = Выборка.Ссылка.ПолучитьОбъект();
		Если Не ЗадачаОбъект.ПометкаУдаления Тогда
			ЗадачаОбъект.КодСДР = Строка(Счетчик);
			ЗадачаОбъект.НомерЗадачиВУровне = Счетчик;
			Счетчик = Счетчик + 1;
		Иначе
			ЗадачаОбъект.КодСДР = "";
			ЗадачаОбъект.НомерЗадачиВУровне = 0;
		КонецЕсли;
		ЗадачаОбъект.Записать();
		ЗаполнитьКодыСДРПодчиненныхЗадач(ЗадачаОбъект.Ссылка);
	КонецЦикла;

КонецПроцедуры

// Заполняет коды СДР подчиненных задач 
Процедура ЗаполнитьКодыСДРПодчиненныхЗадач(РодительСсылка, ИзмененныеЗадачи = Неопределено) Экспорт
	
	Если ИзмененныеЗадачи = Неопределено Тогда 
		ИзмененныеЗадачи = Новый Массив;
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПроектныеЗадачи.Ссылка КАК Ссылка,
		|	ПроектныеЗадачи.НомерЗадачиВУровне КАК НомерЗадачиВУровне
		|ИЗ
		|	Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
		|ГДЕ
		|	ПроектныеЗадачи.Родитель = &РодительСсылка
		|	И ПроектныеЗадачи.Владелец = &Проект
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерЗадачиВУровне";
	Запрос.УстановитьПараметр("РодительСсылка", РодительСсылка);
	Запрос.УстановитьПараметр("Проект", РодительСсылка.Владелец);
	
	КодРодителя = РодительСсылка.КодСДР;
	
	РезультатВыполнения = Запрос.Выполнить();	
	Если РезультатВыполнения.Пустой() Тогда
		Возврат;
	КонецЕсли;
	Выборка = РезультатВыполнения.Выбрать();
	Счетчик = 1;
	Пока Выборка.Следующий() Цикл
		ЗадачаОбъект = Выборка.Ссылка.ПолучитьОбъект();
		Если НЕ ЗадачаОбъект.ПометкаУдаления Тогда
			Если Не ЗначениеЗаполнено(КодРодителя) Тогда
				ЗадачаОбъект.КодСДР = Строка(Счетчик);
			Иначе
				ЗадачаОбъект.КодСДР = КодРодителя + "." + Строка(Счетчик);
			КонецЕсли;
			ЗадачаОбъект.НомерЗадачиВУровне = Счетчик;
			Счетчик = Счетчик + 1;
		Иначе
			ЗадачаОбъект.КодСДР = "";
			ЗадачаОбъект.НомерЗадачиВУровне = 0;
		КонецЕсли;
		ЗадачаОбъект.Записать();
		ИзмененныеЗадачи.Добавить(ЗадачаОбъект.Ссылка);
		
		ЗаполнитьКодыСДРПодчиненныхЗадач(ЗадачаОбъект.Ссылка, ИзмененныеЗадачи);
	КонецЦикла;	
	
КонецПроцедуры

// Возвращает максимальный номер проектной задачи в пределах родителя 
Функция ПолучитьМаксимальныйНомерЗадачиУровня(Проект, РодительСсылка) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЕСТЬNULL(МАКСИМУМ(ПроектныеЗадачи.НомерЗадачиВУровне), 0) КАК НомерЗадачиВУровне
		|ИЗ
		|	Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
		|ГДЕ
		|	ПроектныеЗадачи.Родитель = &РодительСсылка
		|	И ПроектныеЗадачи.Владелец = &Проект";
	Запрос.УстановитьПараметр("РодительСсылка", РодительСсылка);	
	Запрос.УстановитьПараметр("Проект", Проект);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.НомерЗадачиВУровне;
	КонецЕсли;
	
	Возврат 0;
	
КонецФункции

// Возвращает массив проектных задач одного уровня с переданной 
Функция ПолучитьМассивЗадачОдногоУровняСУказанной(Проект, Задача) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПроектныеЗадачи.Ссылка
		|ИЗ
		|	Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
		|ГДЕ
		|	ПроектныеЗадачи.Родитель = &Родитель
		|	И ПроектныеЗадачи.Владелец = &Проект";
		
	Запрос.УстановитьПараметр("Родитель", Задача.Родитель);
	Запрос.УстановитьПараметр("Проект", Проект);	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции

// Пересчитывает трудозатраты из одной единицы измерения в другую
Функция ПересчитатьТрудозатраты(Знач Трудозатраты, ИзЕдиницы, ВЕдиницу, ГрафикРаботы) Экспорт 
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьГрафикиРаботы") Тогда 
		КоличествоРабочихЧасовВДне = ГрафикРаботы.КоличествоРабочихЧасовВДне;
		КоличествоРабочихЧасовВНеделе = ГрафикРаботы.КоличествоРабочихЧасовВНеделе;
		КоличествоРабочихДнейВМесяце = ГрафикРаботы.КоличествоРабочихДнейВМесяце;
	Иначе
		КоличествоРабочихЧасовВДне = 24;
		КоличествоРабочихЧасовВНеделе = 168;
		КоличествоРабочихДнейВМесяце = 30;
	КонецЕсли;	
	
	ТрудозатратыСек = 0;
	
	Если ИзЕдиницы = Перечисления.ЕдиницыТрудозатрат.ЧеловекоМинута Тогда 
		ТрудозатратыСек = Трудозатраты * 60;
	ИначеЕсли ИзЕдиницы = Перечисления.ЕдиницыТрудозатрат.ЧеловекоЧас Тогда 
		ТрудозатратыСек = Трудозатраты * 3600;
	ИначеЕсли ИзЕдиницы = Перечисления.ЕдиницыТрудозатрат.ЧеловекоДень Тогда 
		ТрудозатратыСек = Трудозатраты * (3600 * КоличествоРабочихЧасовВДне);
	ИначеЕсли ИзЕдиницы = Перечисления.ЕдиницыТрудозатрат.ЧеловекоНеделя Тогда 	
		ТрудозатратыСек = Трудозатраты * (3600 * КоличествоРабочихЧасовВНеделе);
	ИначеЕсли ИзЕдиницы = Перечисления.ЕдиницыТрудозатрат.ЧеловекоМесяц Тогда 	
		ТрудозатратыСек = Трудозатраты * (3600 * КоличествоРабочихЧасовВДне * КоличествоРабочихДнейВМесяце);	
	КонецЕсли;
	
	Если ВЕдиницу = Перечисления.ЕдиницыТрудозатрат.ЧеловекоМинута Тогда 
		Трудозатраты = ТрудозатратыСек / 60;
	ИначеЕсли ВЕдиницу = Перечисления.ЕдиницыТрудозатрат.ЧеловекоЧас Тогда 
		Трудозатраты = ТрудозатратыСек / 3600;
	ИначеЕсли ВЕдиницу = Перечисления.ЕдиницыТрудозатрат.ЧеловекоДень Тогда 
		Трудозатраты = ТрудозатратыСек / (3600 * КоличествоРабочихЧасовВДне);
	ИначеЕсли ВЕдиницу = Перечисления.ЕдиницыТрудозатрат.ЧеловекоНеделя Тогда 	
		Трудозатраты = ТрудозатратыСек / (3600 * КоличествоРабочихЧасовВНеделе);
	ИначеЕсли ВЕдиницу = Перечисления.ЕдиницыТрудозатрат.ЧеловекоМесяц Тогда 	
		Трудозатраты = ТрудозатратыСек / (3600 * КоличествоРабочихЧасовВДне * КоличествоРабочихДнейВМесяце);	
	КонецЕсли;
		
	Возврат Трудозатраты;	
		
КонецФункции

// Пересчитывает длительность из одной единицы измерения в другую
Функция ПересчитатьДлительность(Знач Длительность, ИзЕдиницы, ВЕдиницу, ГрафикРаботы) Экспорт 
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьГрафикиРаботы") Тогда 
		КоличествоРабочихЧасовВДне = ГрафикРаботы.КоличествоРабочихЧасовВДне;
		КоличествоРабочихЧасовВНеделе = ГрафикРаботы.КоличествоРабочихЧасовВНеделе;
		КоличествоРабочихДнейВМесяце = ГрафикРаботы.КоличествоРабочихДнейВМесяце;
	Иначе
		КоличествоРабочихЧасовВДне = 24;
		КоличествоРабочихЧасовВНеделе = 168;
		КоличествоРабочихДнейВМесяце = 30;
	КонецЕсли;
	
	ДлительностьСек = 0;
	
	Если ИзЕдиницы = Перечисления.ЕдиницыДлительности.Минута Тогда 
		ДлительностьСек = Длительность * 60;
	ИначеЕсли ИзЕдиницы = Перечисления.ЕдиницыДлительности.Час Тогда 
		ДлительностьСек = Длительность * 3600;
	ИначеЕсли ИзЕдиницы = Перечисления.ЕдиницыДлительности.День Тогда 
		ДлительностьСек = Длительность * (3600 * КоличествоРабочихЧасовВДне);
	ИначеЕсли ИзЕдиницы = Перечисления.ЕдиницыДлительности.Неделя Тогда 	
		ДлительностьСек = Длительность * (3600 * КоличествоРабочихЧасовВНеделе);
	ИначеЕсли ИзЕдиницы = Перечисления.ЕдиницыДлительности.Месяц Тогда 	
		ДлительностьСек = Длительность * (3600 * КоличествоРабочихЧасовВДне * КоличествоРабочихДнейВМесяце);	
	КонецЕсли;
	
	Если ВЕдиницу = Перечисления.ЕдиницыДлительности.Минута Тогда 
		Длительность = ДлительностьСек / 60;
	ИначеЕсли ВЕдиницу = Перечисления.ЕдиницыДлительности.Час Тогда 
		Длительность = ДлительностьСек / 3600;
	ИначеЕсли ВЕдиницу = Перечисления.ЕдиницыДлительности.День Тогда 
		Длительность = ДлительностьСек / (3600 * КоличествоРабочихЧасовВДне);
	ИначеЕсли ВЕдиницу = Перечисления.ЕдиницыДлительности.Неделя Тогда 	
		Длительность = ДлительностьСек / (3600 * КоличествоРабочихЧасовВНеделе);
	ИначеЕсли ВЕдиницу = Перечисления.ЕдиницыДлительности.Месяц Тогда 	
		Длительность = ДлительностьСек / (3600 * КоличествоРабочихЧасовВДне * КоличествоРабочихДнейВМесяце);
	КонецЕсли;
		
	Возврат Длительность;
	
КонецФункции

// Возвращает единицу фактических трудозатрат для задачи бизнес-процесса
Функция ПолучитьЕдиницуФактическихТрудозатратЗадачиИсполнителя(Задача) Экспорт 
	
	Если ЗначениеЗаполнено(Задача.ПроектнаяЗадача) Тогда 
		Возврат Задача.ПроектнаяЗадача.ЕдиницаТрудозатратФакт;
	ИначеЕсли ЗначениеЗаполнено(Задача.Проект) Тогда 
		Возврат Задача.ПроектнаяЗадача.ЕдиницаТрудозатратЗадач;
	Иначе
		Возврат Константы.ОсновнаяЕдиницаТрудозатрат.Получить();
	КонецЕсли;	
	
КонецФункции	

// Возвращает график работы проектной задачи
Функция ПолучитьГрафикРаботыПроектнойЗадачи(ПроектнаяЗадача) Экспорт 
	
	ГрафикЗадачи = ПроектнаяЗадача.ГрафикРаботы;
	Если ЗначениеЗаполнено(ГрафикЗадачи) Тогда 
		Возврат ГрафикЗадачи;
	КонецЕсли;
		
	ГрафикПроекта = ОбщегоНазначения.ПолучитьЗначениеРеквизита(ПроектнаяЗадача.Владелец, "ГрафикРаботы");
	Если ЗначениеЗаполнено(ГрафикПроекта) Тогда 
		Возврат ГрафикПроекта;
	КонецЕсли;	
		
	Возврат Константы.ОсновнойГрафикРаботы.Получить();
	
КонецФункции

// Возвращает график работы проекта
Функция ПолучитьГрафикРаботыПроекта(Проект) Экспорт 
	
	Если ЗначениеЗаполнено(Проект.ГрафикРаботы) Тогда 
		Возврат Проект.ГрафикРаботы;
	Иначе
		Возврат Константы.ОсновнойГрафикРаботы.Получить();
	КонецЕсли;	
	
КонецФункции	

// Рассчитывает окончание проектной задачи по дате начала и длительности
Функция РассчитатьОкончаниеПериода(Знач ПроектнаяЗадача, ДатаНачала, Длительность, ЕдиницаДлительности) Экспорт 
	
	Проект = ПроектнаяЗадача.Владелец;
	Если Не ЗначениеЗаполнено(ЕдиницаДлительности) Тогда 
		ЕдиницаДлительности = Проект.ЕдиницаДлительностиЗадач;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Длительность) Тогда 
		Возврат ДатаНачала;
	КонецЕсли;	
	
	ДлительностьЧас = ПересчитатьДлительность(Длительность, ЕдиницаДлительности, Перечисления.ЕдиницыДлительности.Час, Проект.ГрафикРаботы);
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьГрафикиРаботы") Тогда 
		ГрафикРаботы = ПолучитьГрафикРаботыПроектнойЗадачи(ПроектнаяЗадача);
		ДатаОкончания = ГрафикиРаботы.ПолучитьДатуОкончанияСек(ГрафикРаботы, ДатаНачала, ДлительностьЧас * 3600); 
	Иначе
		ДатаОкончания = ДатаНачала + ДлительностьЧас * 3600;
	КонецЕсли;	
	
	Возврат ДатаОкончания;
	
КонецФункции

// Рассчитывает длительность проектной задачи по дате начала и дате окончания
Функция РассчитатьДлительностьПериода(Знач ПроектнаяЗадача, ДатаНачала, ДатаОкончания, ЕдиницаДлительности) Экспорт 

	Проект = ПроектнаяЗадача.Владелец;
	Если Не ЗначениеЗаполнено(ЕдиницаДлительности) Тогда 
		ЕдиницаДлительности = Проект.ЕдиницаДлительностиЗадач;
	КонецЕсли;	
	
	Если Не ЗначениеЗаполнено(ДатаНачала) Или Не ЗначениеЗаполнено(ДатаОкончания) Тогда 
		Возврат 0;
	КонецЕсли;
	
	Если ДатаНачала > ДатаОкончания Тогда 
		Возврат 0;
	КонецЕсли;	
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьГрафикиРаботы") Тогда 
		ГрафикРаботы = ПолучитьГрафикРаботыПроектнойЗадачи(ПроектнаяЗадача);
		ДлительностьСек = ГрафикиРаботы.ПолучитьДлительностьПериодаСек(ГрафикРаботы, ДатаНачала, ДатаОкончания); 
	Иначе
		ДлительностьСек = ДатаОкончания - ДатаНачала;
	КонецЕсли;
	
	ДлительностьЧас = ДлительностьСек / 3600;
	
	Длительность = ПересчитатьДлительность(ДлительностьЧас, Перечисления.ЕдиницыДлительности.Час, ЕдиницаДлительности, Проект.ГрафикРаботы);
	
	Возврат Длительность;
	
КонецФункции	

// Рассчитывает начало проектной задачи по дате окончания и длительности
Функция РассчитатьНачалоПериода(Знач ПроектнаяЗадача, ДатаОкончания, Длительность, ЕдиницаДлительности) Экспорт 
	
	Проект = ПроектнаяЗадача.Владелец;
	Если Не ЗначениеЗаполнено(ЕдиницаДлительности) Тогда 
		ЕдиницаДлительности = Проект.ЕдиницаДлительностиЗадач;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Длительность) Тогда 
		Возврат ДатаОкончания;
	КонецЕсли;	
	
	ДлительностьЧас = ПересчитатьДлительность(Длительность, ЕдиницаДлительности, Перечисления.ЕдиницыДлительности.Час, Проект.ГрафикРаботы);
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьГрафикиРаботы") Тогда 
		ГрафикРаботы = ПолучитьГрафикРаботыПроектнойЗадачи(ПроектнаяЗадача);
		ДатаНачала = ГрафикиРаботы.ПолучитьДатуНачалаПериодаСек(ГрафикРаботы, ДатаОкончания, ДлительностьЧас * 3600); 
	Иначе
		ДатаНачала = ДатаОкончания - ДлительностьЧас * 3600;
	КонецЕсли;	
	
	Возврат ДатаНачала;
	
КонецФункции	

// Возвращает текущий план трудозатрат, вычисленный из проектных задач
Функция ВычислитьПлановыеТрудозатратыПроекта(Проект) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ПроектныеЗадачиИсполнители.ТекущийПланТрудозатраты ЕСТЬ NULL 
		|			ТОГДА ПроектныеЗадачи.ТекущийПланТрудозатраты
		|		ИНАЧЕ ПроектныеЗадачиИсполнители.ТекущийПланТрудозатраты
		|	КОНЕЦ КАК ТекущийПланТрудозатраты,
		|	ПроектныеЗадачи.ТекущийПланЕдиницаТрудозатрат,
		|	ВЫБОР
		|		КОГДА ПроектныеЗадачи.ГрафикРаботы <> ЗНАЧЕНИЕ(Справочник.ГрафикиРаботы.ПустаяСсылка)
		|			ТОГДА ПроектныеЗадачи.ГрафикРаботы
		|		ИНАЧЕ ПроектныеЗадачи.Владелец.ГрафикРаботы
		|	КОНЕЦ КАК ГрафикРаботы
		|ИЗ
		|	Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
		|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			СУММА(Исполнители.ТекущийПланТрудозатраты) КАК ТекущийПланТрудозатраты,
		|			Исполнители.Ссылка КАК Ссылка
		|		ИЗ
		|			Справочник.ПроектныеЗадачи.Исполнители КАК Исполнители
		|		
		|		СГРУППИРОВАТЬ ПО
		|			Исполнители.Ссылка) КАК ПроектныеЗадачиИсполнители
		|		ПО (ПроектныеЗадачиИсполнители.Ссылка = ПроектныеЗадачи.Ссылка)
		|ГДЕ
		|	ПроектныеЗадачи.Владелец = &Проект
		|	И (НЕ ПроектныеЗадачи.ПометкаУдаления)";
			
	Запрос.УстановитьПараметр("Проект", Проект); 
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат 0;
	КонецЕсли;	
		
	ТекущийПланТрудозатраты = 0;
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		ТекущийПланТрудозатраты = ТекущийПланТрудозатраты +
		ПересчитатьДлительность(Выборка.ТекущийПланТрудозатраты, 
			Выборка.ТекущийПланЕдиницаТрудозатрат, 
			Проект.ЕдиницаТрудозатратЗадач, 
			Выборка.ГрафикРаботы);
	КонецЦикла;	
	
	Возврат ТекущийПланТрудозатраты;
	
КонецФункции	

// Возвращает текущий план окончания проекта, вычисленный из проектных задач
Функция ВычислитьТекущийПланОкончанияПроекта(Проект) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЕСТЬNULL(МАКСИМУМ(ПроектныеЗадачи.ТекущийПланОкончание), ДАТАВРЕМЯ(1, 1, 1)) КАК ТекущийПланОкончание
		|ИЗ
		|	Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
		|ГДЕ
		|	ПроектныеЗадачи.Владелец = &Проект
		|	И (НЕ ПроектныеЗадачи.ПометкаУдаления)";
			
	Запрос.УстановитьПараметр("Проект", Проект); 
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат '00010101';
	КонецЕсли;	
		
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	Возврат Выборка.ТекущийПланОкончание;
	
КонецФункции

// Рассчитывает план всего проекта
Процедура РассчитатьПланВсегоПроекта(Проект) Экспорт
	
	Если Проект.РедактируетсяВСтороннейСистеме Тогда
		Возврат;
	КонецЕсли;
	
	// Поиск проектных задач без предшественников
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПроектныеЗадачи.Ссылка
		|ИЗ
		|	Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи 
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПроектныеЗадачи.Предшественники КАК ПроектныеЗадачиПредшественники 
		|		ПО ПроектныеЗадачиПредшественники.Ссылка = ПроектныеЗадачи.Ссылка
		|ГДЕ
		|	ПроектныеЗадачиПредшественники.Ссылка ЕСТЬ NULL 
		|	И ПроектныеЗадачи.Владелец = &Проект";
		
	Запрос.УстановитьПараметр("Проект", Проект);		
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			РассчитатьПланПроекта(Выборка.Ссылка);
		КонецЦикла;
	КонецЕсли;	
	
КонецПроцедуры

// Рассчитывает план проекта, начиная с переданной задачи
Процедура РассчитатьПланПроекта(ПроектнаяЗадача, МассивИзмененныхЗадач = Неопределено) Экспорт 
	
	Если ПроектнаяЗадача.Владелец.РедактируетсяВСтороннейСистеме Тогда
		Возврат;
	КонецЕсли;

	Если МассивИзмененныхЗадач = Неопределено Тогда 
		МассивИзмененныхЗадач = Новый Массив;
	КонецЕсли;	
	
	ПересчитатьПланПоЗадаче = Истина;
	
	РассчитатьПланПроектаПоЗадаче(ПроектнаяЗадача, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач);	
		
КонецПроцедуры

// Рекурсивная процедура расчета плана проекта 
Процедура РассчитатьПланПроектаПоЗадаче(ПроектнаяЗадача, Знач ПересчитатьПланПоЗадаче = Ложь, МассивИзмененныхЗадач = Неопределено) Экспорт 
	
	Если ПроектнаяЗадача.Владелец.РедактируетсяВСтороннейСистеме Тогда
		Возврат;
	КонецЕсли;
	
	Если ПересчитатьПланПоЗадаче Тогда 
		Если МассивИзмененныхЗадач <> Неопределено Тогда 
			МассивИзмененныхЗадач.Добавить(ПроектнаяЗадача);
		КонецЕсли;
	КонецЕсли;	
	
    // установка даты начала
	Если ПроектнаяЗадача.СуммарнаяЗадача Тогда 
		ПодчиненныеЗадачи = ПолучитьПодчиненныеЗадачи(ПроектнаяЗадача);
		
		// фактические даты
		ДатаНачала = '99990101';
		ДатаОкончания = '00010101';
		
		Для Каждого Строка из ПодчиненныеЗадачи Цикл
			Если ЗначениеЗаполнено(Строка.НачалоФакт) И Строка.НачалоФакт < ДатаНачала Тогда 
				ДатаНачала = Строка.НачалоФакт;
			КонецЕсли;	
		КонецЦикла;	
		Если ДатаНачала = '99990101' Тогда 
			ДатаНачала = '00010101'
		КонецЕсли;	
		
		Для Каждого Строка из ПодчиненныеЗадачи Цикл	
			Если Не ЗначениеЗаполнено(Строка.ОкончаниеФакт) Тогда 
				ДатаОкончания = '00010101';
				Прервать;
			ИначеЕсли Строка.ОкончаниеФакт > ДатаОкончания Тогда 	
				ДатаОкончания = Строка.ОкончаниеФакт;
			КонецЕсли;	
		КонецЦикла;
		
		Если (ДатаНачала <> ПроектнаяЗадача.НачалоФакт Или ДатаОкончания <> ПроектнаяЗадача.ОкончаниеФакт) Тогда 
			
			ЗадачаОбъект = ПроектнаяЗадача.ПолучитьОбъект();
			ЗадачаОбъект.НачалоФакт = ДатаНачала;
			ЗадачаОбъект.ОкончаниеФакт = ДатаОкончания;
			Если ЗначениеЗаполнено(ДатаНачала) И ЗначениеЗаполнено(ДатаОкончания) Тогда 
				ЗадачаОбъект.ДлительностьФакт = РаботаСПроектами.РассчитатьДлительностьПериода(ЗадачаОбъект, 
					ЗадачаОбъект.НачалоФакт, 
					ЗадачаОбъект.ОкончаниеФакт, 
					ЗадачаОбъект.ТекущийПланЕдиницаДлительности);
			КонецЕсли;	
			ЗадачаОбъект.Записать();
			
			Если МассивИзмененныхЗадач <> Неопределено Тогда 
				МассивИзмененныхЗадач.Добавить(ЗадачаОбъект.Ссылка);
			КонецЕсли;	
			
		КонецЕсли;	
			
		// плановые даты
		ДатаНачала = '99990101';
		ДатаОкончания = '00010101';
		
		Для Каждого Строка из ПодчиненныеЗадачи Цикл
			Если Строка.ТекущийПланНачало < ДатаНачала Тогда 
				ДатаНачала = Строка.ТекущийПланНачало;
			КонецЕсли;	
			
			Если Строка.ТекущийПланОкончание > ДатаОкончания Тогда 
				ДатаОкончания = Строка.ТекущийПланОкончание;
			КонецЕсли;
		КонецЦикла;	
		
		Если (ДатаНачала <> ПроектнаяЗадача.ТекущийПланНачало Или ДатаОкончания <> ПроектнаяЗадача.ТекущийПланОкончание) Тогда 
			
			ЗадачаОбъект = ПроектнаяЗадача.ПолучитьОбъект();
			ЗадачаОбъект.ТекущийПланНачало = ДатаНачала;
			ЗадачаОбъект.ТекущийПланОкончание = ДатаОкончания;
			ЗадачаОбъект.ТекущийПланДлительность = РаботаСПроектами.РассчитатьДлительностьПериода(ЗадачаОбъект, 
				ЗадачаОбъект.ТекущийПланНачало, 
				ЗадачаОбъект.ТекущийПланОкончание, 
				ЗадачаОбъект.ТекущийПланЕдиницаДлительности);
			ЗадачаОбъект.Записать();
			
			Если МассивИзмененныхЗадач <> Неопределено Тогда 
				МассивИзмененныхЗадач.Добавить(ЗадачаОбъект.Ссылка);
			КонецЕсли;
			
			ПересчитатьПланПоЗадаче = Истина;
			
		КонецЕсли;	
		
		Если ПересчитатьПланПоЗадаче Тогда
		
			Если ЗначениеЗаполнено(ПроектнаяЗадача.Родитель) Тогда 
				РассчитатьПланПроектаПоЗадаче(ПроектнаяЗадача.Родитель,,МассивИзмененныхЗадач);
			КонецЕсли;
		
			Последователи = ПолучитьВсехПоследователей(ПроектнаяЗадача);
			Для Каждого Строка Из Последователи Цикл
				РассчитатьПланПроектаПоЗадаче(Строка.Ссылка,,МассивИзмененныхЗадач);
			КонецЦикла;	
			
		КонецЕсли;		
			
	Иначе
		
		Если ПроектнаяЗадача.ТипОграничения = Перечисления.ТипыОграниченийПроектныхЗадач.ФиксированноеНачало Тогда 
			
			ОбработатьЗадачуФиксированноеНачало(ПроектнаяЗадача, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач);
			
		ИначеЕсли ПроектнаяЗадача.ТипОграничения = Перечисления.ТипыОграниченийПроектныхЗадач.ФиксированноеОкончание Тогда 	
			
			ОбработатьЗадачуФиксированноеОкончание(ПроектнаяЗадача, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач);
			
		ИначеЕсли ПроектнаяЗадача.ТипОграничения = Перечисления.ТипыОграниченийПроектныхЗадач.КакМожноРаньше Тогда 
			
			ОбработатьЗадачуКакМожноРаньше(ПроектнаяЗадача, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач);
			
		ИначеЕсли ПроектнаяЗадача.ТипОграничения = Перечисления.ТипыОграниченийПроектныхЗадач.КакМожноПозже Тогда 
			
			ОбработатьЗадачуКакМожноПозже(ПроектнаяЗадача, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач);
			
		ИначеЕсли ПроектнаяЗадача.ТипОграничения = Перечисления.ТипыОграниченийПроектныхЗадач.НачалоНеРанее Тогда 
			
			ОбработатьЗадачуНачалоНеРанее(ПроектнаяЗадача, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач);
			
		ИначеЕсли ПроектнаяЗадача.ТипОграничения = Перечисления.ТипыОграниченийПроектныхЗадач.НачалоНеПозднее Тогда 
			
			ОбработатьЗадачуНачалоНеПозднее(ПроектнаяЗадача, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач);
			
		ИначеЕсли ПроектнаяЗадача.ТипОграничения = Перечисления.ТипыОграниченийПроектныхЗадач.ОкончаниеНеРанее Тогда 
			
			ОбработатьЗадачуОкончаниеНеРанее(ПроектнаяЗадача, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач);
			
		ИначеЕсли ПроектнаяЗадача.ТипОграничения = Перечисления.ТипыОграниченийПроектныхЗадач.ОкончаниеНеПозднее Тогда 	
			
			ОбработатьЗадачуОкончаниеНеПозднее(ПроектнаяЗадача, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач);
			
		ИначеЕсли ПроектнаяЗадача.ТипОграничения = Перечисления.ТипыОграниченийПроектныхЗадач.НеПрименять Тогда 	
			
			ОбработатьЗадачуВручную(ПроектнаяЗадача, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач);
	
		КонецЕсли;	
			
	КонецЕсли;
	
КонецПроцедуры	


Процедура ОбработатьЗадачуКакМожноРаньше(ПроектнаяЗадача, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач, НачалоНеРанее = '00010101', НачалоНеПозднее = '99990101')
	
	ВсеПредшественники = ПолучитьВсехПредшественников(ПроектнаяЗадача);
	
	ТекущийПланНачало = НачалоНеРанее;
	
	Для Каждого Строка из ВсеПредшественники Цикл
		
		Если Строка.ТипЗависимости = Перечисления.ТипыЗависимостейПроектныхЗадач.ОкончаниеНачало Тогда 
			
			НовыйПланНачало = Строка.ТекущийПланОкончание;
			Если Строка.Задержка <> 0 Тогда 
				НовыйПланНачало = РаботаСПроектами.РассчитатьОкончаниеПериода(ПроектнаяЗадача, 
					НовыйПланНачало, 
					Строка.Задержка, 
					Строка.ЕдиницаЗадержки);
			КонецЕсли;		
			
			Если ПолучитьФункциональнуюОпцию("ИспользоватьГрафикиРаботы") И ПроектнаяЗадача.ТекущийПланДлительность <> 0 Тогда 
				
				ГрафикРаботы = ПолучитьГрафикРаботыПроектнойЗадачи(ПроектнаяЗадача);
				врНовыйПланНачало = ГрафикиРаботы.ПолучитьДатуОкончанияСек(ГрафикРаботы, НовыйПланНачало, 1); 
				Если врНовыйПланНачало-1 <> НовыйПланНачало Тогда 
					НовыйПланНачало = врНовыйПланНачало-1;
				КонецЕсли;	
				
			КонецЕсли;
			
		ИначеЕсли Строка.ТипЗависимости = Перечисления.ТипыЗависимостейПроектныхЗадач.НачалоНачало Тогда 	
			
			НовыйПланНачало = Строка.ТекущийПланНачало;
			Если Строка.Задержка <> 0 Тогда 
				НовыйПланНачало = РаботаСПроектами.РассчитатьОкончаниеПериода(ПроектнаяЗадача, 
					НовыйПланНачало, 
					Строка.Задержка, 
					Строка.ЕдиницаЗадержки);
			КонецЕсли;
			
		ИначеЕсли Строка.ТипЗависимости = Перечисления.ТипыЗависимостейПроектныхЗадач.ОкончаниеОкончание Тогда 
			
			НовыйПланНачало = РаботаСПроектами.РассчитатьНачалоПериода(ПроектнаяЗадача, 
				Строка.ТекущийПланОкончание, 
				ПроектнаяЗадача.ТекущийПланДлительность, 
				ПроектнаяЗадача.ТекущийПланЕдиницаДлительности);
			
			Если Строка.Задержка <> 0 Тогда 
				НовыйПланНачало = РаботаСПроектами.РассчитатьНачалоПериода(ПроектнаяЗадача, 
					НовыйПланНачало, 
					Строка.Задержка, 
					Строка.ЕдиницаЗадержки);
			КонецЕсли;	
			
		ИначеЕсли Строка.ТипЗависимости = Перечисления.ТипыЗависимостейПроектныхЗадач.НачалоОкончание Тогда 
			
			НовыйПланНачало = РаботаСПроектами.РассчитатьНачалоПериода(ПроектнаяЗадача, 
				Строка.ТекущийПланНачало, 
				ПроектнаяЗадача.ТекущийПланДлительность, 
				ПроектнаяЗадача.ТекущийПланЕдиницаДлительности);
			
			Если Строка.Задержка <> 0 Тогда 
				НовыйПланНачало = РаботаСПроектами.РассчитатьНачалоПериода(ПроектнаяЗадача, 
					НовыйПланНачало, 
					Строка.Задержка, 
					Строка.ЕдиницаЗадержки);
			КонецЕсли;
			
		КонецЕсли;	
		
		Если ЗначениеЗаполнено(НовыйПланНачало) Тогда 
			Если НовыйПланНачало > ТекущийПланНачало Тогда 
				ТекущийПланНачало = НовыйПланНачало;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ТекущийПланНачало > НачалоНеПозднее Тогда 
		ТекущийПланНачало = НачалоНеПозднее;
	КонецЕсли;	
	
	Если (ТекущийПланНачало <> ПроектнаяЗадача.ТекущийПланНачало) Тогда 
		
		ЗадачаОбъект = ПроектнаяЗадача.ПолучитьОбъект();
		ЗадачаОбъект.ТекущийПланНачало = ТекущийПланНачало;
		ЗадачаОбъект.ТекущийПланОкончание = РаботаСПроектами.РассчитатьОкончаниеПериода(ЗадачаОбъект, 
			ЗадачаОбъект.ТекущийПланНачало, 
			ЗадачаОбъект.ТекущийПланДлительность, 
			ЗадачаОбъект.ТекущийПланЕдиницаДлительности);
		ЗадачаОбъект.Записать();
		
		Если МассивИзмененныхЗадач <> Неопределено Тогда 
			МассивИзмененныхЗадач.Добавить(ЗадачаОбъект.Ссылка);
		КонецЕсли;
		
		ПересчитатьПланПоЗадаче = Истина;
		
	КонецЕсли;	
	
	Если ПересчитатьПланПоЗадаче Тогда 
		
		Если ЗначениеЗаполнено(ПроектнаяЗадача.Родитель) Тогда 
			РассчитатьПланПроектаПоЗадаче(ПроектнаяЗадача.Родитель, , МассивИзмененныхЗадач);
		КонецЕсли;	
		
		Последователи = ПолучитьВсехПоследователей(ПроектнаяЗадача, Ложь);
		Для Каждого Строка Из Последователи Цикл
			РассчитатьПланПроектаПоЗадаче(Строка.Ссылка, , МассивИзмененныхЗадач);
		КонецЦикла;
		
	КонецЕсли;	
	
КонецПроцедуры	

Процедура ОбработатьЗадачуКакМожноПозже(ПроектнаяЗадача, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач, ОкончаниеНеРанее = '00010101', ОкончаниеНеПозднее = '99990101')
	
	ВсеПоследователи = ПолучитьВсехПоследователей(ПроектнаяЗадача);
	
	ТекущийПланОкончание = ОкончаниеНеПозднее;
	
	Для Каждого Строка из ВсеПоследователи Цикл
		
		Если Строка.ТипЗависимости = Перечисления.ТипыЗависимостейПроектныхЗадач.ОкончаниеНачало Тогда 
			
			НовыйПланОкончание = Строка.ТекущийПланНачало;
			Если Строка.Задержка <> 0 Тогда 
				НовыйПланОкончание = РаботаСПроектами.РассчитатьНачалоПериода(ПроектнаяЗадача, 
					НовыйПланОкончание, 
					Строка.Задержка, 
					Строка.ЕдиницаЗадержки);
			КонецЕсли;		
			
			Если ПолучитьФункциональнуюОпцию("ИспользоватьГрафикиРаботы") И ПроектнаяЗадача.ТекущийПланДлительность <> 0 Тогда 
				
				ГрафикРаботы = ПолучитьГрафикРаботыПроектнойЗадачи(ПроектнаяЗадача);
				врНовыйПланОкончание = ГрафикиРаботы.ПолучитьДатуНачалаПериодаСек(ГрафикРаботы, НовыйПланОкончание, 1); 
				Если врНовыйПланОкончание+1 <> НовыйПланОкончание Тогда 
					НовыйПланОкончание = врНовыйПланОкончание+1;
				КонецЕсли;	
				
			КонецЕсли;
			
			
		ИначеЕсли Строка.ТипЗависимости = Перечисления.ТипыЗависимостейПроектныхЗадач.НачалоНачало Тогда 	
			
			НовыйПланОкончание = РаботаСПроектами.РассчитатьОкончаниеПериода(ПроектнаяЗадача, 
				Строка.ТекущийПланНачало,
				ПроектнаяЗадача.ТекущийПланДлительность, 
				ПроектнаяЗадача.ТекущийПланЕдиницаДлительности);
			
			Если Строка.Задержка <> 0 Тогда 
				НовыйПланОкончание = РаботаСПроектами.РассчитатьНачалоПериода(ПроектнаяЗадача, 
					НовыйПланОкончание, 
					Строка.Задержка, 
					Строка.ЕдиницаЗадержки);
			КонецЕсли;
			
		ИначеЕсли Строка.ТипЗависимости = Перечисления.ТипыЗависимостейПроектныхЗадач.ОкончаниеОкончание Тогда 
			
			НовыйПланОкончание = ПроектнаяЗадача.ТекущийПланОкончание;
			
			Если Строка.Задержка <> 0 Тогда 
				НовыйПланОкончание = РаботаСПроектами.РассчитатьНачалоПериода(ПроектнаяЗадача, 
					НовыйПланОкончание, 
					Строка.Задержка, 
					Строка.ЕдиницаЗадержки);
			КонецЕсли;	
			
		ИначеЕсли Строка.ТипЗависимости = Перечисления.ТипыЗависимостейПроектныхЗадач.НачалоОкончание Тогда 
			
			НовыйПланОкончание = РаботаСПроектами.РассчитатьОкончаниеПериода(ПроектнаяЗадача, 
				Строка.ТекущийПланОкончание, 
				ПроектнаяЗадача.ТекущийПланДлительность, 
				ПроектнаяЗадача.ЕдиницаДлительности);
			
			Если Строка.Задержка <> 0 Тогда 
				НовыйПланОкончание = РаботаСПроектами.РассчитатьНачалоПериода(ПроектнаяЗадача, 
					НовыйПланОкончание, 
					Строка.Задержка, 
					Строка.ЕдиницаЗадержки);
			КонецЕсли;
			
			
		КонецЕсли;	
		
		Если ЗначениеЗаполнено(НовыйПланОкончание) Тогда 
			Если НовыйПланОкончание < ТекущийПланОкончание Тогда 
				ТекущийПланОкончание = НовыйПланОкончание;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ТекущийПланОкончание < ОкончаниеНеРанее Тогда 
		ТекущийПланОкончание = ОкончаниеНеРанее;
	КонецЕсли;
	
	Если (ТекущийПланОкончание <> ПроектнаяЗадача.ТекущийПланОкончание) Тогда 
		
		ЗадачаОбъект = ПроектнаяЗадача.ПолучитьОбъект();
		ЗадачаОбъект.ТекущийПланОкончание = ТекущийПланОкончание;
		ЗадачаОбъект.ТекущийПланНачало = РаботаСПроектами.РассчитатьНачалоПериода(ЗадачаОбъект, 
			ЗадачаОбъект.ТекущийПланОкончание, 
			ЗадачаОбъект.ТекущийПланДлительность, 
			ЗадачаОбъект.ТекущийПланЕдиницаДлительности);
		ЗадачаОбъект.Записать();
		
		Если МассивИзмененныхЗадач <> Неопределено Тогда 
			МассивИзмененныхЗадач.Добавить(ЗадачаОбъект.Ссылка);
		КонецЕсли;
		
		ПересчитатьПланПоЗадаче = Истина;
		
	КонецЕсли;	
	
	Если ПересчитатьПланПоЗадаче Тогда 
		
		Если ЗначениеЗаполнено(ПроектнаяЗадача.Родитель) Тогда 
			РассчитатьПланПроектаПоЗадаче(ПроектнаяЗадача.Родитель, , МассивИзмененныхЗадач);
		КонецЕсли;	
		
		Предшественники = ПолучитьВсехПредшественников(ПроектнаяЗадача, Ложь);
		Для Каждого Строка Из Предшественники Цикл
			РассчитатьПланПроектаПоЗадаче(Строка.Предшественник, , МассивИзмененныхЗадач);
		КонецЦикла;
		
	КонецЕсли;	
	
	
КонецПроцедуры	

Процедура ОбработатьЗадачуФиксированноеНачало(ПроектнаяЗадача, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач)
	
	ТекущийПланНачало = ПроектнаяЗадача.ДатаОграничения;
	
	Если (ТекущийПланНачало <> ПроектнаяЗадача.ТекущийПланНачало) Тогда 
		
		ЗадачаОбъект = ПроектнаяЗадача.ПолучитьОбъект();
		ЗадачаОбъект.ТекущийПланНачало = ТекущийПланНачало;
		ЗадачаОбъект.ТекущийПланОкончание = РаботаСПроектами.РассчитатьОкончаниеПериода(ЗадачаОбъект, 
			ЗадачаОбъект.ТекущийПланНачало, 
			ЗадачаОбъект.ТекущийПланДлительность, 
			ЗадачаОбъект.ТекущийПланЕдиницаДлительности);
		ЗадачаОбъект.Записать();
		
		Если МассивИзмененныхЗадач <> Неопределено Тогда 
			МассивИзмененныхЗадач.Добавить(ЗадачаОбъект.Ссылка);
		КонецЕсли;
		
		ПересчитатьПланПоЗадаче = Истина;
		
	КонецЕсли;	
	
	Если ПересчитатьПланПоЗадаче Тогда 
		
		Если ЗначениеЗаполнено(ПроектнаяЗадача.Родитель) Тогда 
			РассчитатьПланПроектаПоЗадаче(ПроектнаяЗадача.Родитель, , МассивИзмененныхЗадач);
		КонецЕсли;	
		
		Предшественники = ПолучитьВсехПредшественников(ПроектнаяЗадача, Ложь);
		Для Каждого Строка Из Предшественники Цикл
			РассчитатьПланПроектаПоЗадаче(Строка.Предшественник, , МассивИзмененныхЗадач);
		КонецЦикла;
		
		Последователи = ПолучитьВсехПоследователей(ПроектнаяЗадача, Ложь);
		Для Каждого Строка Из Последователи Цикл
			РассчитатьПланПроектаПоЗадаче(Строка.Ссылка, , МассивИзмененныхЗадач);
		КонецЦикла;
		
	КонецЕсли;	
	
КонецПроцедуры	

Процедура ОбработатьЗадачуФиксированноеОкончание(ПроектнаяЗадача, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач)
	
	ТекущийПланОкончание = ПроектнаяЗадача.ДатаОграничения;
	
	Если (ТекущийПланОкончание <> ПроектнаяЗадача.ТекущийПланОкончание) Тогда 
		
		ЗадачаОбъект = ПроектнаяЗадача.ПолучитьОбъект();
		ЗадачаОбъект.ТекущийПланОкончание = ТекущийПланОкончание;
		ЗадачаОбъект.ТекущийПланНачало = РаботаСПроектами.РассчитатьНачалоПериода(ЗадачаОбъект, 
			ЗадачаОбъект.ТекущийПланОкончание, 
			ЗадачаОбъект.ТекущийПланДлительность, 
			ЗадачаОбъект.ТекущийПланЕдиницаДлительности);
		ЗадачаОбъект.Записать();
		
		Если МассивИзмененныхЗадач <> Неопределено Тогда 
			МассивИзмененныхЗадач.Добавить(ЗадачаОбъект.Ссылка);
		КонецЕсли;
		
		ПересчитатьПланПоЗадаче = Истина;
		
	КонецЕсли;	
	
	Если ПересчитатьПланПоЗадаче Тогда 
		
		Если ЗначениеЗаполнено(ПроектнаяЗадача.Родитель) Тогда 
			РассчитатьПланПроектаПоЗадаче(ПроектнаяЗадача.Родитель, , МассивИзмененныхЗадач);
		КонецЕсли;	
		
		Предшественники = ПолучитьВсехПредшественников(ПроектнаяЗадача, Ложь);
		Для Каждого Строка Из Предшественники Цикл
			РассчитатьПланПроектаПоЗадаче(Строка.Предшественник, , МассивИзмененныхЗадач);
		КонецЦикла;
		
		Последователи = ПолучитьВсехПоследователей(ПроектнаяЗадача, Ложь);
		Для Каждого Строка Из Последователи Цикл
			РассчитатьПланПроектаПоЗадаче(Строка.Ссылка, , МассивИзмененныхЗадач);
		КонецЦикла;
		
	КонецЕсли;	
	
КонецПроцедуры	

Процедура ОбработатьЗадачуНачалоНеРанее(ПроектнаяЗадача, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач)
	
	НачалоНеРанее = ПроектнаяЗадача.ДатаОграничения;
	
	Если ПроектнаяЗадача.Владелец.СпособПланирования = Перечисления.СпособыПланированияПроекта.ОтДатыНачалаПроекта Тогда 
		ОбработатьЗадачуКакМожноРаньше(ПроектнаяЗадача, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач, НачалоНеРанее);
	Иначе
		ОкончаниеНеРанее = РаботаСПроектами.РассчитатьОкончаниеПериода(ПроектнаяЗадача, 
			НачалоНеРанее, 
			ПроектнаяЗадача.ТекущийПланДлительность, 
			ПроектнаяЗадача.ТекущийПланЕдиницаДлительности);
		
		ОбработатьЗадачуКакМожноПозже(ПроектнаяЗадача, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач, ОкончаниеНеРанее);
	КонецЕсли;	
	
КонецПроцедуры	

Процедура ОбработатьЗадачуОкончаниеНеРанее(ПроектнаяЗадача, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач)
	
	ОкончаниеНеРанее = ПроектнаяЗадача.ДатаОграничения;
	
	Если ПроектнаяЗадача.Владелец.СпособПланирования = Перечисления.СпособыПланированияПроекта.ОтДатыНачалаПроекта Тогда 
		
		НачалоНеРанее = РаботаСПроектами.РассчитатьНачалоПериода(ПроектнаяЗадача, 
			ОкончаниеНеРанее, 
			ПроектнаяЗадача.ТекущийПланДлительность, 
			ПроектнаяЗадача.ТекущийПланЕдиницаДлительности);
		
		ОбработатьЗадачуКакМожноРаньше(ПроектнаяЗадача, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач, НачалоНеРанее);
	Иначе
		
		ОбработатьЗадачуКакМожноПозже(ПроектнаяЗадача, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач, ОкончаниеНеРанее);
	КонецЕсли;	
	
КонецПроцедуры	

Процедура ОбработатьЗадачуНачалоНеПозднее(ПроектнаяЗадача, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач)
	
	НачалоНеПозднее = ПроектнаяЗадача.ДатаОграничения;
	
	Если ПроектнаяЗадача.Владелец.СпособПланирования = Перечисления.СпособыПланированияПроекта.ОтДатыНачалаПроекта Тогда 
		ОбработатьЗадачуКакМожноРаньше(ПроектнаяЗадача, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач, , НачалоНеПозднее);
	Иначе
		ОкончаниеНеПозднее = РаботаСПроектами.РассчитатьОкончаниеПериода(ПроектнаяЗадача, 
			НачалоНеПозднее, 
			ПроектнаяЗадача.ТекущийПланДлительность, 
			ПроектнаяЗадача.ТекущийПланЕдиницаДлительности);
		
		ОбработатьЗадачуКакМожноПозже(ПроектнаяЗадача, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач, , ОкончаниеНеПозднее);
	КонецЕсли;	
	
КонецПроцедуры

Процедура ОбработатьЗадачуОкончаниеНеПозднее(ПроектнаяЗадача, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач)
	
	ОкончаниеНеПозднее = ПроектнаяЗадача.ДатаОграничения;
	
	Если ПроектнаяЗадача.Владелец.СпособПланирования = Перечисления.СпособыПланированияПроекта.ОтДатыНачалаПроекта Тогда 
		НачалоНеПозднее = РаботаСПроектами.РассчитатьНачалоПериода(ПроектнаяЗадача, 
			ОкончаниеНеПозднее, 
			ПроектнаяЗадача.ТекущийПланДлительность, 
			ПроектнаяЗадача.ТекущийПланЕдиницаДлительности);
		
		ОбработатьЗадачуКакМожноРаньше(ПроектнаяЗадача, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач, , НачалоНеПозднее);
	Иначе
		ОбработатьЗадачуКакМожноПозже(ПроектнаяЗадача, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач, , ОкончаниеНеПозднее);
	КонецЕсли;	
	
КонецПроцедуры

Процедура ОбработатьЗадачуВручную(ПроектнаяЗадача, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач)
	
	ОкончаниеНеПозднее = ОбщегоНазначения.ПолучитьЗначениеРеквизита(ПроектнаяЗадача.Владелец, "ТекущийПланОкончание");
	Начало = ПроектнаяЗадача.ТекущийПланНачало;
	Если ПроектнаяЗадача.Владелец.СпособПланирования = Перечисления.СпособыПланированияПроекта.ОтДатыНачалаПроекта Тогда 
		НачалоНеПозднее = РаботаСПроектами.РассчитатьНачалоПериода(ПроектнаяЗадача, 
			ОкончаниеНеПозднее, 
			ПроектнаяЗадача.ТекущийПланДлительность, 
			ПроектнаяЗадача.ТекущийПланЕдиницаДлительности);
		
		ОбработатьЗадачуКакМожноРаньше(ПроектнаяЗадача, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач, Начало , НачалоНеПозднее);
	Иначе
		ОбработатьЗадачуКакМожноПозже(ПроектнаяЗадача, ПересчитатьПланПоЗадаче, МассивИзмененныхЗадач, Начало , ОкончаниеНеПозднее);
	КонецЕсли;	
	
КонецПроцедуры

// Возвращает Истина, если переданная задача является суммарной
Функция ЭтоСуммарнаяЗадача(ПроектнаяЗадача) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИСТИНА
	|ИЗ
	|	Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
	|ГДЕ
	|	ПроектныеЗадачи.Родитель = &ПроектнаяЗадача
	|	И Не ПроектныеЗадачи.ПометкаУдаления";
				   
	Запрос.УстановитьПараметр("ПроектнаяЗадача", ПроектнаяЗадача);
	Результат = Запрос.Выполнить();
	
	Возврат Не Результат.Пустой();
	
КонецФункции	

// Возвращает подчиненные переданной задачи без учета суммарных задач
Функция ПолучитьПодчиненныеЗадачи(ПроектнаяЗадача) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПроектныеЗадачи.Ссылка,
	|	ПроектныеЗадачи.ТекущийПланНачало,
	|	ПроектныеЗадачи.ТекущийПланОкончание,
	|	ПроектныеЗадачи.ТекущийПланТрудозатраты,
	|	ПроектныеЗадачи.ТекущийПланДлительность,
	|	ПроектныеЗадачи.ТекущийПланЕдиницаДлительности,
	|	ПроектныеЗадачи.ТекущийПланЕдиницаТрудозатрат,
	|	ПроектныеЗадачи.НачалоФакт,
	|	ПроектныеЗадачи.ОкончаниеФакт,
	|	ПроектныеЗадачи.ДлительностьФакт,
	|	ПроектныеЗадачи.ЕдиницаДлительностиФакт,
	|	ПроектныеЗадачи.ЕдиницаТрудозатратФакт
	|ИЗ
	|	Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
	|ГДЕ
	|	ПроектныеЗадачи.Родитель = &ПроектнаяЗадача";
	
	
	Запрос.УстановитьПараметр("ПроектнаяЗадача", ПроектнаяЗадача);
	Результат = Запрос.Выполнить();
	
	Возврат Результат.Выгрузить();
	
КонецФункции	

// Возвращает все подчиненные переданной задачи с учетом суммарных задач
Функция ПолучитьВсеПодчиненныеЗадачи(ПроектнаяЗадача) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПроектныеЗадачи.Ссылка,
	|	ПроектныеЗадачи.ТекущийПланНачало,
	|	ПроектныеЗадачи.ТекущийПланОкончание,
	|	ПроектныеЗадачи.ТекущийПланТрудозатраты,
	|	ПроектныеЗадачи.ТекущийПланДлительность,
	|	ПроектныеЗадачи.ТекущийПланЕдиницаДлительности,
	|	ПроектныеЗадачи.ТекущийПланЕдиницаТрудозатрат,
	|	ПроектныеЗадачи.НачалоФакт,
	|	ПроектныеЗадачи.ОкончаниеФакт,
	|	ПроектныеЗадачи.ДлительностьФакт,
	|	ПроектныеЗадачи.ЕдиницаДлительностиФакт,
	|	ПроектныеЗадачи.ЕдиницаТрудозатратФакт,
	|	ПроектныеЗадачи.СуммарнаяЗадача
	|ИЗ
	|	Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
	|ГДЕ
	|	ПроектныеЗадачи.Ссылка В ИЕРАРХИИ(&ПроектнаяЗадача)";
	
	
	Запрос.УстановитьПараметр("ПроектнаяЗадача", ПроектнаяЗадача);
	Результат = Запрос.Выполнить();
	
	Возврат Результат.Выгрузить();
	
КонецФункции

// Возвращает предшественников переданной задачи без учета суммарных задач
Функция ПолучитьПредшественников(ПроектнаяЗадача, ВключатьНачалоПроекта = Истина) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПроектныеЗадачиПредшественники.Ссылка КАК Ссылка,
	|	ПроектныеЗадачиПредшественники.ТипЗависимости,
	|	ПроектныеЗадачиПредшественники.Ссылка.ТекущийПланНачало КАК ТекущийПланНачало,
	|	ПроектныеЗадачиПредшественники.Ссылка.ТекущийПланОкончание КАК ТекущийПланОкончание,
	|	ПроектныеЗадачиПредшественники.Задержка,
	|	ПроектныеЗадачиПредшественники.ЕдиницаЗадержки
	|ИЗ
	|	Справочник.ПроектныеЗадачи.Предшественники КАК ПроектныеЗадачиПредшественники
	|ГДЕ
	|	ПроектныеЗадачиПредшественники.Ссылка = &ПроектнаяЗадача";
	
	Запрос.УстановитьПараметр("ПроектнаяЗадача", ПроектнаяЗадача);
	Результат = Запрос.Выполнить().Выгрузить();
	
	Если ВключатьНачалоПроекта Тогда 
		Если Результат.Найти(ПроектнаяЗадача, "Ссылка") = Неопределено Тогда 
			НоваяСтрока = Результат.Добавить();
			НоваяСтрока.Ссылка = ПроектнаяЗадача;
			НоваяСтрока.ТипЗависимости = Перечисления.ТипыЗависимостейПроектныхЗадач.ОкончаниеНачало;
			НоваяСтрока.ТекущийПланНачало = ПроектнаяЗадача.Владелец.ТекущийПланНачало;
			НоваяСтрока.ТекущийПланОкончание = ПроектнаяЗадача.Владелец.ТекущийПланНачало;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции	

// Возвращает всех предшественников переданной задачи с учетом суммарных задач
Функция ПолучитьВсехПредшественников(ПроектнаяЗадача, ВключатьНачалоПроекта = Истина) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	СписокЗадач = Новый СписокЗначений;
	
	ТекРодитель = ПроектнаяЗадача;
	Пока ЗначениеЗаполнено(ТекРодитель) Цикл
		СписокЗадач.Добавить(ТекРодитель);
		ТекРодитель = ТекРодитель.Родитель;
	КонецЦикла;	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПроектныеЗадачиПредшественники.Ссылка КАК Ссылка,
	|	ПроектныеЗадачиПредшественники.Предшественник КАК Предшественник,
	|	ПроектныеЗадачиПредшественники.ТипЗависимости,
	|	ПроектныеЗадачиПредшественники.Предшественник.ТекущийПланНачало КАК ТекущийПланНачало,
	|	ПроектныеЗадачиПредшественники.Предшественник.ТекущийПланОкончание КАК ТекущийПланОкончание,
	|	ПроектныеЗадачиПредшественники.Задержка,
	|	ПроектныеЗадачиПредшественники.ЕдиницаЗадержки
	|ИЗ
	|	Справочник.ПроектныеЗадачи.Предшественники КАК ПроектныеЗадачиПредшественники
	|ГДЕ
	|	ПроектныеЗадачиПредшественники.Ссылка В(&СписокЗадач)";
	
	Запрос.УстановитьПараметр("СписокЗадач", СписокЗадач);
	Результат = Запрос.Выполнить().Выгрузить();
	
	Если ВключатьНачалоПроекта Тогда 
		НачалоПроекта = ОбщегоНазначения.ПолучитьЗначениеРеквизита(ПроектнаяЗадача.Владелец, "ТекущийПланНачало");
		Для Каждого Строка Из СписокЗадач Цикл
			Если Результат.Найти(Строка.Значение, "Ссылка") = Неопределено Тогда 
				НоваяСтрока = Результат.Добавить();
				НоваяСтрока.Ссылка = Строка.Значение;
				НоваяСтрока.ТипЗависимости = Перечисления.ТипыЗависимостейПроектныхЗадач.ОкончаниеНачало;
				НоваяСтрока.ТекущийПланНачало = НачалоПроекта;
				НоваяСтрока.ТекущийПланОкончание = НачалоПроекта;
			КонецЕсли;
		КонецЦикла;	
	КонецЕсли;	
	
	Возврат Результат;
	
КонецФункции	

// Возвращает последователей переданной задачи без учета суммарных задач
Функция ПолучитьПоследователей(ПроектнаяЗадача, ВключатьОкончаниеПроекта = Истина) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	// перемещение последующих задач
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПроектныеЗадачиПредшественники.Ссылка,
		|	ПроектныеЗадачиПредшественники.ТипЗависимости,
		|	ПроектныеЗадачиПредшественники.Задержка,
		|	ПроектныеЗадачиПредшественники.ЕдиницаЗадержки,
		|	ПроектныеЗадачиПредшественники.Ссылка.ТекущийПланНачало КАК ТекущийПланНачало,
		|	ПроектныеЗадачиПредшественники.Ссылка.ТекущийПланОкончание КАК ТекущийПланОкончание
		|ИЗ
		|	Справочник.ПроектныеЗадачи.Предшественники КАК ПроектныеЗадачиПредшественники
		|ГДЕ
		|	ПроектныеЗадачиПредшественники.Предшественник = &Предшественник";
	Запрос.УстановитьПараметр("Предшественник", ПроектнаяЗадача);
	
	Результат = Запрос.Выполнить().Выгрузить();
	ВсеПоследователи = Результат.Скопировать();
	
	Если ВключатьОкончаниеПроекта Тогда 
		ОкончаниеПроекта = ОбщегоНазначения.ПолучитьЗначениеРеквизита(ПроектнаяЗадача.Владелец, "ТекущийПланОкончание");
		Если Результат.Количество() = 0 Тогда 
			НоваяСтрока = ВсеПоследователи.Добавить();
			НоваяСтрока.Ссылка = ПроектнаяЗадача;
			НоваяСтрока.ТипЗависимости = Перечисления.ТипыЗависимостейПроектныхЗадач.ОкончаниеНачало;
			НоваяСтрока.Задержка = 0;
			НоваяСтрока.ТекущийПланНачало = ОкончаниеПроекта;
			НоваяСтрока.ТекущийПланОкончание = ОкончаниеПроекта;
		КонецЕсли;	
	КонецЕсли;	
	
	Возврат ВсеПоследователи;
	
КонецФункции	

// Возвращает всех последователей переданной задачи с учетом суммарных задач
Функция ПолучитьВсехПоследователей(ПроектнаяЗадача, ВключатьОкончаниеПроекта = Истина) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	// перемещение последующих задач
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПроектныеЗадачиПредшественники.Ссылка,
		|	ПроектныеЗадачиПредшественники.Ссылка.СуммарнаяЗадача КАК СуммарнаяЗадача,
		|	ПроектныеЗадачиПредшественники.Ссылка.ТекущийПланНачало КАК ТекущийПланНачало,
		|	ПроектныеЗадачиПредшественники.Ссылка.ТекущийПланОкончание КАК ТекущийПланОкончание,
		|	ПроектныеЗадачиПредшественники.ТипЗависимости,
		|	ПроектныеЗадачиПредшественники.Задержка,
		|	ПроектныеЗадачиПредшественники.ЕдиницаЗадержки
		|ИЗ
		|	Справочник.ПроектныеЗадачи.Предшественники КАК ПроектныеЗадачиПредшественники
		|ГДЕ
		|	ПроектныеЗадачиПредшественники.Предшественник = &Предшественник";
	
	Запрос.УстановитьПараметр("Предшественник", ПроектнаяЗадача);
	Результат = Запрос.Выполнить().Выгрузить();
	
	ВсеПоследователи = Результат.Скопировать();
	ВсеПоследователи.Очистить();
	
	Для Каждого Строка Из Результат Цикл
		
		Если Строка.СуммарнаяЗадача Тогда 
			ПодчиненныеЗадачи = ПолучитьВсеПодчиненныеЗадачи(Строка.Ссылка);
			Для Каждого ПодчиненнаяЗадача Из ПодчиненныеЗадачи Цикл
				Если ПодчиненнаяЗадача.СуммарнаяЗадача Тогда 
					Продолжить;
				КонецЕсли;
				
				НоваяСтрока = ВсеПоследователи.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ПодчиненнаяЗадача);
			КонецЦикла;	
		Иначе	
			НоваяСтрока = ВсеПоследователи.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		КонецЕсли;	
		
	КонецЦикла;	
	
	Если ВключатьОкончаниеПроекта Тогда 
		
		ОкончаниеПроекта = ОбщегоНазначения.ПолучитьЗначениеРеквизита(ПроектнаяЗадача.Владелец, "ТекущийПланОкончание");
		Если ВсеПоследователи.Количество() = 0 Тогда 
			НоваяСтрока = ВсеПоследователи.Добавить();
			НоваяСтрока.Ссылка = ПроектнаяЗадача;
			НоваяСтрока.ТипЗависимости = Перечисления.ТипыЗависимостейПроектныхЗадач.ОкончаниеНачало;
			НоваяСтрока.Задержка = 0;
			НоваяСтрока.ТекущийПланНачало = ОкончаниеПроекта;
			НоваяСтрока.ТекущийПланОкончание = ОкончаниеПроекта;
		КонецЕсли;	
		
	КонецЕсли;
	
	Возврат ВсеПоследователи;
	
КонецФункции

// Выдает исключение, если по проектной задаче указаны некорректные предшественники
Процедура ПроверитьПредшественников(ПроектнаяЗадача) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	// Формируем родительские задачи
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПроектныеЗадачи.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
		|ГДЕ
		|	ПроектныеЗадачи.Ссылка = &ПроектнаяЗадача				
		|ИТОГИ ПО
		|	Ссылка ТОЛЬКО ИЕРАРХИЯ";

	Запрос.УстановитьПараметр("ПроектнаяЗадача", ПроектнаяЗадача);

	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	ПроектныеЗадачиРодители = Новый СписокЗначений();
	
	Пока Выборка.Следующий() Цикл
		Если ПроектныеЗадачиРодители.НайтиПоЗначению(Выборка.Ссылка) = Неопределено Тогда
			ПроектныеЗадачиРодители.Добавить(Выборка.Ссылка);	
		КонецЕсли;
	КонецЦикла;
	
	// Формируем подчиненные задачи
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПроектныеЗадачи.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
		|ГДЕ
		|	ПроектныеЗадачи.Ссылка В ИЕРАРХИИ(&ПроектнаяЗадача)
		|ИТОГИ ПО
		|	Ссылка ТОЛЬКО ИЕРАРХИЯ";

	Запрос.УстановитьПараметр("ПроектнаяЗадача", ПроектнаяЗадача);

	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	ПроектныеЗадачиПодчиненные = Новый СписокЗначений();

	Пока Выборка.Следующий() Цикл
		Если ПроектныеЗадачиПодчиненные.НайтиПоЗначению(Выборка.Ссылка) = Неопределено Тогда
			ПроектныеЗадачиПодчиненные.Добавить(Выборка.Ссылка);	
		КонецЕсли;
	КонецЦикла;

	// Проверка предшественников
	Для Каждого СтрокаТаблЧасти Из ПроектнаяЗадача.Предшественники Цикл
		Если СтрокаТаблЧасти.Предшественник = ПроектнаяЗадача Тогда
			ВызватьИсключение(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Проектная задача не может быть предшественником самой себя'"), ПроектнаяЗадача));	
		КонецЕсли;
		Если ПроектныеЗадачиРодители.НайтиПоЗначению(СтрокаТаблЧасти.Предшественник) <> Неопределено Тогда
			ВызватьИсключение(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Подчиненная задача %1 не может иметь в качестве предшественника одну из родительских задач'"), ПроектнаяЗадача));	
		КонецЕсли;
		Если ПроектныеЗадачиПодчиненные.НайтиПоЗначению(СтрокаТаблЧасти.Предшественник) <> Неопределено Тогда
			ВызватьИсключение(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Суммарная задача %1 не может иметь в качестве предшественника одну из подчиненных задач'"), ПроектнаяЗадача));	
		КонецЕсли; 
	КонецЦикла;
	
КонецПроцедуры

// Возвращает массив основных бизнес-процессов по проектной задачи
Функция ПолучитьОсновныеБизнесПроцессы(ПроектнаяЗадача, ТолькоНезавершенные = Ложь) Экспорт 
	
	Результат = Новый Массив;
	
	Запрос = Новый Запрос;
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	%1.Ссылка
	|ИЗ
	|	БизнесПроцесс.%1 КАК %1
	|ГДЕ
	|	%1.ПроектнаяЗадача = &ПроектнаяЗадача
	|	И Не %1.ПометкаУдаления";
	Если ТолькоНезавершенные Тогда 
		ТекстЗапроса = ТекстЗапроса + " И Не %1.Завершен ";
	КонецЕсли;	
		
	Запрос.УстановитьПараметр("ПроектнаяЗадача", ПроектнаяЗадача);
		
	Для Каждого БизнесПроцесс Из Метаданные.БизнесПроцессы Цикл
		Если БизнесПроцесс.Реквизиты.Найти("ПроектнаяЗадача") <> Неопределено Тогда
			ИмяТипаПроцесса = БизнесПроцесс.Имя;
			Запрос.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ТекстЗапроса,
				ИмяТипаПроцесса);
				
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				Результат.Добавить(Выборка.Ссылка);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции	

// Отмечает начало выполнения проектной задачи при завершении бизнес-процесса
Процедура ОтметитьНачалоВыполненияПроектнойЗадачи(БизнесПроцесс) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ПолучитьФункциональнуюОпцию("ВестиУчетПоПроектам") Тогда 
		Возврат;
	КонецЕсли;	
		
	ПроектнаяЗадача = БизнесПроцесс.ПроектнаяЗадача;
	Если Не ЗначениеЗаполнено(ПроектнаяЗадача) Тогда 
		Возврат;
	КонецЕсли;	
	
	Если ПроектнаяЗадача.СуммарнаяЗадача Тогда 
		Возврат;
	КонецЕсли;	
	
	Если Не ЗначениеЗаполнено(ПроектнаяЗадача.НачалоФакт) Тогда 
		
		ПроектнаяЗадачаОбъект = ПроектнаяЗадача.ПолучитьОбъект();
		ЗаблокироватьДанныеДляРедактирования(ПроектнаяЗадачаОбъект.Ссылка);
	
		ПроектнаяЗадачаОбъект.НачалоФакт = БизнесПроцесс.ДатаНачала;
		ПроектнаяЗадачаОбъект.Записать();
		
		Если ЗначениеЗаполнено(ПроектнаяЗадача.Родитель) Тогда 
			РассчитатьПланПроекта(ПроектнаяЗадача.Родитель);
		КонецЕсли;	
		
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(ПроектнаяЗадача.ОкончаниеФакт) Тогда 
		
		ПроектнаяЗадачаОбъект = ПроектнаяЗадача.ПолучитьОбъект();
		ЗаблокироватьДанныеДляРедактирования(ПроектнаяЗадачаОбъект.Ссылка);
	
		ПроектнаяЗадачаОбъект.ОкончаниеФакт = '00010101';
		ПроектнаяЗадачаОбъект.Записать();
		
		Если ЗначениеЗаполнено(ПроектнаяЗадача.Родитель) Тогда 
			РассчитатьПланПроекта(ПроектнаяЗадача.Родитель);
		КонецЕсли;
		
	КонецЕсли;
		
КонецПроцедуры	

// Отмечает окончание выполнения проектной задачи при завершении бизнес-процесса
Процедура ОтметитьОкончаниеВыполненияПроектнойЗадачи(БизнесПроцесс) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ПолучитьФункциональнуюОпцию("ВестиУчетПоПроектам") Тогда 
		Возврат;
	КонецЕсли;	
		
	ПроектнаяЗадача = БизнесПроцесс.ПроектнаяЗадача;
	Если Не ЗначениеЗаполнено(ПроектнаяЗадача) Тогда 
		Возврат;
	КонецЕсли;	
	
	Если ПроектнаяЗадача.СуммарнаяЗадача Тогда 
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПроектнаяЗадача.ОкончаниеФакт) Тогда 
		Возврат;
	КонецЕсли;
	
	// последний процесс
	МассивПроцессов = ПолучитьОсновныеБизнесПроцессы(ПроектнаяЗадача, Истина);
	Если Не (МассивПроцессов.Количество() = 1 И МассивПроцессов[0] = БизнесПроцесс.Ссылка) Тогда 
		Возврат;
	КонецЕсли;	
	
	ПроектнаяЗадачаОбъект = ПроектнаяЗадача.ПолучитьОбъект();
	ЗаблокироватьДанныеДляРедактирования(ПроектнаяЗадачаОбъект.Ссылка);
	
	ПроектнаяЗадачаОбъект.ОкончаниеФакт = БизнесПроцесс.ДатаЗавершения;
	ПроектнаяЗадачаОбъект.ДлительностьФакт = РаботаСПроектами.РассчитатьДлительностьПериода(ПроектнаяЗадачаОбъект, 
		ПроектнаяЗадачаОбъект.НачалоФакт, 
		ПроектнаяЗадачаОбъект.ОкончаниеФакт, 
		ПроектнаяЗадачаОбъект.ЕдиницаДлительностиФакт);
	
	ПроектнаяЗадачаОбъект.Записать();
	
	Если ЗначениеЗаполнено(ПроектнаяЗадача.Родитель) Тогда 
		РассчитатьПланПроекта(ПроектнаяЗадача.Родитель);
	КонецЕсли;
	
КонецПроцедуры	

// Возвращает Истина, если по проектной задачи есть незавершенные предшественники
Функция ЕстьНезавершенныеПредшественники(ПроектнаяЗадача) Экспорт 
	
	Если Не ПолучитьФункциональнуюОпцию("ВестиУчетПоПроектам") Тогда 
		Возврат Ложь;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИСТИНА 
		|ИЗ
		|	Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
		|ГДЕ
		|	ПроектныеЗадачи.Предшественники.Предшественник.ОкончаниеФакт = ДАТАВРЕМЯ(1, 1, 1)
		|	И ПроектныеЗадачи.Предшественники.ТипЗависимости = ЗНАЧЕНИЕ(Перечисление.ТипыЗависимостейПроектныхЗадач.ОкончаниеНачало)
		|	И ПроектныеЗадачи.Ссылка = &ПроектнаяЗадача";
	
	Запрос.УстановитьПараметр("ПроектнаяЗадача", ПроектнаяЗадача);
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда 
		Возврат Истина;
	КонецЕсли;	
	
	Возврат Ложь;
	
КонецФункции	
	
// Проверяет, что текущая дата позже, чем дата начала проекта 
Функция НаступилаДатаНачалаПроекта(Проект) Экспорт 
	
	Если Не ПолучитьФункциональнуюОпцию("ВестиУчетПоПроектам") Тогда 
		Возврат Истина;
	КонецЕсли;	
	
	Возврат (Проект.ТекущийПланНачало <= ТекущаяДата());
	
КонецФункции

// Проверяет, что текущая дата позже, чем дата начала проектной задачи 
Функция НаступилаДатаНачалаЗадачи(ПроектнаяЗадача) Экспорт 
	
	Если Не ПолучитьФункциональнуюОпцию("ВестиУчетПоПроектам") Тогда 
		Возврат Истина;
	КонецЕсли;	
	
	Возврат (ПроектнаяЗадача.ТекущийПланНачало <= ТекущаяДата());
	
КонецФункции

// Возвращает проект по умолчанию
Функция ПолучитьПроектПоУмолчанию() Экспорт
	
	ПроектПоУмолчанию = Неопределено;
	
	Если Не ПолучитьФункциональнуюОпцию("ВестиУчетПоПроектам") Тогда 
		Возврат ПроектПоУмолчанию;
	КонецЕсли;	
	
	ПроектПоУмолчанию = ХранилищеОбщихНастроек.Загрузить("НастройкиРаботыСПроектами", "ПроектПоУмолчанию");
	
	Возврат ПроектПоУмолчанию;
	
КонецФункции

// Возвращает наименьшие трудозатраты среди исполнителей проектной задачи
Функция МинимальныеТрудозатратыИсполнителей(Исполнители) Экспорт 
	
	Если Исполнители.Количество() = 0 Тогда 
		Возврат 0;
	КонецЕсли;	
	
	МинимальныеТрудозатраты = Исполнители[0].ТекущийПланТрудозатраты;
	
	Для Каждого Строка Из Исполнители Цикл
		Если Строка.ТекущийПланТрудозатраты < МинимальныеТрудозатраты Тогда 
			МинимальныеТрудозатраты = Строка.ТекущийПланТрудозатраты;
		КонецЕсли;	
	КонецЦикла;	
	
	Возврат МинимальныеТрудозатраты;
	
КонецФункции	

// Выводит в задачах бизнес-процессов плановые трудозатраты
Процедура ЗаполнитьТрудозатратыПланСтрокой(ЭтотОбъект, ТрудозатратыПлан, УчитыватьИсполнителя = Ложь) Экспорт 
	
	Если Не ПолучитьФункциональнуюОпцию("ВестиУчетПоПроектам") Тогда 
		Возврат;
	КонецЕсли;	
	
	УстановитьПривилегированныйРежим(Истина);
	
	Объект = ЭтотОбъект.Объект;
	
	
	Если ЗначениеЗаполнено(Объект.ПроектнаяЗадача) Тогда 
		ЕдиницаТрудозатрат = Строка(Объект.ПроектнаяЗадача.ТекущийПланЕдиницаТрудозатрат);
	ИначеЕсли ЗначениеЗаполнено(Объект.Проект) Тогда 
		ЕдиницаТрудозатрат = Строка(Объект.Проект.ЕдиницаТрудозатратЗадач);
	Иначе
		ЕдиницаТрудозатрат = Строка(Константы.ОсновнаяЕдиницаТрудозатрат.Получить());
	КонецЕсли;
	ЕдиницаТрудозатрат = НРег(ЕдиницаТрудозатрат);
	
	ТрудозатратыФакт = 0;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Ссылка КАК Ссылка
		|ИЗ
		|	Задача.ЗадачаИсполнителя КАК ЗадачаИсполнителя
		|ГДЕ
		|	ЗадачаИсполнителя.Ссылка <> &Ссылка
		|	И ЗадачаИсполнителя.Дата < &Дата
		|	И ЗадачаИсполнителя.БизнесПроцесс = &БизнесПроцесс
		|	И ЗадачаИсполнителя.ТочкаМаршрута = &ТочкаМаршрута";
	
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Запрос.УстановитьПараметр("Дата", Объект.Дата);
	Запрос.УстановитьПараметр("БизнесПроцесс", Объект.БизнесПроцесс);
	Запрос.УстановитьПараметр("ТочкаМаршрута", Объект.ТочкаМаршрута);
	
	Если УчитыватьИсполнителя Тогда 
		Если ЗначениеЗаполнено(Объект.Исполнитель) Тогда 
			
			Запрос.Текст = Запрос.Текст + 
				" И ЗадачаИсполнителя.Исполнитель = &Исполнитель ";
				
			Запрос.УстановитьПараметр("Исполнитель", Объект.Исполнитель);	
		Иначе
			Запрос.Текст = Запрос.Текст + 
				" И ЗадачаИсполнителя.РольИсполнителя = &РольИсполнителя
				|И ЗадачаИсполнителя.ОсновнойОбъектАдресации = &ОсновнойОбъектАдресации
				|И ЗадачаИсполнителя.ДополнительныйОбъектАдресации = &ДополнительныйОбъектАдресации ";
				
			Запрос.УстановитьПараметр("РольИсполнителя", Объект.РольИсполнителя);
			Запрос.УстановитьПараметр("ОсновнойОбъектАдресации", Объект.ОсновнойОбъектАдресации);
			Запрос.УстановитьПараметр("ДополнительныйОбъектАдресации", Объект.ДополнительныйОбъектАдресации);
		КонецЕсли;	
	КонецЕсли;	
	
	ТрудозатратыФакт = 0;
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл 
		//ТрудозатратыФакт = ТрудозатратыФакт + РаботаСБизнесПроцессами.ПолучитьФактическиеТрудозатратыПоЗадаче(Выборка.Ссылка);
		ТрудозатратыФакт = ТрудозатратыФакт ;
	КонецЦикла;
	
	ТрудозатратыПланСтр = Формат(ТрудозатратыПлан, "ЧН=; ЧГ=");
	Если ЗначениеЗаполнено(ТрудозатратыФакт) Тогда 
		ТрудозатратыПланСтр = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1 (отработано %2)'"),
			ТрудозатратыПланСтр,
			Формат(ТрудозатратыФакт, "ЧГ="));
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(ТрудозатратыПланСтр) Тогда 
		ЭтотОбъект.ТрудозатратыПлан = ТрудозатратыПланСтр + " " + ЕдиницаТрудозатрат;
	КонецЕсли;	
	
	Если ТрудозатратыПлан <= ТрудозатратыФакт И ЗначениеЗаполнено(ТрудозатратыПлан) Тогда 
		ЭтотОбъект.Элементы.ТрудозатратыПлан.ЦветТекста = ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи;
	КонецЕсли;	
	
КонецПроцедуры

// Заполняет реквизит проект у подчиненных файлов
Процедура ЗаполнитьПроектПодчиненныхФайлов(Владелец) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ВестиУчетПоПроектам") Тогда
		Возврат;
	КонецЕсли;	
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Файлы.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Файлы КАК Файлы
		|ГДЕ
		|	Файлы.ВладелецФайла = &Владелец
		|	И Файлы.Проект <> &Проект";
	
	Запрос.УстановитьПараметр("Владелец", Владелец);
	Запрос.УстановитьПараметр("Проект", Владелец.Проект);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Объект = Выборка.Ссылка.ПолучитьОбъект();
		ЗаблокироватьДанныеДляРедактирования(Объект.Ссылка);
		Объект.Проект = Владелец.Проект;
		Объект.Записать();
		РазблокироватьДанныеДляРедактирования(Объект.Ссылка);
	КонецЦикла;
	
КонецПроцедуры

// Получает сумму фактических трудозатрат по проектной задаче
Функция ПолучитьФактическиеТрудозатратыПроектнойЗадачи(
	ПроектнаяЗадача, 
	ЕдиницаТрудозатратФакт = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(ПроектнаяЗадача) Тогда 
		Возврат 0;
	КонецЕсли;	
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЕСТЬNULL(СУММА(ФактическиеТрудозатраты.Длительность), 0) КАК ФактТрудозатраты
		|ИЗ
		|	РегистрСведений.ФактическиеТрудозатраты КАК ФактическиеТрудозатраты
		|ГДЕ
		|	ФактическиеТрудозатраты.ПроектнаяЗадача = &Задача";
		
	Запрос.УстановитьПараметр("Задача", ПроектнаяЗадача);		
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		РеквизитыПроектнойЗадачи = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(
			ПроектнаяЗадача, 
			"Владелец, ГрафикРаботы, ЕдиницаТрудозатратФакт");
		ГрафикРаботы = ОбщегоНазначения.ПолучитьЗначениеРеквизита(РеквизитыПроектнойЗадачи.Владелец, "ГрафикРаботы");
		Если ЗначениеЗаполнено(РеквизитыПроектнойЗадачи.ГрафикРаботы) Тогда
			ГрафикРаботы = РеквизитыПроектнойЗадачи.ГрафикРаботы;
		КонецЕсли;
		ЕдиницаТрудозатратДляРасчета = 
			?(ЕдиницаТрудозатратФакт = Неопределено,
			РеквизитыПроектнойЗадачи.ЕдиницаТрудозатратФакт,
			ЕдиницаТрудозатратФакт);
			
		Выборка = Результат.Выбрать();
		Если Выборка.Следующий() Тогда
			ТрудозатратыФакт = ПересчитатьТрудозатраты(
				Выборка.ФактТрудозатраты / 60,
				Перечисления.ЕдиницыТрудозатрат.ЧеловекоМинута,
				ЕдиницаТрудозатратДляРасчета,
				ГрафикРаботы);
			Возврат ТрудозатратыФакт;
		КонецЕсли;
	КонецЕсли;
	
	Возврат 0;
	
КонецФункции

//АбисСофт-Кострицын Олег-Старт  17 января 2014 г.

// Получает плановые трудозатраты исполнителей проектной задачи по заявкам
// Параметры:
//	ПроектнаяЗадача - ссылка на проектную задачу
//	ЕдиницаТрудозатратФакт - единица трудозатрат, в которых считать трудозатраты.
//		Если параметр не указан, единицы берутся из проектной задачи.
// Возвращает:
//	ТаблицаЗначений, содержащая исполнителей проектной задачи и их фактические трудозатраты
//		Исполнитель - пользователь или роль исполнителей
//		ТрудозатратыПланПоЗаявкам - неотрицательное число (10.2)
Функция ПолучитьПлановыеТрудозатратыПоПроектнойЗадачи(
	ПроектнаяЗадача,
	ЕдиницаТрудозатрат = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТаблицаИсполнителей = Новый ТаблицаЗначений();
	ТипИсполнитель = Новый ОписаниеТипов("СправочникСсылка.Пользователи,СправочникСсылка.РолиИсполнителей");
	ТипВеличинаТрудозатрат = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10, 2, ДопустимыйЗнак.Неотрицательный));
	ТаблицаИсполнителей.Колонки.Добавить("Исполнитель", ТипИсполнитель);
	ТаблицаИсполнителей.Колонки.Добавить("ТрудозатратыПланПоЗаявкам", ТипВеличинаТрудозатрат);
	
	Для каждого СтрокаТаблицы из ПроектнаяЗадача.Исполнители Цикл
		НоваяСтрока = ТаблицаИсполнителей.Добавить();
		НоваяСтрока.Исполнитель = СтрокаТаблицы.Исполнитель;
		НоваяСтрока.ТрудозатратыПланПоЗаявкам = 0;
	КонецЦикла;
	
	// Получение величин для расчета трудозатрат
	РеквизитыПроектнойЗадачи = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(ПроектнаяЗадача,
		"Владелец, 
		|ГрафикРаботы, 
		|ЕдиницаТрудозатратФакт");
	РеквизитыПроекта = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(РеквизитыПроектнойЗадачи.Владелец,
		"ГрафикРаботы");
		
	ГрафикРаботы = 
		?(ЗначениеЗаполнено(РеквизитыПроектнойЗадачи.ГрафикРаботы), 
		РеквизитыПроектнойЗадачи.ГрафикРаботы, 
		РеквизитыПроекта.ГрафикРаботы);
		
	ЕдиницаТрудозатратДляРасчета = ЕдиницаТрудозатрат;
	Если ЕдиницаТрудозатрат = Неопределено Тогда
		ЕдиницаТрудозатратДляРасчета = РеквизитыПроектнойЗадачи.ЕдиницаТрудозатратФакт;
	КонецЕсли;
	
	//Выборка всех записей о трудозатратах для этой проектной задачи
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПлановыеТрудозатратыПоЗаявкамСрезПоследних.Период,
		|	ПлановыеТрудозатратыПоЗаявкамСрезПоследних.Исполнитель,
		|	ПлановыеТрудозатратыПоЗаявкамСрезПоследних.Источник,
		|	ПлановыеТрудозатратыПоЗаявкамСрезПоследних.Проект,
		|	ПлановыеТрудозатратыПоЗаявкамСрезПоследних.ПроектнаяЗадача,
		|	ПлановыеТрудозатратыПоЗаявкамСрезПоследних.Длительность,
		|	ПлановыеТрудозатратыПоЗаявкамСрезПоследних.ПричинаИзменения,
		|	ПлановыеТрудозатратыПоЗаявкамСрезПоследних.Ответственный
		|ИЗ
		|	РегистрСведений.ПлановыеТрудозатратыПоЗаявкам.СрезПоследних(&ДатаСреза, ПроектнаяЗадача = &ПроектнаяЗадача) КАК ПлановыеТрудозатратыПоЗаявкамСрезПоследних";

	Запрос.УстановитьПараметр("ДатаСреза", ТекущаяДата());
	Запрос.УстановитьПараметр("ПроектнаяЗадача", ПроектнаяЗадача);

	РезультатЗапроса = Запрос.Выполнить();

	ВыборкаЗаписей = РезультатЗапроса.Выбрать();

	Пока ВыборкаЗаписей.Следующий() Цикл
		ТрудозатратыПланПоЗаявкам = РаботаСПроектами.ПересчитатьТрудозатраты(
			ВыборкаЗаписей.Длительность / 60,
			Перечисления.ЕдиницыТрудозатрат.ЧеловекоМинута,
			ЕдиницаТрудозатратДляРасчета,
			ГрафикРаботы);
			
		ТрудозатратыПрисвоены = Ложь;
		
		// Поиск роли, которой приписать трудозатраты
		Для Каждого Участник Из ТаблицаИсполнителей Цикл
			Если Участник.Исполнитель = ВыборкаЗаписей.Исполнитель Тогда 
				Участник.ТрудозатратыПланПоЗаявкам = Участник.ТрудозатратыПланПоЗаявкам + ТрудозатратыПланПоЗаявкам;
				ТрудозатратыПрисвоены = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ТаблицаИсполнителей;
	
КонецФункции

//АбисСофт-Кострицын Олег-Финиш  17 января 2014 г.

// Получает фактические трудозатраты исполнителей проектной задачи
// Параметры:
//	ПроектнаяЗадача - ссылка на проектную задачу
//	ЕдиницаТрудозатратФакт - единица трудозатрат, в которых считать трудозатраты.
//		Если параметр не указан, единицы берутся из проектной задачи.
// Возвращает:
//	ТаблицаЗначений, содержащая исполнителей проектной задачи и их фактические трудозатраты
//		Исполнитель - пользователь или роль исполнителей
//		ОсновнойОбъектАдресации - объект адресации
//		ДополнительныйОбъектАдресации - объект адресации
//		ТрудозатратыФакт - неотрицательное число (10.2)
Функция ПолучитьФактическиеТрудозатратыИсполнителейПроектнойЗадачи(
	ПроектнаяЗадача,
	ЕдиницаТрудозатрат = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТаблицаИсполнителей = Новый ТаблицаЗначений();
	ТипИсполнитель = Новый ОписаниеТипов("СправочникСсылка.Пользователи,СправочникСсылка.РолиИсполнителей");
	ТипОбъектАдресации = Новый ОписаниеТипов("ПланВидовХарактеристикСсылка.ОбъектыАдресацииЗадач");
	ТипВеличинаТрудозатрат = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10, 2, ДопустимыйЗнак.Неотрицательный));
	ТаблицаИсполнителей.Колонки.Добавить("Исполнитель", ТипИсполнитель);
	ТаблицаИсполнителей.Колонки.Добавить("ОсновнойОбъектАдресации", ТипОбъектАдресации);
	ТаблицаИсполнителей.Колонки.Добавить("ДополнительныйОбъектАдресации", ТипОбъектАдресации);
	ТаблицаИсполнителей.Колонки.Добавить("ТрудозатратыФакт", ТипВеличинаТрудозатрат);
	
	Для каждого СтрокаТаблицы из ПроектнаяЗадача.Исполнители Цикл
		НоваяСтрока = ТаблицаИсполнителей.Добавить();
		НоваяСтрока.Исполнитель = СтрокаТаблицы.Исполнитель;
		НоваяСтрока.ОсновнойОбъектАдресации = СтрокаТаблицы.ОсновнойОбъектАдресации;
		НоваяСтрока.ДополнительныйОбъектАдресации = СтрокаТаблицы.ДополнительныйОбъектАдресации;
		НоваяСтрока.ТрудозатратыФакт = 0;
	КонецЦикла;
	
	// Получение величин для расчета трудозатрат
	РеквизитыПроектнойЗадачи = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(ПроектнаяЗадача,
		"Владелец, 
		|ГрафикРаботы, 
		|ЕдиницаТрудозатратФакт");
	РеквизитыПроекта = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(РеквизитыПроектнойЗадачи.Владелец,
		"ГрафикРаботы");
		
	ГрафикРаботы = 
		?(ЗначениеЗаполнено(РеквизитыПроектнойЗадачи.ГрафикРаботы), 
		РеквизитыПроектнойЗадачи.ГрафикРаботы, 
		РеквизитыПроекта.ГрафикРаботы);
		
	ЕдиницаТрудозатратДляРасчета = ЕдиницаТрудозатрат;
	Если ЕдиницаТрудозатрат = Неопределено Тогда
		ЕдиницаТрудозатратДляРасчета = РеквизитыПроектнойЗадачи.ЕдиницаТрудозатратФакт;
	КонецЕсли;
	
	//Выборка всех записей о трудозатратах для этой проектной задачи
	Отбор = Новый Структура("ПроектнаяЗадача", ПроектнаяЗадача);
	ВыборкаЗаписей = РегистрыСведений.ФактическиеТрудозатраты.Выбрать(Отбор);
	
	Пока ВыборкаЗаписей.Следующий() Цикл
		ТрудозатратыФакт = РаботаСПроектами.ПересчитатьТрудозатраты(
			ВыборкаЗаписей.Длительность / 60,
			Перечисления.ЕдиницыТрудозатрат.ЧеловекоМинута,
			ЕдиницаТрудозатратДляРасчета,
			ГрафикРаботы);
			
		ТрудозатратыПрисвоены = Ложь;
		
		// Поиск пользователя, которому приписать трудозатраты
		Для Каждого Участник Из ТаблицаИсполнителей Цикл
			Если ТипЗнч(Участник.Исполнитель) = Тип("СправочникСсылка.Пользователи")
				И Участник.Исполнитель = ВыборкаЗаписей.Пользователь Тогда 
				Участник.ТрудозатратыФакт = Участник.ТрудозатратыФакт + ТрудозатратыФакт;
				ТрудозатратыПрисвоены = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если НЕ ТрудозатратыПрисвоены Тогда
			// Поиск роли, которой приписать трудозатраты
			Для Каждого Участник Из ТаблицаИсполнителей Цикл	
				Если ТипЗнч(Участник.Исполнитель) = Тип("СправочникСсылка.РолиИсполнителей") Тогда
					Если ТипЗнч(ВыборкаЗаписей.Источник) = Тип("ЗадачаСсылка.ЗадачаИсполнителя") Тогда
						РеквизитыЗадачи = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(
							ВыборкаЗаписей.Источник,
							"РольИсполнителя, ОсновнойОбъектАдресации, ДополнительныйОбъектАдресации");
						Если РеквизитыЗадачи.РольИсполнителя = Участник.Исполнитель
							И РеквизитыЗадачи.ОсновнойОбъектАдресации = Участник.ОсновнойОбъектАдресации 
							И РеквизитыЗадачи.ДополнительныйОбъектАдресации = Участник.ДополнительныйОбъектАдресации Тогда
							
							Участник.ТрудозатратыФакт = Участник.ТрудозатратыФакт + ТрудозатратыФакт;
							ТрудозатратыПрисвоены = Истина;
							
						КонецЕсли;		
					//АбисСофт-Кострицын Олег-Старт  25 января 2014 г.	
					//Иначе
					//	ИсполнителиРоли = БизнесПроцессыИЗадачиСервер.ИсполнителиРоли(
					//		Участник.Исполнитель,
					//		Участник.ОсновнойОбъектАдресации,
					//		Участник.ДополнительныйОбъектАдресации);
					//	
					//	Если ИсполнителиРоли.Найти(ВыборкаЗаписей.Пользователь) <> Неопределено Тогда
					//		Если ЗначениеЗаполнено(Участник.ОсновнойОбъектАдресации)
					//			И ЗначениеЗаполнено(Участник.ДополнительныйОбъектАдресации) Тогда
					//			Участник.ТрудозатратыФакт = Участник.ТрудозатратыФакт + ТрудозатратыФакт;
					//			ТрудозатратыПрисвоены = Истина;
					//		ИначеЕсли ЗначениеЗаполнено(Участник.ОсновнойОбъектАдресации) Тогда
					//			Участник.ТрудозатратыФакт = Участник.ТрудозатратыФакт + ТрудозатратыФакт;
					//			ТрудозатратыПрисвоены = Истина;
					//		ИначеЕсли ЗначениеЗаполнено(Участник.ДополнительныйОбъектАдресации) Тогда
					//			Участник.ТрудозатратыФакт = Участник.ТрудозатратыФакт + ТрудозатратыФакт;
					//			ТрудозатратыПрисвоены = Истина;
					//		Иначе
					//			Участник.ТрудозатратыФакт = Участник.ТрудозатратыФакт + ТрудозатратыФакт;
					//			ТрудозатратыПрисвоены = Истина;
					//		КонецЕсли;
					//	КонецЕсли;
					//КонецЕсли;
					Иначе
						ИсполнителиРоли = РаботаСЗаявками.ПолучитьПроектнуюКоманду(ВыборкаЗаписей.Проект,ВыборкаЗаписей.ПроектнаяЗадача,ВыборкаЗаписей.ДатаДобавления);
							
						Если ИсполнителиРоли.Найти(ВыборкаЗаписей.Пользователь,"Пользователь") <> Неопределено Тогда
							Участник.ТрудозатратыФакт = Участник.ТрудозатратыФакт + ТрудозатратыФакт;
							ТрудозатратыПрисвоены = Истина;
						КонецЕсли;
					КонецЕсли;
					//АбисСофт-Кострицын Олег-финиш  25 января 2014 г.
					Если ТрудозатратыПрисвоены Тогда
						Прервать;
					КонецЕсли;
				КонецЕсли;	
			КонецЦикла;
		КонецЕсли;	
	КонецЦикла;
	
	Возврат ТаблицаИсполнителей;
	
КонецФункции

// Возвращает общие фактические трудозатраты по проекту
Функция ПолучитьФактическиеТрудозатратыПроекта(Проект) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПроектныеЗадачи.Ссылка
		|ИЗ
		|	Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
		|ГДЕ
		|	ПроектныеЗадачи.Владелец = &Владелец
		|	И ПроектныеЗадачи.ПометкаУдаления = ЛОЖЬ";
	Запрос.УстановитьПараметр("Владелец", Проект);
	РезультатЗапроса = Запрос.Выполнить();
	СуммарныеФактическиеТрудозатраты = 0;
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			СуммарныеФактическиеТрудозатраты = 
				СуммарныеФактическиеТрудозатраты
				+ ПолучитьФактическиеТрудозатратыПроектнойЗадачи(Выборка.Ссылка);	
		КонецЦикла;
	КонецЕсли;
	
	Возврат СуммарныеФактическиеТрудозатраты;	
	
КонецФункции

// Выполняет поиск переданного пользователя среди исполнителей проектной задачи с учетом ролевой адресации
Функция НайтиИсполнителяВПроектнойЗадачеСУчетомВхожденияВРоль(
	ПроектнаяЗадача,
	Исполнитель,
	ЗадачаИсполнителя) Экспорт
	
	Если ПроектнаяЗадача.Исполнители.Количество() = 0 Тогда 
		Возврат Истина;
	КонецЕсли;	
	
	ИсполнительНайден = Ложь;
	Если ЗначениеЗаполнено(ЗадачаИсполнителя) И ТипЗнч(ЗадачаИсполнителя) = Тип("ЗадачаСсылка.ЗадачаИсполнителя") Тогда
		
		ИсполнителиЗадачи = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(ЗадачаИсполнителя, 
			"РольИсполнителя,
			|ОсновнойОбъектАдресации,
			|ДополнительныйОбъектАдресации,
			|Исполнитель");
			
		Для Каждого ИсполнительПроектнойКоманды Из ПроектнаяЗадача.Исполнители Цикл
			Если ЗначениеЗаполнено(ИсполнителиЗадачи.Исполнитель)
				И ИсполнителиЗадачи.Исполнитель = ИсполнительПроектнойКоманды.Исполнитель
				ИЛИ 
				ЗначениеЗаполнено(ИсполнителиЗадачи.РольИсполнителя)
				И ИсполнителиЗадачи.РольИсполнителя = ИсполнительПроектнойКоманды.Исполнитель
				И ИсполнителиЗадачи.ОсновнойОбъектАдресации = ИсполнительПроектнойКоманды.ОсновнойОбъектАдресации
				И ИсполнителиЗадачи.ДополнительныйОбъектАдресации = ИсполнительПроектнойКоманды.ДополнительныйОбъектАдресации Тогда
				
				ИсполнительНайден = Истина;
				Прервать;
				
			КонецЕсли;	
		КонецЦикла;	
			
	ИначеЕсли ЗначениеЗаполнено(Исполнитель) Тогда
		
		Для Каждого ИсполнительПроектнойКоманды Из ПроектнаяЗадача.Исполнители Цикл

			//Если ТипЗнч(ИсполнительПроектнойКоманды.Исполнитель) = Тип("СправочникСсылка.РолиИсполнителей") Тогда
			//	
			//	ИсполнителиРоли = БизнесПроцессыИЗадачиСервер.ИсполнителиРоли(
			//	ИсполнительПроектнойКоманды.Исполнитель,
			//	ИсполнительПроектнойКоманды.ОсновнойОбъектАдресации,
			//	ИсполнительПроектнойКоманды.ДополнительныйОбъектАдресации);
			//	
			//	Для Каждого ИсполнительМассива Из ИсполнителиРоли Цикл
			//		Если ИсполнительМассива = Исполнитель Тогда
			//			ИсполнительНайден = Истина;
			//			Прервать;
			//		КонецЕсли;
			//	КонецЦикла;	
			//	
			//ИначеЕсли Исполнитель = ИсполнительПроектнойКоманды.Исполнитель Тогда	
			//	
			//	ИсполнительНайден = Истина;
			//	
			//КонецЕсли;	
			
			Если Исполнитель = ИсполнительПроектнойКоманды.Исполнитель Тогда	
				
				ИсполнительНайден = Истина;
				
			КонецЕсли;
			
			Если ИсполнительНайден Тогда
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ИсполнительНайден;
	
КонецФункции

// Выполняет поиск переданного пользователя среди исполнителей проектной задачи с учетом ролевой адресации
Функция НайтиИсполнителяВКомандеПроектаСУчетомВхожденияВРоль(
	Проект,
	Исполнитель,
	ЗадачаИсполнителя) Экспорт
	
	Если Проект.ПроектнаяКоманда.Количество() = 0 Тогда 
		Возврат Истина;
	КонецЕсли;	
	
	ИсполнительНайден = Ложь;
	Если ЗначениеЗаполнено(ЗадачаИсполнителя) И ТипЗнч(ЗадачаИсполнителя) = Тип("ЗадачаСсылка.ЗадачаИсполнителя") Тогда
		
		ИсполнителиЗадачи = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(ЗадачаИсполнителя, 
			"РольИсполнителя,
			|ОсновнойОбъектАдресации,
			|ДополнительныйОбъектАдресации,
			|Исполнитель");
			
		Для Каждого ИсполнительПроектнойКоманды Из Проект.ПроектнаяКоманда Цикл
			Если ЗначениеЗаполнено(ИсполнителиЗадачи.Исполнитель)
				И ИсполнителиЗадачи.Исполнитель = ИсполнительПроектнойКоманды.Исполнитель
				ИЛИ 
				ЗначениеЗаполнено(ИсполнителиЗадачи.РольИсполнителя)
				И ИсполнителиЗадачи.РольИсполнителя = ИсполнительПроектнойКоманды.Исполнитель
				И ИсполнителиЗадачи.ОсновнойОбъектАдресации = ИсполнительПроектнойКоманды.ОсновнойОбъектАдресации
				И ИсполнителиЗадачи.ДополнительныйОбъектАдресации = ИсполнительПроектнойКоманды.ДополнительныйОбъектАдресации Тогда
				
				ИсполнительНайден = Истина;
				Прервать;
				
			КонецЕсли;	
		КонецЦикла;	
	ИначеЕсли ЗначениеЗаполнено(Исполнитель) Тогда
		
		Для Каждого ИсполнительПроектнойКоманды Из Проект.ПроектнаяКоманда Цикл
			
			Если Исполнитель = ИсполнительПроектнойКоманды.Исполнитель Тогда	
				
				ИсполнительНайден = Истина;
				
			КонецЕсли;	
			
			Если ИсполнительНайден Тогда
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ИсполнительНайден;
	
КонецФункции

// Поверяет возможность отнесения фактических трудозатрат на проектную задачу
Функция ПроверитьЗаписьОФактическихТрудозатратах(
	Проект,
	ПроектнаяЗадача,
	Источник,
	Пользователь,
	ФормируемоеСообщениеОбОшибке) Экспорт
	
	ФормируемоеСообщениеОбОшибке = "";
	Если ЗначениеЗаполнено(ПроектнаяЗадача) Тогда 
		
		ИсполнительНайден = РаботаСПроектами.НайтиИсполнителяВПроектнойЗадачеСУчетомВхожденияВРоль(
			ПроектнаяЗадача,
			Пользователь,
			Источник);
			
		Если Не ИсполнительНайден Тогда
			ФормируемоеСообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Нельзя списать трудозатраты исполнителя ""%1"", так как он не запланирован в проектной задаче ""%2""'"),
				Строка(Пользователь),
				Строка(ПроектнаяЗадача));	
		КонецЕсли;
		
	ИначеЕсли ЗначениеЗаполнено(Проект) Тогда 
		
		Если Не Проект.СписыватьЗатратыНаПроект Тогда 
			ФормируемоеСообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Нельзя списать трудозатраты исполнителя на проект ""%1"", так как для этого проекта запрещено списывать трудозатраты на проект в целом'"),
				Строка(Проект));
		Иначе		
			ИсполнительНайден = РаботаСПроектами.НайтиИсполнителяВКомандеПроектаСУчетомВхожденияВРоль(
				Проект,
				Пользователь,
				Источник);	
				
			Если Не ИсполнительНайден Тогда
				ФормируемоеСообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Нельзя списать трудозатраты исполнителя ""%1"" на проект ""%2"", так как он не входит в проектную команду'"),
					Строка(Пользователь),
					Строка(Проект));
			КонецЕсли;	
				
		КонецЕсли;	
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ФормируемоеСообщениеОбОшибке) Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;		
		
КонецФункции


// Выполняет поиск переданного пользователя среди исполнителей проектной задачи с учетом ролевой адресации
Функция НайтиИсполнителяРольВПроектнойЗадаче(
	ПроектнаяЗадача,
	Исполнитель,
	ОсновнойОбъектАдресации = Неопределено,
	ДополнительныйОбъектАдресации = Неопределено) Экспорт
	
	Если ПроектнаяЗадача.Исполнители.Количество() = 0 Тогда 
		Возврат Истина;
	КонецЕсли;	
	
	Для Каждого ИсполнительПроектнойКоманды Из ПроектнаяЗадача.Исполнители Цикл
		
		Если ТипЗнч(ИсполнительПроектнойКоманды.Исполнитель) = Тип("СправочникСсылка.РолиИсполнителей") Тогда
			
			Если Исполнитель = ИсполнительПроектнойКоманды.Исполнитель
				И ОсновнойОбъектАдресации = ИсполнительПроектнойКоманды.ОсновнойОбъектАдресации
				И ДополнительныйОбъектАдресации = ИсполнительПроектнойКоманды.ДополнительныйОбъектАдресации Тогда
				Возврат Истина;
			КонецЕсли;
			
		ИначеЕсли Исполнитель = ИсполнительПроектнойКоманды.Исполнитель Тогда 
			Возврат Истина;
		КонецЕсли;	
		
	КонецЦикла;	
	
	Возврат Ложь;
	
КонецФункции

// Выполняет поиск переданного пользователя среди исполнителей проектной задачи с учетом ролевой адресации
Функция НайтиИсполнителяРольВКомандеПроекта(
	Проект,
	Исполнитель,
	ОсновнойОбъектАдресации = Неопределено,
	ДополнительныйОбъектАдресации = Неопределено) Экспорт
	
	Если Проект.ПроектнаяКоманда.Количество() = 0 Тогда 
		Возврат Истина;
	КонецЕсли;	
	
	Для Каждого ИсполнительПроектнойКоманды Из Проект.ПроектнаяКоманда Цикл
		
		Если ТипЗнч(ИсполнительПроектнойКоманды.Исполнитель) = Тип("СправочникСсылка.РолиИсполнителей") Тогда
			
			Если Исполнитель = ИсполнительПроектнойКоманды.Исполнитель
				И ОсновнойОбъектАдресации = ИсполнительПроектнойКоманды.ОсновнойОбъектАдресации
				И ДополнительныйОбъектАдресации = ИсполнительПроектнойКоманды.ДополнительныйОбъектАдресации Тогда
				Возврат Истина;
			КонецЕсли;
			
		ИначеЕсли Исполнитель = ИсполнительПроектнойКоманды.Исполнитель Тогда 
			Возврат Истина;
		КонецЕсли;	
		
	КонецЦикла;	
	
	Возврат Ложь;
	
КонецФункции


// Проверяет возможность старта бизнес-процесса по проекту и проектной задаче
Процедура ПроверитьВозможностьСтартаБизнесПроцесса(Знач Объект, ВозвращаемыйМассивОшибок) Экспорт 
	
	ВозвращаемыйМассивОшибок = Новый Массив;
	
	Если ЗначениеЗаполнено(Объект.ПроектнаяЗадача) Тогда 
		
		Если Не РаботаСПроектами.НаступилаДатаНачалаЗадачи(Объект.ПроектнаяЗадача) Тогда 
			ТекстОшибки = НСтр("ru = 'Дата начала процесса меньше, чем дата начала проектной задачи. Выполнить запуск процесса?'");
			ВозвращаемыйМассивОшибок.Добавить(ТекстОшибки);
		КонецЕсли;	
			
		Если РаботаСПроектами.ЕстьНезавершенныеПредшественники(Объект.ПроектнаяЗадача) Тогда 
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'У проектной задачи ""%1"" есть незавершенные предшественники. Выполнить запуск процесса?'"),
				Строка(Объект.ПроектнаяЗадача));
			ВозвращаемыйМассивОшибок.Добавить(ТекстОшибки);
		КонецЕсли;	
		
		ЗадатьВопрос = Ложь;
		ПлановаяДатаОкончания = ОбщегоНазначения.ПолучитьЗначениеРеквизита(Объект.ПроектнаяЗадача, "ТекущийПланОкончание");
		Если ЗначениеЗаполнено(ПлановаяДатаОкончания) Тогда
			
			Если Объект.Ссылка.Метаданные().Реквизиты.Найти("СрокИсполнения") <> Неопределено Тогда 
				ЗадатьВопрос = (Объект.СрокИсполнения > ПлановаяДатаОкончания);
			КонецЕсли;
			
			Если ЗадатьВопрос Тогда
				ТекстОшибки = НСтр("ru = 'Срок исполнения процесса превышает плановую дату окончания проектной задачи. Продолжить?'");
				ВозвращаемыйМассивОшибок.Добавить(ТекстОшибки);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;	
		
	Если ЗначениеЗаполнено(Объект.Проект) Тогда 
		
		Если Не РаботаСПроектами.НаступилаДатаНачалаПроекта(Объект.Проект) Тогда 
			ТекстОшибки = НСтр("ru = 'Дата начала процесса меньше, чем дата начала проекта. Выполнить запуск процесса?'");
			ВозвращаемыйМассивОшибок.Добавить(ТекстОшибки);
		КонецЕсли;
		
		ЗадатьВопрос = Ложь;
		ПлановаяДатаОкончания = ОбщегоНазначения.ПолучитьЗначениеРеквизита(Объект.Проект, "ТекущийПланОкончание");
		Если ЗначениеЗаполнено(ПлановаяДатаОкончания) Тогда
			
			Если Объект.Ссылка.Метаданные().Реквизиты.Найти("СрокИсполнения") <> Неопределено Тогда 
				ЗадатьВопрос = (Объект.СрокИсполнения > ПлановаяДатаОкончания);
			КонецЕсли;
			
			Если ЗадатьВопрос Тогда
				ТекстОшибки = НСтр("ru = 'Срок исполнения процесса превышает плановую дату окончания проекта. Продолжить?'");
				ВозвращаемыйМассивОшибок.Добавить(ТекстОшибки);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Проверяет, есть ли у указанноу проектной задачи вложенные задачи
Функция ЕстьДочерниеПроектныеЗадачи(ПроектнаяЗадача) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =	
		"ВЫБРАТЬ
		|	ПроектныеЗадачи.Ссылка
		|ИЗ
		|	Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
		|ГДЕ
		|	ПроектныеЗадачи.Родитель = &Родитель";
	Запрос.УстановитьПараметр("Родитель", ПроектнаяЗадача);
	Возврат НЕ Запрос.Выполнить().Пустой();
	
КонецФункции

Функция СформироватьДанныеВыбораЗадачиПроекта(Текст, Проект) Экспорт 
	
	ДанныеВыбора = Новый СписокЗначений;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПроектныеЗадачи.Ссылка КАК Ссылка,
		|	ПроектныеЗадачи.КодСДР
		|ИЗ
		|	Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
		|ГДЕ
		|	ПроектныеЗадачи.Владелец = &Проект
		|	И (ПроектныеЗадачи.Наименование ПОДОБНО &Текст
		|			ИЛИ ПроектныеЗадачи.КодСДР ПОДОБНО &Текст)
		|	И (НЕ ПроектныеЗадачи.ПометкаУдаления)";
	
	Запрос.УстановитьПараметр("Проект", Проект);
	Запрос.УстановитьПараметр("Текст", Текст + "%");
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ДанныеВыбора.Добавить(Выборка.Ссылка, Строка(Выборка.Ссылка) + " (" + Строка(Выборка.КодСДР) + ")");
	КонецЦикла;	
	
	Возврат ДанныеВыбора;
	
КонецФункции

Функция ПолучитьКодСДРИНомерЗадачиВУровне(Проект, Родитель) Экспорт 
	
	МаксимальныйНомер = РаботаСПроектами.ПолучитьМаксимальныйНомерЗадачиУровня(Проект, Родитель);
	КодСДРСтрока = "";
	Если ЗначениеЗаполнено(Родитель) Тогда
		КодСДРСтрока = Родитель.КодСДР;	
	КонецЕсли;
	НомерЗадачиВУровне = МаксимальныйНомер + 1;
	КодСДР = 
		КодСДРСтрока
		+ ?(ЗначениеЗаполнено(КодСДРСтрока), ".", "")
		+ Строка(НомерЗадачиВУровне);
		
	ДанныеВозврата = Новый Структура;
	ДанныеВозврата.Вставить("КодСДР", КодСДР);
	ДанныеВозврата.Вставить("НомерЗадачиВУровне", НомерЗадачиВУровне);
	
	Возврат ДанныеВозврата;
	
КонецФункции

Функция СформироватьДанныеВыбораИсполнителя(Проект, Текст = "") Экспорт 
	
	ДанныеВыбора = Новый СписокЗначений;
	Для Каждого Строка Из Проект.ПроектнаяКоманда Цикл
		Если Не ЗначениеЗаполнено(Строка.Исполнитель) Тогда 
			Продолжить;
		КонецЕсли;	
		
		Если Текст <> "" И Нрег(Лев(Строка.Исполнитель, СтрДлина(Текст))) <> НРег(Текст) Тогда 
			Продолжить;
		КонецЕсли;	
		
		ЗначениеВыбора = Новый Структура;
		ЗначениеВыбора.Вставить("Исполнитель", Строка.Исполнитель);
		ЗначениеВыбора.Вставить("ОсновнойОбъектАдресации", Строка.ОсновнойОбъектАдресации);
		ЗначениеВыбора.Вставить("ДополнительныйОбъектАдресации", Строка.ДополнительныйОбъектАдресации);
		
		Если ТипЗнч(Строка.Исполнитель) = Тип("СправочникСсылка.Контрагенты") 
			Или ТипЗнч(Строка.Исполнитель) = Тип("СправочникСсылка.КонтактныеЛица") 
			Или ТипЗнч(Строка.Исполнитель) = Тип("СправочникСсылка.Пользователи") Тогда 
			ПредставлениеВыбора = Строка(Строка.Исполнитель);
		ИначеЕсли ЗначениеЗаполнено(Строка.ОсновнойОбъектАдресации) И ЗначениеЗаполнено(Строка.ДополнительныйОбъектАдресации) Тогда 
			ПредставлениеВыбора = Строка(Строка.Исполнитель) + "(" + Строка.ОсновнойОбъектАдресации + ", " + Строка.ДополнительныйОбъектАдресации + ")";
		ИначеЕсли ЗначениеЗаполнено(Строка.ОсновнойОбъектАдресации) Тогда 
			ПредставлениеВыбора = Строка(Строка.Исполнитель) + "(" + Строка.ОсновнойОбъектАдресации + ")";
		Иначе
			ПредставлениеВыбора = Строка(Строка.Исполнитель);
		КонецЕсли;	
		
		ДанныеВыбора.Добавить(ЗначениеВыбора, ПредставлениеВыбора);
	КонецЦикла;	
	
	Возврат ДанныеВыбора;
	
КонецФункции

Функция ЕстьПроектныеЗадачи(Проект) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА
	|ИЗ
	|	Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
	|ГДЕ
	|	ПроектныеЗадачи.Владелец = &Проект";
	
	Запрос.УстановитьПараметр("Проект", Проект);
	
	Результат = Запрос.Выполнить();
	
	Возврат Не Результат.Пустой();
	
КонецФункции


Функция ЕстьНезавершенныеПроектныеЗадачи(Проект) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА 
	|ИЗ
	|	Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
	|ГДЕ
	|	ПроектныеЗадачи.Владелец = &Проект
	|	И ПроектныеЗадачи.ОкончаниеФакт = ДАТАВРЕМЯ(1, 1, 1)";
	
	Запрос.УстановитьПараметр("Проект", Проект);
	
	Результат = Запрос.Выполнить();
	
	Возврат Не Результат.Пустой();
	
КонецФункции


Функция СформироватьДанныеВыбораПроектаЗадачи(Текст) Экспорт 
	
	ДанныеВыбора = Новый СписокЗначений;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Проекты.Ссылка КАК Проект,
	|	НЕОПРЕДЕЛЕНО КАК ПроектнаяЗадача,
	|	ЕСТЬNULL(ПроектныеЗадачи.ЕстьПроектныеЗадачи, ЛОЖЬ) КАК ЕстьПроектныеЗадачи
	|ИЗ
	|	Справочник.Проекты КАК Проекты
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ИСТИНА КАК ЕстьПроектныеЗадачи,
	|			ПроектныеЗадачи.Владелец КАК Владелец
	|		ИЗ
	|			Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ПроектныеЗадачи.Владелец) КАК ПроектныеЗадачи
	|		ПО Проекты.Ссылка = ПроектныеЗадачи.Владелец
	|ГДЕ
	|	Проекты.Наименование ПОДОБНО &Текст
	|	И НЕ Проекты.ЭтоГруппа
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПроектныеЗадачи.Владелец,
	|	ПроектныеЗадачи.Ссылка,
	|	ИСТИНА
	|ИЗ
	|	Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
	|ГДЕ
	|	ПроектныеЗадачи.Наименование ПОДОБНО &Текст";
	Запрос.УстановитьПараметр("Текст", Текст + "%");
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Значение = Новый Структура("Проект, ПроектнаяЗадача, ЕстьПроектныеЗадачи", Выборка.Проект, Выборка.ПроектнаяЗадача, Выборка.ЕстьПроектныеЗадачи);
		Представление = РаботаСПроектамиКлиентСервер.ПредставлениеПроектаЗадачи(Выборка.Проект, Выборка.ПроектнаяЗадача);
		
		ДанныеВыбора.Добавить(Значение, Представление);
	КонецЦикла;	
	
	Возврат ДанныеВыбора;
	
КонецФункции	

// Возвращает вид проекта по умолчанию
Функция ПолучитьВидПроектаПоУмолчанию() Экспорт
	
	ВидПроектаПоУмолчанию = Неопределено;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьВидыПроектов") Тогда 
		Возврат ВидПроектаПоУмолчанию;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВидыПроектов.Ссылка
	|ИЗ
	|	Справочник.ВидыПроектов КАК ВидыПроектов
	|ГДЕ
	|	НЕ ВидыПроектов.ПометкаУдаления";
	
	ВидыПроектов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	Если ВидыПроектов.Количество() = 1 Тогда
    	ВидПроектаПоУмолчанию = ВидыПроектов[0];
		Возврат ВидПроектаПоУмолчанию;
	КонецЕсли;
	
	ВидПроектаПоУмолчанию = ХранилищеОбщихНастроек.Загрузить(
		"НастройкиРаботыСПроектами", 
		"ВидПроектаПоУмолчанию");
		
	Возврат ВидПроектаПоУмолчанию;
			
КонецФункции

// Возвращает количество проектов с пустым видом
Функция КоличествоПроектовСПустымВидом() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КОЛИЧЕСТВО(*) КАК Количество
		|ИЗ
		|	Справочник.Проекты КАК Проекты
		|ГДЕ
		|	Проекты.ВидПроекта = ЗНАЧЕНИЕ(Справочник.ВидыПроектов.ПустаяСсылка)";		
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат 0;
	КонецЕсли;	
	
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	Возврат Выборка.Количество;
	
КонецФункции

// Создает вид проекта с заданным наименованием.
// Возвращает ссылку на созданный вид проекта.
Функция СоздатьВидПроекта(Наименование) Экспорт
	
	ВидПроекта = Справочники.ВидыПроектов.СоздатьЭлемент();
	ВидПроекта.Наименование = Наименование;
	ВидПроекта.Записать();
	Возврат ВидПроекта.Ссылка;
	
КонецФункции

// Заполняет вид проекта
Процедура ЗаполнитьВидПроекта(ВидПроекта) Экспорт
	
	НачатьТранзакцию();
	Попытка 
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Проекты.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Проекты Как Проекты
		|ГДЕ
		|	Проекты.ВидПроекта = ЗНАЧЕНИЕ(Справочник.ВидыПроектов.ПустаяСсылка)";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			ПроектОбъект = Выборка.Ссылка.ПолучитьОбъект();
			ПроектОбъект.Заблокировать();
			ПроектОбъект.ВидПроекта = ВидПроекта;
			ПроектОбъект.Записать();
		КонецЦикла;	
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры
