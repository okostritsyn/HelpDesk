Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Ссылка.Пустая() Тогда
		ПредыдущаяПометкаУдаления = ОбщегоНазначения.ПолучитьЗначениеРеквизита(Ссылка, "ПометкаУдаления");
		Если ПометкаУдаления <> ПредыдущаяПометкаУдаления Тогда 
			РаботаСФайламиВызовСервера.ПометитьНаУдалениеПриложенныеФайлы(Ссылка, ПометкаУдаления);
		КонецЕсли;
	КонецЕсли;
	
	ВстроеннаяПочтаСервер.ПрименитьПравила(ЭтотОбъект);
	
	Если Не ЗначениеЗаполнено(Папка) Тогда
		Отказ = Истина;
		ВызватьИсключение НСтр("ru = 'Не указана папка письма'");
	КонецЕсли;
	
КонецПроцедуры

// Устанавливает текст письма.
//
Процедура УстановитьТекстПисьма(Текст, Знач ПараметрТипТекста = Неопределено, Знач ПараметрКодировка = Неопределено) Экспорт
	
	Если ПараметрТипТекста = Неопределено Тогда
		ПараметрТипТекста = Перечисления.ТипыТекстовПочтовыхСообщений.ПростойТекст;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрКодировка) Тогда
		Кодировка = ПараметрКодировка;
	КонецЕсли;
	
	ТипТекста = ПараметрТипТекста;
	
	Если ТипТекста = Перечисления.ТипыТекстовПочтовыхСообщений.ПростойТекст Тогда
		ТекстПисьмаПростойТекстХранилище = Новый ХранилищеЗначения(Текст, Новый СжатиеДанных);
	Иначе
		ТекстПисьмаHTMLХранилище = Новый ХранилищеЗначения(
			Текст,
			Новый СжатиеДанных);
		ТекстПисьмаПростойТекстХранилище = Новый ХранилищеЗначения(
			РаботаСHTML.ПолучитьПростойТекстИзHTML(Текст, ПараметрКодировка),
			Новый СжатиеДанных);
	КонецЕсли;
	
КонецПроцедуры

// Возвращает структуру текста письма
//
Функция ПолучитьСтруктуруТекстаПисьма() Экспорт
	
	Если ТипТекста = Перечисления.ТипыТекстовПочтовыхСообщений.ПростойТекст
		Или Не ЗначениеЗаполнено(ТипТекста) Тогда
		ТипТекстаПисьма = Перечисления.ТипыТекстовПочтовыхСообщений.ПростойТекст;
		ТекстПисьма = ТекстПисьмаПростойТекстХранилище.Получить();
		Если ТекстПисьма = Неопределено Тогда
			ТекстПисьма = "";
		КонецЕсли;
		ПростойТекст = ТекстПисьма;
		
	ИначеЕсли ТипТекста = Перечисления.ТипыТекстовПочтовыхСообщений.HTML Тогда
		ТипТекстаПисьма = Перечисления.ТипыТекстовПочтовыхСообщений.HTML;
		ТекстПисьма = ТекстПисьмаHTMLХранилище.Получить();
		Если ТекстПисьма = Неопределено Тогда
			ТекстПисьма = "";
		КонецЕсли;
		ПростойТекст = ТекстПисьмаПростойТекстХранилище.Получить();
		Если ПростойТекст = Неопределено Тогда
			ПростойТекст = "";
		КонецЕсли;
		
	ИначеЕсли ТипТекста = Перечисления.ТипыТекстовПочтовыхСообщений.РазмеченныйТекст Тогда
		ТипТекстаПисьма = Перечисления.ТипыТекстовПочтовыхСообщений.РазмеченныйТекст;
		ТекстПисьма = ТекстПисьмаРазмеченныйТекстХранилище.Получить();
		Если ТекстПисьма = Неопределено Тогда
			ТекстПисьма = "";
		КонецЕсли;
		ПростойТекст = ТекстПисьмаПростойТекстХранилище.Получить();
		Если ПростойТекст = Неопределено Тогда
			ПростойТекст = "";
		КонецЕсли;
		
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("ТипТекста", ТипТекстаПисьма);
	Результат.Вставить("Текст", ТекстПисьма);
	Результат.Вставить("ПростойТекст", ПростойТекст);
	Результат.Вставить("Кодировка", Кодировка);
	
	Возврат Результат;
	
КонецФункции

