
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Параметры.Проект) Тогда 
		Элементы.СписокПроекты.ТекущаяСтрока = Параметры.Проект;
		ЗаполнитьДеревоЗадач(Параметры.Проект);
		
		Если ЗначениеЗаполнено(Параметры.ПроектнаяЗадача) Тогда 
			Идентификатор = Неопределено;
			НайтиСтрокуДерева(Параметры.ПроектнаяЗадача, ДеревоЗадач.ПолучитьЭлементы(), Идентификатор);
			Если Идентификатор <> Неопределено Тогда 
				Элементы.ДеревоЗадач.ТекущаяСтрока = Идентификатор;
			КонецЕсли;	
		КонецЕсли;	
	КонецЕсли;	
	
	ВыбиратьТолькоПроектнуюЗадачу = Параметры.ВыбиратьТолькоПроектнуюЗадачу;
	Если ВыбиратьТолькоПроектнуюЗадачу Тогда 
		Элементы.ФормаВыбратьПроект.Видимость = Ложь;
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура НайтиСтрокуДерева(Ссылка, КоллекцияСтрок, Идентификатор)
	
	Для Каждого Строка Из КоллекцияСтрок Цикл
		Если Строка.Ссылка = Ссылка Тогда 
			Иднтификатор = Строка.ПолучитьИдентификатор();	
		    Возврат;
		КонецЕсли;	
		
		НайтиСтрокуДерева(Ссылка, Строка.ПолучитьЭлементы(), Иднтификатор);
	КонецЦикла;	
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПроектыПриАктивизацииСтроки(Элемент)
	
	Если ПерваяАктивизация = Истина ИЛИ ПерваяАктивизация = Неопределено Тогда
		ПерваяАктивизация = Ложь;
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.СписокПроекты.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("ОбработкаОжидания", 0.2, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОжидания()
	
	ТекущиеДанные = Элементы.СписокПроекты.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ЗаполнитьДеревоЗадач(ТекущиеДанные.Ссылка);
	
	Для Каждого Строка Из ДеревоЗадач.ПолучитьЭлементы() Цикл 
		Элементы.ДеревоЗадач.Развернуть(Строка.ПолучитьИдентификатор(), Истина);
	КонецЦикла;	
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДеревоЗадач(Проект)
	
	Дерево = РеквизитФормыВЗначение("ДеревоЗадач");
	Дерево.Строки.Очистить();
	
	Если ЗначениеЗаполнено(Проект) Тогда 
		
		Выборка = Справочники.ПроектныеЗадачи.ВыбратьИерархически(,Проект,,"НомерЗадачиВУровне Возр");
		Пока Выборка.Следующий() Цикл
			Родитель = Выборка.Родитель;
			
			Если Не ЗначениеЗаполнено(Родитель) Тогда 
				НоваяСтрока = Дерево.Строки.Добавить();
			Иначе	
				НайденнаяСтрока = Дерево.Строки.Найти(Родитель, "Ссылка", Истина);	
				НоваяСтрока = НайденнаяСтрока.Строки.Добавить();
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			НоваяСтрока.ИндексКартинки = ?(НоваяСтрока.ПометкаУдаления, 3, 2);
			НоваяСтрока.Представление = НоваяСтрока.КодСДР + " " + НоваяСтрока.Наименование;
			
		КонецЦикла;
		
	КонецЕсли;
	
	ЗначениеВРеквизитФормы(Дерево, "ДеревоЗадач");
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПроект(Команда)
	
	ТекущиеДанные = Элементы.СписокПроекты.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	Если ТекущиеДанные.ЭтоГруппа Тогда 
		ПоказатьПредупреждение(Неопределено, НСтр("ru = 'Выберите элемент, а не группу!'"));
		Возврат;
	КонецЕсли;	
	
	ВыбранноеЗначение = Новый Структура("Проект, ПроектнаяЗадача, ЕстьПроектныеЗадачи", 
		ТекущиеДанные.Ссылка, 
		Неопределено,
		ТекущиеДанные.ЕстьПроектныеЗадачи);
	
	ОповеститьОВыборе(ВыбранноеЗначение);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьЗадачу(Команда)
	
	ТекущиеДанные = Элементы.ДеревоЗадач.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	Если ВыбиратьТолькоПроектнуюЗадачу Тогда 
		ВыбранноеЗначение = ТекущиеДанные.Ссылка;
	Иначе	
		ВыбранноеЗначение = Новый Структура("Проект, ПроектнаяЗадача, ЕстьПроектныеЗадачи", 
			ТекущиеДанные.Владелец,
			ТекущиеДанные.Ссылка,
			Истина);
	КонецЕсли;		
	
	ОповеститьОВыборе(ВыбранноеЗначение);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПроектыВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ВыбиратьТолькоПроектнуюЗадачу Тогда 
		Возврат;
	КонецЕсли;	
	
	ТекущиеДанные = Элементы.СписокПроекты.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.ЭтоГруппа Тогда 
		ПоказатьПредупреждение(Неопределено, НСтр("ru = 'Выберите элемент, а не группу!'"));
		Возврат;
	КонецЕсли;
	
	ВыбранноеЗначение = Новый Структура("Проект, ПроектнаяЗадача, ЕстьПроектныеЗадачи", 
		ТекущиеДанные.Ссылка,
		Неопределено,
		ТекущиеДанные.ЕстьПроектныеЗадачи);
		
	ОповеститьОВыборе(ВыбранноеЗначение);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоЗадачВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ДеревоЗадач.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если ВыбиратьТолькоПроектнуюЗадачу Тогда 
		ВыбранноеЗначение = ТекущиеДанные.Ссылка;
	Иначе	
		ВыбранноеЗначение = Новый Структура("Проект, ПроектнаяЗадача, ЕстьПроектныеЗадачи", 
			ТекущиеДанные.Владелец, 
			ТекущиеДанные.Ссылка,
			Истина);
	КонецЕсли;	
	
	ОповеститьОВыборе(ВыбранноеЗначение);
	
КонецПроцедуры




