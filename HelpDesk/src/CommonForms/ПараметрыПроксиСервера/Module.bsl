////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НастройкаПроксиНаКлиенте = Параметры.НастройкаПроксиНаКлиенте;
	
	Если НастройкаПроксиНаКлиенте Тогда
		НастройкаПроксиСервера = ХранилищеОбщихНастроек.Загрузить("НастройкаПроксиСервера");
	Иначе
		НастройкаПроксиСервера = ПолучениеФайловИзИнтернета.ПолучитьНастройкиПроксиНаСервере1СПредприятие();
	КонецЕсли;
	
	Если ТипЗнч(НастройкаПроксиСервера) = Тип("Соответствие") Тогда
		ИспользоватьПрокси = НастройкаПроксиСервера["ИспользоватьПрокси"];
		Сервер       = НастройкаПроксиСервера["Сервер"];
		Пользователь = НастройкаПроксиСервера["Пользователь"];
		Пароль       = НастройкаПроксиСервера["Пароль"];
		Порт         = НастройкаПроксиСервера["Порт"];
		НеИспользоватьПроксиДляЛокальныхАдресов =
		           НастройкаПроксиСервера["НеИспользоватьПроксиДляЛокальныхАдресов"];
	КонецЕсли;
	
	Элементы.ПараметрыПрокси.Доступность = ?(ИспользоватьПрокси, Истина, Ложь);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

// Обработчик события нажатия на кнопку "ОК" командной панели формы
// Закрывает форму, передавая в качестве возвращаемого результата
// параметры прокси.
//
&НаКлиенте
Процедура ОкВыполнить()
	
	НастройкаПроксиСервера = Новый Соответствие;
	НастройкаПроксиСервера.Вставить("ИспользоватьПрокси", ИспользоватьПрокси);
	НастройкаПроксиСервера.Вставить("Пользователь", Пользователь);
	НастройкаПроксиСервера.Вставить("Пароль", Пароль);
	НастройкаПроксиСервера.Вставить("Порт", Порт);
	НастройкаПроксиСервера.Вставить("Сервер", Сервер);
	НастройкаПроксиСервера.Вставить("НеИспользоватьПроксиДляЛокальныхАдресов",
	                              НеИспользоватьПроксиДляЛокальныхАдресов);
	
	СохранитьНастройкиПроксиСервера(НастройкаПроксиНаКлиенте, НастройкаПроксиСервера);
	
	Закрыть(НастройкаПроксиСервера);
	
КонецПроцедуры

// Изменяем доступность группы редактирования параметров прокси если они не используются
//
&НаКлиенте
Процедура ИспользоватьПроксиПриИзменении(Элемент)
	
	Элементы.ПараметрыПрокси.Доступность = ?(ИспользоватьПрокси, Истина, Ложь);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Сохранение настроек прокси
//
&НаСервереБезКонтекста
Процедура СохранитьНастройкиПроксиСервера(НастройкаПроксиНаКлиенте, НастройкаПроксиСервера)
	
	Если НастройкаПроксиНаКлиенте Тогда
		ХранилищеОбщихНастроек.Сохранить("НастройкаПроксиСервера", , НастройкаПроксиСервера);
		ОбновитьПовторноИспользуемыеЗначения();
	Иначе
		ПолучениеФайловИзИнтернета.СохранитьНастройкиПроксиНаСервере1СПредприятие(НастройкаПроксиСервера);
	КонецЕсли;
	
КонецПроцедуры

