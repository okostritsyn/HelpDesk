////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ПрочитатьЗначенияПараметров();
	
	ПредставлениеСВидом = НаименованиеВида + ": " + Представление;
	Заголовок = НаименованиеВида;
	
	БылиНажатыКнопкиЗакрытия = Ложь;
	
	Если НЕ ПустаяСтрока(Параметры.Заголовок) Тогда
		АвтоЗаголовок = Ложь;
		Заголовок = Параметры.Заголовок;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиент.ЗапроситьПодтверждениеЗакрытияФормы(Отказ, ЗавершениеРаботы, Модифицированность, БылиНажатыКнопкиЗакрытия,ТекстПредупреждения); 
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ПолеПриИзменении(Элемент)
	
	Если Не ПустаяСтрока(КодСтраны) И Лев(КодСтраны, 1) <> "+" Тогда
		КодСтраны = "+" + КодСтраны;
	КонецЕсли;
	
	Представление = УправлениеКонтактнойИнформациейКлиент.СформироватьПредставлениеТелефона(КодСтраны, КодГорода, НомерТелефона, Добавочный, Комментарий);
	ПредставлениеСВидом = НаименованиеВида + ": " + Представление;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОКВыполнить()
	
	БылиНажатыКнопкиЗакрытия = Истина;
	Закрыть(ПолучитьЗначенияПараметров());
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтменаВыполнить()
	
	БылиНажатыКнопкиЗакрытия = Истина;
	Закрыть();
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура ПрочитатьЗначенияПараметров()
	
	Представление    = Параметры.Представление;
	НаименованиеВида = Строка(Параметры.Вид);
	
	Для Каждого ЭлементТелефона Из Параметры.ЗначенияПолей Цикл
		ЭтотОбъект[ЭлементТелефона.Представление] = ЭлементТелефона.Значение;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьЗначенияПараметров()
	
	ЗначенияПолей = Новый СписокЗначений;
	ЗначенияПолей.Добавить(КодСтраны,     "КодСтраны");
	ЗначенияПолей.Добавить(КодГорода,     "КодГорода");
	ЗначенияПолей.Добавить(НомерТелефона, "НомерТелефона");
	ЗначенияПолей.Добавить(Добавочный,    "Добавочный");
	ЗначенияПолей.Добавить(Комментарий,   "Комментарий");
	
	Результат = Новый Структура;
	Результат.Вставить("ЗначенияПолей", ЗначенияПолей);
	Результат.Вставить("Представление", Представление);
	
	Возврат Результат;
	
КонецФункции

