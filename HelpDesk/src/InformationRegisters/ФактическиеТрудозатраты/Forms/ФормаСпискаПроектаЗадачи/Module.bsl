
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИсточникСписка = Параметры.ПроектЗадача;
	
	Если ТипЗнч(Параметры.ПроектЗадача) = Тип("СправочникСсылка.Проекты") Тогда 
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор,
			"Проект",
			Параметры.ПроектЗадача);
		
		Элементы.Проект.Видимость = Ложь;
		
	Иначе
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор,
			"Проект",
			Параметры.ПроектЗадача.Владелец);
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор,
			"ПроектнаяЗадача",
			Параметры.ПроектЗадача);
		
		Элементы.Проект.Видимость = Ложь;	
		Элементы.ПроектнаяЗадача.Видимость = Ложь;
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередУдалением(Элемент, Отказ)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(ТекущиеДанные.ЕжедневныйОтчет) Тогда 
		Отказ = Истина;
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Нельзя удалить запись, так как она отражена в ежедневном отчете %1'"),
			Строка(ТекущиеДанные.ЕжедневныйОтчет));
		ПоказатьПредупреждение(Неопределено, ТекстСообщения);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПослеУдаления(Элемент)
	
	Оповестить("Изменение_ФактическиеТрудозатратыПроектнойЗадачи", ИсточникСписка, ЭтотОбъект);
		
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Источник", ИсточникСписка);
	
	ОткрытьФорму("РегистрСведений.ФактическиеТрудозатраты.ФормаЗаписи", ПараметрыФормы, Элементы.Список);
	
КонецПроцедуры
