////////////////////////////////////////////////////////////////////////////////
// Обработчики команд

&НаКлиенте
Процедура ЗаписатьВыполнить()
	
	ОчиститьСообщения();
	
	Если Объект.БлокировкаУстановкиСоединенийВключена Тогда
		
		// проверки возможности установки блокировки
		Если ЗначениеЗаполнено(Объект.ОкончаниеДействияБлокировки) И Объект.НачалоДействияБлокировки > Объект.ОкончаниеДействияБлокировки Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru='Дата окончания блокировки не может быть меньше даты начала блокировки. Блокировка не установлена.';uk='Дата закінчення блокування не може бути менше дати початку блокування. Блокування не встановлене.'"),,
				"Объект.ОкончаниеДействияБлокировки");
			Возврат;
		КонецЕсли;
		
		Попытка
			ПроверитьПредусловияБлокировки();
		Исключение
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ИнформацияОбОшибке().Описание);
			Возврат;
		КонецПопытки;
		
		ТекстВопроса = НСтр("ru='После установки блокировки в информационной базе будет запущен"
"режим завершения работы всех пользователей (включая Вас)"
"на период действия блокировки."
"Вы действительно хотите установить блокировку?';uk='Після установки блокування в інформаційній базі буде запущений"
"режим завершення роботи всіх користувачів (включаючи Вас)"
"на період дії блокування."
"Ви дійсно хочете встановити блокування?'");
		
		ЗаголовокВопроса = НСтр("ru='Установка блокировки соединений с информационной базой';uk=""Встановлення блокування з'єднань із інформаційною базою""");
		
		Ответ = Неопределено;

		
		ПоказатьВопрос(Новый ОписаниеОповещения("ЗаписатьВыполнитьЗавершение", ЭтотОбъект), ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена, 60, КодВозвратаДиалога.Нет,
		               ЗаголовокВопроса);
        Возврат;
		
	КонецЕсли;
	
	ЗаписатьВыполнитьФрагмент();
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьВыполнитьЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Ответ = РезультатВопроса;
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	
	ЗаписатьВыполнитьФрагмент();

КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьВыполнитьФрагмент()
	
	УстановитьСнятьБлокировку();
	
	СоединенияИБКлиент.УстановитьОбработчикиОжиданияЗавершенияРаботыПользователей(
	Объект.БлокировкаУстановкиСоединенийВключена);

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Вспомогательные процедуры

&НаСервере
Процедура УстановитьСнятьБлокировку()
	
	РеквизитФормыВЗначение("Объект").ВыполнитьУстановку();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
		Элементы.ПараметрыАдминистрированияИнформационнойБазы.Видимость = Ложь;
	КонецЕсли;
	
	Обработка = РеквизитФормыВЗначение("Объект");
	Обработка.ПолучитьПараметрыБлокировки();
	
	ЗначениеВРеквизитФормы(Обработка, "Объект");
	
КонецПроцедуры

&НаКлиенте
Процедура АктивныеПользователи(Команда)
	
	ОткрытьФорму("Обработка.АктивныеПользователи.Форма",, ЭтотОбъект);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПроверитьПредусловияБлокировки()
	
	Если ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
		НазванияСоединенийИБ = "";
		Если НЕ АктивныТолькоКлиентскиеПриложения(НазванияСоединенийИБ) Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Существуют активные сеансы, которые невозможно завершить. Блокировка не установлена."
"%1';uk='Існують активні сеанси, які неможливо завершити. Блокування не встановлене."
"%1'"), 
				НазванияСоединенийИБ);
			ВызватьИсключение ТекстСообщения;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция АктивныТолькоКлиентскиеПриложения(ИменаАктивныхСеансов)
	
	Результат = Истина;
	НазванияСоединенийИБ = "";
	НомерТекущегоСеанса = НомерСеансаИнформационнойБазы();
	Для каждого Сеанс Из ПолучитьСеансыИнформационнойБазы() Цикл
		Если Сеанс.НомерСеанса = НомерТекущегоСеанса Тогда
			Продолжить;
		КонецЕсли;
		Если Сеанс.ИмяПриложения <> "1CV8" И Сеанс.ИмяПриложения <> "1CV8C" И
			Сеанс.ИмяПриложения <> "WebClient" Тогда
			ИменаАктивныхСеансов = ИменаАктивныхСеансов + Символы.ПС + " - " + Сеанс;
			Результат = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ПараметрыАдминистрированияИнформационнойБазы(Команда)
	
	ОткрытьФорму("ОбщаяФорма.ПараметрыАдминистрированияСервернойИБ");
	
КонецПроцедуры
