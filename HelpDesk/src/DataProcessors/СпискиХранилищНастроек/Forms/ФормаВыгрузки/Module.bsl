
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ИзменитьРежимОбработки(ЭтоКлиент);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	КлючОбъекта      = Параметры.КлючОбъекта;
	КлючНастроек     = Параметры.КлючНастроек;
	Пользователь     = Параметры.Пользователь;
	ТекущееХранилище = Параметры.ТекущееХранилище;
	
	Если Параметры.РежимВыгрузки Тогда
		Элементы.ГруппаСтраниц.ТекущаяСтраница = Элементы.ГруппаВыгрузка;
	Иначе
		Элементы.ГруппаСтраниц.ТекущаяСтраница = Элементы.ГруппаЗагрузка;
		ЭтаФорма.Заголовок = "Загрузка настроек из файла";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НаКлиенте(Команда)
	
	Если Не ЭтоКлиент Тогда
		
		ЭтоКлиент = Истина;
		
		ИзменитьРежимОбработки(ЭтоКлиент);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НаСервере(Команда)
	
	Если ЭтоКлиент Тогда
		
		ЭтоКлиент = Ложь;
		
		ИзменитьРежимОбработки(ЭтоКлиент);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	
	АдресФайлаВХранилище = "";
	ОшибкаЗаписи = "";
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("КлючОбъекта",?(ПустаяСтрока(КлючОбъекта),Неопределено,КлючОбъекта));
	СтруктураПараметров.Вставить("КлючНастроек",?(ПустаяСтрока(КлючНастроек),Неопределено,КлючНастроек));
	СтруктураПараметров.Вставить("Пользователь",?(ПустаяСтрока(Пользователь),Неопределено,Пользователь));
	СтруктураПараметров.Вставить("ТекущееХранилище",ТекущееХранилище);
	СтруктураПараметров.Вставить("ЭтоКлиент",ЭтоКлиент);
	
	Если Не ЭтоКлиент Тогда
		ВыборФайла(СтруктураПараметров);
	Иначе
		АдресФайлаВХранилище = ВыгрузитВФайлНаСервере(СтруктураПараметров, ОшибкаЗаписи);
		Если ПустаяСтрока(ОшибкаЗаписи) Тогда
			Если Не ПустаяСтрока(АдресФайлаВХранилище) Тогда
				ИмяСохраняемогоФайла = НСтр("ru = 'Файл выгрузки.xml'");
				Если ПолучитьФайл(АдресФайлаВХранилище,ИмяСохраняемогоФайла) Тогда
					Оповещение = Новый ОписаниеОповещения("ПослеПредупреждения",ЭтаФорма);
					ПоказатьПредупреждение(Оповещение,"Выгружено "+ СтруктураПараметров.КоличествоЗаписей+" записей.");
				КонецЕсли;
				
			КонецЕсли;
		Иначе
			Сообщить(ОшибкаЗаписи);
		КонецЕсли;
		
	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Процедура Загрузить(Команда)
	
	АдресФайла = "";
	ИмяФайлаДляРасширения = "";
		
	Если ЭтоКлиент Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ВыполнитьЗагрузкуИзФормыЗавершение", ЭтотОбъект);
		НачатьПомещениеФайла(ОписаниеОповещения, АдресФайла,Нстр("ru = 'Файл обмена'"),, Новый УникальныйИдентификатор);
		
	Иначе
		
		Если ПустаяСтрока(ИмяФайлаЗагрузки) Тогда
			Возврат;
		КонецЕсли;
		
		ВыполнитьЗагрузкуИзФормыЗавершение(Истина, АдресФайла, ИмяФайлаЗагрузки, Неопределено);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементов

&НаКлиенте
Процедура ИмяФайлаЗагрузкиОткрытие(Элемент, СтандартнаяОбработка)
	
	ОткрытьВПриложении(Элемент.ТекстРедактирования, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяФайлаЗагрузкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);

	ДиалогВыбораФайла.Фильтр = "Файл данных (*.xml)|*.xml";
	ДиалогВыбораФайла.Расширение = "xml";
	ДиалогВыбораФайла.Заголовок = НСтр("ru = 'Выберите файл'");
	ДиалогВыбораФайла.ПредварительныйПросмотр = Ложь;
	ДиалогВыбораФайла.ИндексФильтра = 0;
	ДиалогВыбораФайла.ПроверятьСуществованиеФайла = Истина;
	
	Оповещение = Новый ОписаниеОповещения("ПослеВыбораФайлаЗагрузки",ЭтаФорма);
	
	ДиалогВыбораФайла.Показать(Оповещение);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыполнитьЗагрузкуИзФормыЗавершение(Результат, Адрес, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
	
	Если Результат ТОгда
		
		Состояние(НСтр("ru = 'Выполняется загрузка данных. Пожалуйста, подождите...'"));
		ВыполнитьЗагрузкуНаСервере(Адрес, ВыбранноеИмяФайла);
			
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьЗагрузкуНаСервере(АдресФайла, ВыбранноеИмяФайла)
	
	ИмяЗагружаемогоФайлаНаСервере = ИмяФайлаНаСервереИлиКлиенте(ВыбранноеИмяФайла ,АдресФайла);
	
	Если ИмяЗагружаемогоФайлаНаСервере = Неопределено Тогда
		
		Возврат;
				
	КонецЕсли;
	

	Результат = ВыполнитьЗагрузку(ИмяЗагружаемогоФайлаНаСервере);
	
	Попытка
		
		Если Не ПустаяСтрока(АдресФайла) И Не ЭтоКлиент Тогда
			УдалитьФайлы(ИмяЗагружаемогоФайлаНаСервере);
		КонецЕсли;
		
	Исключение
		
	КонецПопытки;
	
	ТекстОшибки = Результат.ТекстОшибки;
	
	Если ПустаяСтрока(ТекстОшибки) Тогда
		ТекстСообщения = "Загружено " + Результат.ЗаписейЗаписано + " из " + Результат.ЗаписейВсего + " настроек.";
		СообщитьПользователю(ТекстСообщения);
	Иначе 
		СообщитьПользователю(ТекстОшибки);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ВыполнитьЗагрузку(ИмяЗагружаемогоФайла)
	
	Перем Менеджер;
	
	СтруктураВозврата = Новый Структура;
	
	ИмяХранилища = "";
	ТекстОшибки = "";
	
	ФайлОбмена = Новый ЧтениеXML();
	Попытка
		ФайлОбмена.ОткрытьФайл(ИмяЗагружаемогоФайла);
	Исключение
		ТекстОшибки = ОписаниеОшибки();
		СтруктураВозврата.Вставить("ТекстОшибки",ТекстОшибки);
		Возврат СтруктураВозврата;
	КонецПопытки;
	
	
	ЭтоНачало  = Истина;
	КоличествоЗаписей = 0;
	КоличествоЗаписейЗаписано = 0;
	Запись = Ложь;
	НастройкиПрочитаны = Истина;
	
	КлючОбъекта = "";
	КлючНастроек = "";
	Пользователь = "";
	Представление = "";
	Настройки = Неопределено;
	
	Пока ФайлОбмена.Прочитать() Цикл
        Если ФайлОбмена.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
            ТекстНачало = ФайлОбмена.Имя;
			Если ЭтоНачало Тогда
				ИмяХранилища = ТекстНачало;
				Менеджер = Вычислить(ИмяХранилища);
				ЭтоНачало = Ложь;
			КонецЕсли;
			Если ТекстНачало = "Запись" Тогда
				Запись = Истина;
			КонецЕсли;
			
        КонецЕсли;    

        Если ФайлОбмена.ТипУзла = ТипУзлаXML.Текст Тогда
			Текст = ФайлОбмена.Значение;
			Если Запись Тогда
				Если ТекстНачало = "КлючОбъекта" Тогда
					КлючОбъекта = Текст;
				ИначеЕсли ТекстНачало = "КлючНастроек" Тогда
					КлючНастроек = Текст;
				ИначеЕсли ТекстНачало = "Пользователь" Тогда
					Пользователь = Текст;	
				ИначеЕсли ТекстНачало = "Представление" Тогда
					Представление = Текст;
				ИначеЕсли ТекстНачало = "Настройки" Тогда
					Попытка 
						Настройки = ЗначениеИзСтрокиВнутр(Текст);
						НастройкиПрочитаны = Истина;
					Исключение
						Настройки = Ложь;
					КонецПопытки;
				КонецЕсли;
				
			КонецЕсли;
			
        КонецЕсли;    

        Если ФайлОбмена.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
			ТекстКонец = ФайлОбмена.Имя;
			Если ТекстКонец = "Запись" Тогда
				Запись = Ложь;
				Если НастройкиПрочитаны Тогда
					
					ОписаниеНастроек = Новый ОписаниеНастроек;
					ОписаниеНастроек.КлючНастроек = КлючНастроек;
					ОписаниеНастроек.КлючОбъекта = КлючОбъекта;
					ОписаниеНастроек.Пользователь = Пользователь;
					ОписаниеНастроек.Представление = Представление;
					
					Попытка
						Менеджер.Сохранить(КлючОбъекта, КлючНастроек, Настройки, ОписаниеНастроек, Пользователь);
						КоличествоЗаписейЗаписано = КоличествоЗаписейЗаписано + 1;
					Исключение
						
					КонецПопытки;
						
				КонецЕсли;
				КоличествоЗаписей = КоличествоЗаписей + 1;
			КонецЕсли;
			Если ТекстКонец = ИмяХранилища Тогда
				ЭтоНачало = Истина;
			КонецЕсли;	
		КонецЕсли;
		
    КонецЦикла;    

    ФайлОбмена.Закрыть();
	
	СтруктураВозврата.Вставить("ТекстОшибки",ТекстОшибки);
	СтруктураВозврата.Вставить("ЗаписейЗаписано",КоличествоЗаписейЗаписано);
	СтруктураВозврата.Вставить("ЗаписейВсего",КоличествоЗаписей);
	
	Возврат СтруктураВозврата;
	
КонецФункции

&НаСервере
Функция ИмяФайлаНаСервереИлиКлиенте(ВыбранноеИмяФайла ,Знач АдресФайла = "")
	
	ИмяФайла = Неопределено;
	
	Если ЭтоКлиент Тогда
		
			
		Расширение = ".xml";
		ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресФайла);
		АдресНаСервере = ПолучитьИмяВременногоФайла(Расширение);
		ДвоичныеДанные.Записать(АдресНаСервере);
		ИмяФайла = АдресНаСервере;

		
	Иначе
		
		ФайлНаСервере = Новый Файл(ВыбранноеИмяФайла);
		
		Если Не ФайлНаСервере.Существует() Тогда
			
			СообщитьПользователю(НСтр("ru = 'Указанный файл не существует.'"));
			
		Иначе
			
			ИмяФайла = ВыбранноеИмяФайла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ИмяФайла;
	
КонецФункции

&НаКлиенте
Процедура ИзменитьРежимОбработки(РежимРаботы)
	
	ГруппаРежима = КоманднаяПанель.ПодчиненныеЭлементы.РежимОбработки.ПодчиненныеЭлементы;
	
	ГруппаРежима.ФормаНаКлиенте.Пометка = РежимРаботы;
	ГруппаРежима.ФормаНаСервере.Пометка = Не РежимРаботы;
	
	КоманднаяПанель.ПодчиненныеЭлементы.РежимОбработки.Заголовок = 
	?(РежимРаботы, НСтр("ru = 'Режим работы (на клиенте)'"), НСтр("ru = 'Режим работы (на сервере)'"));
	
	Элементы.ИмяФайлаЗагрузки.Видимость = Не РежимРаботы;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПредупреждения(ДопПараметры) Экспорт
				
КонецПроцедуры

&НасервереБезКонтекста
Функция ПолучитьЗаписатьХранилище(СтруктураПараметров)
	
	СтрокаОшибки = "";
	
	ТекущееХранилище = СтруктураПараметров.ТекущееХранилище;
	
	МенеджерХранилища = Вычислить(ТекущееХранилище);
		
	СтруктураОтбора = Новый Структура;
	Если НЕ СтруктураПараметров.КлючОбъекта = Неопределено Тогда
		СтруктураОтбора.Вставить("КлючОбъекта",СтруктураПараметров.КлючОбъекта);
	КонецЕсли;
	Если НЕ СтруктураПараметров.КлючНастроек = Неопределено Тогда
		СтруктураОтбора.Вставить("КлючНастроек",СтруктураПараметров.КлючНастроек);
	КонецЕсли;
	Если НЕ СтруктураПараметров.Пользователь = Неопределено Тогда
		СтруктураОтбора.Вставить("Пользователь",СтруктураПараметров.Пользователь);
	КонецЕсли;
	
	ЗаписьXML = Новый ЗаписьXML;
	Попытка
		ЗаписьXML.ОткрытьФайл(СтруктураПараметров.ИмяФайла);
	Исключение
		СтрокаОшибки = ОписаниеОшибки();
		Возврат СтрокаОшибки;
	КонецПопытки;
	
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	
	ЗаписьXML.ЗаписатьНачалоЭлемента(ТекущееХранилище);
	
	Если СтруктураОтбора.Количество() = 0 Тогда
		Выборка = МенеджерХранилища.Выбрать();
	Иначе
		Выборка = МенеджерХранилища.Выбрать(СтруктураОтбора);
	КонецЕсли;
	КоличествоЗаписей = 0;
	
	Пока Выборка.Следующий() Цикл
		
		ЗаписьXML.ЗаписатьНачалоЭлемента("Запись"); //Запись начало
		
		ЗаписьXML.ЗаписатьНачалоЭлемента("КлючОбъекта");
		ЗаписьXML.ЗаписатьТекст(Выборка.КлючОбъекта);
		ЗаписьXML.ЗаписатьКонецЭлемента();
		
		ЗаписьXML.ЗаписатьНачалоЭлемента("КлючНастроек");
		ЗаписьXML.ЗаписатьТекст(Выборка.КлючНастроек);
		ЗаписьXML.ЗаписатьКонецЭлемента();
		
		ЗаписьXML.ЗаписатьНачалоЭлемента("Пользователь");
		ЗаписьXML.ЗаписатьТекст(Выборка.Пользователь);
		ЗаписьXML.ЗаписатьКонецЭлемента();
		
		ЗаписьXML.ЗаписатьНачалоЭлемента("Настройки");
		ЗаписьXML.ЗаписатьТекст(ЗначениеВСтрокуВнутр(Выборка.Настройки));
		ЗаписьXML.ЗаписатьКонецЭлемента();
		
		ЗаписьXML.ЗаписатьНачалоЭлемента("Представление");
		ЗаписьXML.ЗаписатьТекст(Выборка.Представление);
		ЗаписьXML.ЗаписатьКонецЭлемента();
		
		ЗаписьXML.ЗаписатьКонецЭлемента(); //Запись Конец
		
		КоличествоЗаписей = КоличествоЗаписей + 1;
	КонецЦикла;
	
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
	Попытка
		ЗаписьXML.Закрыть();
	Исключение
		СтрокаОшибки = ОписаниеОшибки();
	КонецПопытки;
	
	СтруктураПараметров.Вставить("КоличествоЗаписей",КоличествоЗаписей);
	
	Возврат СтрокаОшибки;
	
КонецФункции

&НаКлиенте
Процедура ВыборФайла(СтруктураПараметров)
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);

	ДиалогВыбораФайла.Фильтр = "Файл данных (*.xml)|*.xml";
	ДиалогВыбораФайла.Расширение = "xml";
	ДиалогВыбораФайла.Заголовок = НСтр("ru = 'Выберите файл'");
	ДиалогВыбораФайла.ПредварительныйПросмотр = Ложь;
	ДиалогВыбораФайла.ИндексФильтра = 0;
	ДиалогВыбораФайла.ПроверятьСуществованиеФайла = Ложь;
	
	Оповещение = Новый ОписаниеОповещения("ПослеВыбораФайла",ЭтаФорма,СтруктураПараметров);
	
	ДиалогВыбораФайла.Показать(Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораФайла(МассивФайлов, СтруктураПараметров) Экспорт
	
	ИмяФайла = "";
	Если НЕ МассивФайлов = Неопределено Тогда
		Если НЕ МассивФайлов.Количество() = 0 Тогда
			ИмяФайла = МассивФайлов[0];
			СтруктураПараметров.Вставить("ИмяФайла",ИмяФайла);
			ОшибкаЗаписи = ПолучитьЗаписатьХранилище(СтруктураПараметров);
			Если ПустаяСтрока(ОшибкаЗаписи) Тогда
				Оповещение = Новый ОписаниеОповещения("ПослеПредупреждения",ЭтаФорма);
				ПоказатьПредупреждение(Оповещение,"Выгружено "+ СтруктураПараметров.КоличествоЗаписей+" записей.");
			Иначе
				Сообщить(ОшибкаЗаписи);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры

&НасервереБезКонтекста
Функция ВыгрузитВФайлНаСервере(СтруктураПараметров,ОшибкаЗаписи)
	
	АдресФайлаДанных = "";
	ИмяФайла = ПолучитьИмяВременногоФайла("xml");
	СтруктураПараметров.Вставить("ИмяФайла",ИмяФайла);
	ОшибкаЗаписи = ПолучитьЗаписатьХранилище(СтруктураПараметров);
	Если ПустаяСтрока(ОшибкаЗаписи) Тогда
		Файл = Новый Файл(ИмяФайла);
		Если Файл.Существует() Тогда
			АдресФайлаДанных = ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(ИмяФайла), Новый УникальныйИдентификатор);
			УдалитьФайлы(ИмяФайла);
		КонецЕсли;	
	КонецЕсли;
	
	Возврат АдресФайлаДанных;
	
КонецФункции

&НаКлиенте
Процедура ОткрытьВПриложении(ИмяФайла, СтандартнаяОбработка = Ложь)

	Файл = Новый Файл(ИмяФайла);
	
	Если Файл.Существует() Тогда
		
		ЗапуститьПриложение(ИмяФайла);
		
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораФайлаЗагрузки(МассивФайлов, СтруктураПараметров) Экспорт
	
	ИмяФайлаЗагрузки = "";
	Если НЕ МассивФайлов = Неопределено Тогда
		Если НЕ МассивФайлов.Количество() = 0 Тогда
			ИмяФайлаЗагрузки = МассивФайлов[0];
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура СообщитьПользователю(Текст, ПутьКДанным = "")
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = Текст;
	Сообщение.ПутьКДанным = ПутьКДанным;
	Сообщение.Сообщить();
	
КонецПроцедуры

#КонецОбласти
