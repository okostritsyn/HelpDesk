
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Параметры.Пользователь) Тогда
		
		Подразделение = РаботаСПользователями.ПолучитьПодразделение(Параметры.Пользователь);
		Если Не Подразделение.Пустая() Тогда
			Элементы.СтруктураПредприятия.ТекущаяСтрока = Подразделение;
			Элементы.Список.ТекущаяСтрока = Параметры.Пользователь;
		КонецЕсли;
		
	КонецЕсли;	
	Список.Параметры.УстановитьЗначениеПараметра("Подразделение", Элементы.СтруктураПредприятия.ТекущаяСтрока);
	
	ИспользоватьИерархию = Истина;
	УстановитьИерархию(ИспользоватьИерархию);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	УстановитьИерархию(Настройки["ИспользоватьИерархию"]);
	
КонецПроцедуры

&НаКлиенте
Процедура СтруктураПредприятияПриАктивизацииСтроки(Элемент)
	
	ПодключитьОбработчикОжидания("ОбработкаОжидания", 0.2, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОжидания()
	
	Если Элементы.СтруктураПредприятия.ТекущаяСтрока <> Неопределено Тогда 
		Список.Параметры.УстановитьЗначениеПараметра("Подразделение", Элементы.СтруктураПредприятия.ТекущаяСтрока);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьИерархию(Команда)
	
	ИспользоватьИерархию = Не ИспользоватьИерархию;
	Если ИспользоватьИерархию И (Элементы.Список.ТекущиеДанные <> Неопределено) Тогда 
		
		Если Элементы.Список.ТекущиеДанные.Свойство("Подразделение") Тогда 
			Элементы.СтруктураПредприятия.ТекущаяСтрока = Элементы.Список.ТекущиеДанные.Подразделение;
		Иначе	
			Элементы.СтруктураПредприятия.ТекущаяСтрока = Неопределено;
		КонецЕсли;	
			
		Список.Параметры.УстановитьЗначениеПараметра("Подразделение", Элементы.СтруктураПредприятия.ТекущаяСтрока);
	КонецЕсли;	
	УстановитьИерархию(ИспользоватьИерархию);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьИерархию(Отметка)
	
	Если Отметка = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Элементы.ИспользоватьИерархию.Пометка = Отметка;
	Если Отметка = Истина Тогда 
		Элементы.СтруктураПредприятия.Видимость = Истина;
	Иначе
		Элементы.СтруктураПредприятия.Видимость = Ложь;
	КонецЕсли;
	Список.Параметры.УстановитьЗначениеПараметра("ИспользоватьИерархию", Отметка);
	
КонецПроцедуры	
