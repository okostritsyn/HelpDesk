
Процедура ПередЗаписью(Отказ)
	
	Если НЕ ОбменДанными.Загрузка Тогда
	
		ПометкаИБ = ПометкаУдаленияВИБ();
		
		Если ПометкаУдаления <> ПометкаИБ И Не Ссылка.Пустая() Тогда
			
			// Отбираем адресатов и пытаемся поставить им пометку удаления
			
			Запрос = Новый Запрос;
			Запрос.Текст = 
				"ВЫБРАТЬ
				|	ЛичныеАдресаты.Ссылка
				|ИЗ
				|	Справочник.ЛичныеАдресаты КАК ЛичныеАдресаты
				|ГДЕ
				|	ЛичныеАдресаты.Группа = &Ссылка";
			
			Запрос.УстановитьПараметр("Ссылка", Ссылка);
			
			Результат = Запрос.Выполнить();
			Выборка = Результат.Выбрать();
			Пока Выборка.Следующий() Цикл

				АдресатОбъект = Выборка.Ссылка.ПолучитьОбъект();
				АдресатОбъект.Заблокировать();
				АдресатОбъект.УстановитьПометкуУдаления(ПометкаУдаления);
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;	
	
КонецПроцедуры

// Возвращает текущее значение пометки удаления в информационной базе
Функция ПометкаУдаленияВИБ()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ГруппыЛичныхАдресатов.ПометкаУдаления
		|ИЗ
		|	Справочник.ГруппыЛичныхАдресатов КАК ГруппыЛичныхАдресатов
		|ГДЕ
		|	ГруппыЛичныхАдресатов.Ссылка = &Ссылка";

	Запрос.УстановитьПараметр("Ссылка", Ссылка);

	Результат = Запрос.Выполнить();

	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.ПометкаУдаления;
	КонецЕсли;	
	
	Возврат Неопределено;
	
КонецФункции
