
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	// Проверка реквизитов текущего плана 
	Если ЗначениеЗаполнено(ТекущийПланНачало) Тогда 
		ПроверяемыеРеквизиты.Добавить("ТекущийПланОкончание");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекущийПланОкончание) Тогда 
		ПроверяемыеРеквизиты.Добавить("ТекущийПланНачало");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекущийПланТрудозатраты) Тогда 
		ПроверяемыеРеквизиты.Добавить("ТекущийПланЕдиницаТрудозатрат");
	КонецЕсли;	
	
	// Проверка реквизитов факта 
	Если ЗначениеЗаполнено(ОкончаниеФакт) Тогда 
		ПроверяемыеРеквизиты.Добавить("НачалоФакт");
	КонецЕсли;
		
	// веха 
	Если Веха Тогда 
		ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("ТекущийПланДлительность"));
	КонецЕсли;	
	
	Если ТипОграничения = Перечисления.ТипыОграниченийПроектныхЗадач.НачалоНеПозднее 
	 Или ТипОграничения = Перечисления.ТипыОграниченийПроектныхЗадач.НачалоНеРанее
	 Или ТипОграничения = Перечисления.ТипыОграниченийПроектныхЗадач.ОкончаниеНеПозднее
	 Или ТипОграничения = Перечисления.ТипыОграниченийПроектныхЗадач.ОкончаниеНеРанее 
	 Или ТипОграничения = Перечисления.ТипыОграниченийПроектныхЗадач.ФиксированноеНачало
	 Или ТипОграничения = Перечисления.ТипыОграниченийПроектныхЗадач.ФиксированноеОкончание Тогда 
		ПроверяемыеРеквизиты.Добавить("ДатаОграничения");
	КонецЕсли;	
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка
		И ЗначениеЗаполнено(ОбменДанными.Отправитель) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Ссылка) Тогда
		ДополнительныеСвойства.Вставить("ЭтоНовый", Истина);
	КонецЕсли;
	
	РеквизитыСсылки = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(Ссылка,
		"ПометкаУдаления,
		|ТекущийПланТрудозатраты, 
		|ТекущийПланЕдиницаТрудозатрат,
		|ТекущийПланОкончание,
		|Родитель");
	
	ПредыдущаяПометкаУдаления = Ложь;
	Если Не Ссылка.Пустая() Тогда
		ПредыдущаяПометкаУдаления = РеквизитыСсылки.ПометкаУдаления;
		
		Если ПометкаУдаления <> ПредыдущаяПометкаУдаления Тогда 
			Если ПометкаУдаления Тогда
				ДополнительныеСвойства.Вставить("НужноПометитьНаУдалениеБизнесСобытия", Истина);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ТекущийРодитель = РеквизитыСсылки.Родитель;
	ДополнительныеСвойства.Вставить("ТекущийРодитель", ТекущийРодитель);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка
		И ЗначениеЗаполнено(ОбменДанными.Отправитель) Тогда
		Возврат;
	КонецЕсли;

	РаботаСПроектами.ПроверитьПредшественников(Ссылка);
	

	// установка признака суммарной задачи у родителя
	ТекущийРодитель = ДополнительныеСвойства.ТекущийРодитель;
	Если Родитель <> ТекущийРодитель Тогда 
		
		Если ЗначениеЗаполнено(ТекущийРодитель) Тогда 
			ЭтоСуммарнаяЗадача = РаботаСПроектами.ЭтоСуммарнаяЗадача(ТекущийРодитель);
			Если ЭтоСуммарнаяЗадача <> ТекущийРодитель.СуммарнаяЗадача Тогда 
				РодительОбъект = ТекущийРодитель.ПолучитьОбъект();
				ЗаблокироватьДанныеДляРедактирования(РодительОбъект.Ссылка);
				РодительОбъект.СуммарнаяЗадача = ЭтоСуммарнаяЗадача;
				РодительОбъект.Записать();
			КонецЕсли;
		КонецЕсли;	
		
		Если ЗначениеЗаполнено(Родитель) Тогда 
			ЭтоСуммарнаяЗадача = РаботаСПроектами.ЭтоСуммарнаяЗадача(Родитель);
			Если ЭтоСуммарнаяЗадача <> Родитель.СуммарнаяЗадача Тогда 
				РодительОбъект = Родитель.ПолучитьОбъект();
				ЗаблокироватьДанныеДляРедактирования(РодительОбъект.Ссылка);
				РодительОбъект.СуммарнаяЗадача = ЭтоСуммарнаяЗадача;
				РодительОбъект.Записать();
			КонецЕсли;
		КонецЕсли;	
		
	КонецЕсли;	
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ВстроеннаяПочтаКлиентСервер.ЭтоПисьмо(ДанныеЗаполнения) Тогда
		Предмет = ДанныеЗаполнения;
		Наименование = ОбщегоНазначения.ПолучитьЗначениеРеквизита(ДанныеЗаполнения, "Тема");
	КонецЕсли;	
	
КонецПроцедуры
