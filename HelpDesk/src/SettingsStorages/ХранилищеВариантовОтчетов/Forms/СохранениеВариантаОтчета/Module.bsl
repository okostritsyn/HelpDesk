
////////////////////////////////////////////////////////////////////////////////
// События формы
////////////////////////////////////////////////////////////////////////////////

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Перем КлючВарианта;
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	// Определяет возможность редактирования чужих вариантов
	//   и возможность изменения настроек видимости варианта
	ПравоМенятьЧужиеВарианты = Пользователи.ЭтоПолноправныйПользователь() ИЛИ РольДоступна("ДобавлениеИзменениеВариантовОтчетов");
	
 	УстановитьПривилегированныйРежим(Истина);
	Если НЕ ПравоМенятьЧужиеВарианты Тогда
		Элементы.ВариантАвтор.Доступность = Ложь;
		Элементы.ГруппаПользователи.Доступность = Ложь;
	КонецЕсли;
	
	ИсходныйКлючОбъекта  = Параметры.КлючОбъекта;                            
	ИсходныйКлючВарианта = Параметры.КлючТекущихНастроек;
	ТекущийПользователь  = Пользователи.ТекущийПользователь();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ВариантыОтчета.Администратор КАК Автор,
	|	ВариантыОтчета.Ссылка,
	|	ВариантыОтчета.Наименование КАК Наименование,
	|	ВариантыОтчета.КлючОбъекта,
	|	ВариантыОтчета.КлючВарианта,
	|	ПОДСТРОКА(ВариантыОтчета.Описание, 0, 1000) КАК Описание,
	|	ВариантыОтчета.БыстрыйДоступ,
	|	ВариантыОтчета.ТипВариантаОтчета,
	|	ВЫБОР
	|		КОГДА ВариантыОтчета.ТипВариантаОтчета В (&ТипыВыводимыхВариантов)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПоказыватьВСписке
	|ИЗ
	|	Справочник.ВариантыОтчетов КАК ВариантыОтчета
	|ГДЕ
	|	ВариантыОтчета.КлючОбъекта = &КлючОбъекта
	|	И ВариантыОтчета.ПометкаУдаления = ЛОЖЬ
	|	И ВариантыОтчета.Администратор = &ТекущийПользователь
	|
	|УПОРЯДОЧИТЬ ПО
	|	Наименование";
	
	Если ПравоМенятьЧужиеВарианты Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И ВариантыОтчета.Администратор = &ТекущийПользователь", "");
	КонецЕсли;
	
	ТипыВыводимыхВариантов = Новый Массив;
	ТипыВыводимыхВариантов.Добавить(Перечисления.ТипыВариантовОтчетов.Пользовательский);
	ТипыВыводимыхВариантов.Добавить(Перечисления.ТипыВариантовОтчетов.ВнешнийОтчет);
	
	Запрос.Параметры.Вставить("КлючОбъекта",            ИсходныйКлючОбъекта);
	Запрос.Параметры.Вставить("КлючВарианта",           ИсходныйКлючВарианта);
	Запрос.Параметры.Вставить("ТипыВыводимыхВариантов", ТипыВыводимыхВариантов);
	
	Варианты.Загрузить(Запрос.Выполнить().Выгрузить());
	
	СоздатьНовыйВариантОтчета = 1;
	
	ПоказываемыеВСписке = Варианты.НайтиСтроки(Новый Структура("ПоказыватьВСписке", Истина));
	ЕстьВариантыДляЗаписи = (ПоказываемыеВСписке.Количество() > 0);
	
	НайденныеВарианты = Варианты.НайтиСтроки(Новый Структура("КлючВарианта", ИсходныйКлючВарианта));
	Если НайденныеВарианты.Количество() > 0 Тогда
		
		// Позиционирование в таблице вариантов на исходном варианте
		Вариант = НайденныеВарианты[0];
		Элементы.Варианты.ТекущаяСтрока = Вариант.ПолучитьИдентификатор();
		
		// Заполнение параметров варианта по исходному варианту
		ВариантНаименование  = Вариант.Наименование;
		ВариантОписание      = Вариант.Описание;
		ВариантАвтор         = Вариант.Автор;
		ВариантБыстрыйДоступ = Вариант.БыстрыйДоступ;
		ВариантИсключенияБыстрогоДоступа.Загрузить(Вариант.Ссылка.ИсключенияБыстрогоДоступа.Выгрузить());
		ВариантПодсистемы.Загрузить(Вариант.Ссылка.Подсистемы.Выгрузить());
		
		// Перезапись варианта только если есть на это доступ
		Если ЕстьВариантыДляЗаписи И (ПравоМенятьЧужиеВарианты ИЛИ ВариантАвтор = ТекущийПользователь) Тогда
			СоздатьНовыйВариантОтчета = 0;
		КонецЕсли;
		
	Иначе
		
		// Заполнение параметров варианта параметрами по умолчанию
		ВариантАвтор = ТекущийПользователь; 
		ВариантБыстрыйДоступ = Ложь;
		ВариантИсключенияБыстрогоДоступа.Очистить();
		ВариантИсключенияБыстрогоДоступа.Добавить().Пользователь = ТекущийПользователь;
		
	КонецЕсли;
	
	Если НЕ ЕстьВариантыДляЗаписи Тогда
		// Переход к странице записи нового варианта
		Элементы.СоздатьНовыйВариантОтчета.Доступность = Ложь;
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаПараметровЗаписиВарианта;
		
		ПодготовитьПараметрыЗаписиВарианта(ЭтотОбъект);
	КонецЕсли;

	ЗаполнитьПодсистемы();
	
	ВидимостьДоступность(ЭтотОбъект);
	
	Элементы.Варианты.ОтборСтрок = Новый ФиксированнаяСтруктура("ПоказыватьВСписке", Истина);
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Контекстные вызовы сервера
////////////////////////////////////////////////////////////////////////////////

&НаСервере
Процедура ЗаполнитьПодсистемы()
	УстановитьПривилегированныйРежим(Истина);
	
	Дерево = Новый ДеревоЗначений;
	Дерево.Колонки.Добавить("ПутьКПодсистеме",  Новый ОписаниеТипов("Строка"));
	Дерево.Колонки.Добавить("Название",         Новый ОписаниеТипов("Строка"));
	Дерево.Колонки.Добавить("Предопределенная", Новый ОписаниеТипов("Булево"));
	Дерево.Колонки.Добавить("Использование",    Новый ОписаниеТипов("Булево"));
	
	ДобавитьВложенныеПодсистемы(Метаданные, Дерево, "");
	
	СписокВиртуальныхПодсистем = ВариантыОтчетовПереопределяемый.ОписаниеВиртуальныхПодсистем();
	Для Каждого ЭлементСписка Из СписокВиртуальныхПодсистем Цикл
		МассивПодсистем = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ЭлементСписка.Значение, "\");
		Если МассивПодсистем.Количество() > 0 Тогда
			СтрокаРодитель = Дерево;
			ПутьКПодсистеме = "";
			Для Каждого ИмяПодсистемы Из МассивПодсистем Цикл
				ПутьКПодсистеме = ПутьКПодсистеме + ?(ПутьКПодсистеме = "", "", "\") + ИмяПодсистемы;
				СтрокаДерева = СтрокаРодитель.Строки.Найти(ПутьКПодсистеме, "ПутьКПодсистеме");
				Если СтрокаДерева = Неопределено Тогда
					СтрокаДерева = СтрокаРодитель.Строки.Добавить();
					СтрокаДерева.ПутьКПодсистеме  = ПутьКПодсистеме;
					СтрокаДерева.Название         = "";
					СтрокаДерева.Предопределенная = Ложь;
					СтрокаДерева.Использование    = Ложь;
				КонецЕсли;
				СтрокаРодитель = СтрокаДерева;
			КонецЦикла;
			СтрокаДерева.Название = ЭлементСписка.Представление;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого СтрокаТаблицы Из ВариантПодсистемы Цикл
		Найденные = Дерево.Строки.НайтиСтроки(Новый Структура("ПутьКПодсистеме", СтрокаТаблицы.Подсистема), Истина);
		Для Каждого СтрокаДерева Из Найденные Цикл
			СтрокаДерева.Использование = Истина;
		КонецЦикла;
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(Дерево, "ДеревоПодсистем");
КонецПроцедуры

&НаСервере
Процедура ДобавитьВложенныеПодсистемы(ОбъектМетаданных, СтрокаРодитель, ПутьРодителя = "")
	УстановитьПривилегированныйРежим(Истина);
	
	Для Каждого Подсистема Из ОбъектМетаданных.Подсистемы Цикл
		
		Если Не Подсистема.ВключатьВКомандныйИнтерфейс тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаДерева = СтрокаРодитель.Строки.Добавить();
		СтрокаДерева.ПутьКПодсистеме  = ПутьРодителя + ?(ПутьРодителя = "", "", "\") + Подсистема.Имя;
		СтрокаДерева.Название         = Подсистема.Синоним;
		СтрокаДерева.Предопределенная = Ложь;
		СтрокаДерева.Использование    = Ложь;
		
		ДобавитьВложенныеПодсистемы(Подсистема, СтрокаДерева, СтрокаДерева.ПутьКПодсистеме);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция СохранитьВариант(СоздатьНовый = Истина)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаВыбораВарианта Тогда
		Вариант = Варианты.НайтиПоИдентификатору(Элементы.Варианты.ТекущаяСтрока);
		ВариантСсылка = Вариант.Ссылка;
	КонецЕсли;
	
	Если СоздатьНовый Тогда
		ВариантОбъект = Справочники.ВариантыОтчетов.СоздатьЭлемент();
		ВариантОбъект.КлючОбъекта  = ИсходныйКлючОбъекта;
		ВариантОбъект.КлючВарианта = Строка(Новый УникальныйИдентификатор());
		Если Метаданные.Отчеты.Найти(СтрЗаменить(ИсходныйКлючОбъекта, "Отчет.", "")) = Неопределено Тогда
			ВариантОбъект.ТипВариантаОтчета = Перечисления.ТипыВариантовОтчетов.ВнешнийОтчет;
		Иначе
			ВариантОбъект.ТипВариантаОтчета = Перечисления.ТипыВариантовОтчетов.Пользовательский;
		КонецЕсли;
	Иначе
		ВариантОбъект = ВыбранныйВариант.ПолучитьОбъект();
	КонецЕсли;
	
	ВариантОбъект.Администратор        = ВариантАвтор;
	ВариантОбъект.Наименование         = ВариантНаименование;
	ВариантОбъект.ПредставлениеОбъекта = ВариантНаименование;
	ВариантОбъект.Описание             = ВариантОписание;
	ВариантОбъект.БыстрыйДоступ        = ВариантБыстрыйДоступ;
	ВариантОбъект.ИсключенияБыстрогоДоступа.Загрузить(ВариантИсключенияБыстрогоДоступа.Выгрузить());
	ВариантОбъект.Подсистемы.Загрузить(ВариантПодсистемы.Выгрузить());
	
	ВариантОбъект.Записать();
	
	Возврат ВариантОбъект.КлючВарианта;
КонецФункции

&НаСервере
Процедура ПодготовитьПараметрыНастройкиПодсистем(ПараметрыФормы)
	
	ДеревоЗначений = ДанныеФормыВЗначение(ДеревоПодсистем, Тип("ДеревоЗначений"));
	ПараметрыФормы.АдресДереваПодсистем = ПоместитьВоВременноеХранилище(ДеревоЗначений, ЭтотОбъект.УникальныйИдентификатор);
	
КонецПроцедуры 

&НаСервере
Процедура ЗафиксироватьРезультатНастройкиПодсистем(ВозвращаемыеПараметры)
	
	ДеревоЗначений = ПолучитьИзВременногоХранилища(ВозвращаемыеПараметры.АдресДереваПодсистем);
	ЗначениеВРеквизитФормы(ДеревоЗначений, "ДеревоПодсистем");
	УдалитьИзВременногоХранилища(ВозвращаемыеПараметры.АдресДереваПодсистем);
	
КонецПроцедуры 

////////////////////////////////////////////////////////////////////////////////
// Вызовы клиента и сервера
////////////////////////////////////////////////////////////////////////////////

&НаКлиентеНаСервереБезКонтекста
Процедура ВидимостьДоступность(ЭтотОбъект, Изменения = "")
	Элементы = ЭтотОбъект.Элементы;
	
	Если Изменения = "" 
		 ИЛИ Изменения = "Варианты" 
		 ИЛИ Изменения = "ТекущаяСтраница" 
		 ИЛИ Изменения = "СоздатьНовыйВариантОтчета"
		Тогда
		
		// По умолчанию все кнопки неактивны
		ДоступностьКнопкиГотово = Ложь;
		ДоступностьКнопкиДалее  = Ложь;
		ДоступностьКнопкиНазад  = Ложь;
		ДоступностьВариантов    = Ложь;
		ДоступностьОписания     = Истина;
		ЗаголовокФормы          = "";
		
		Если Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаВыбораВарианта Тогда
			ЗаголовокФормы = НСтр("ru = 'Сохранить вариант отчета'");
			// Пользователь выбирает вариант
			Если ЭтотОбъект.СоздатьНовыйВариантОтчета = 1 Тогда
				ДоступностьКнопкиДалее = Истина;
			ИначеЕсли Элементы.Варианты.ТекущаяСтрока <> Неопределено Тогда
				ДоступностьВариантов = Истина;
				Вариант = ЭтотОбъект.Варианты.НайтиПоИдентификатору(Элементы.Варианты.ТекущаяСтрока);
				Если ЭтотОбъект.ПравоМенятьЧужиеВарианты 
						ИЛИ Вариант <> Неопределено И Вариант.Автор = ЭтотОбъект.ТекущийПользователь Тогда
					//
					ДоступностьКнопкиГотово = Истина;
					ДоступностьКнопкиДалее  = Истина;
				КонецЕсли;
			КонецЕсли;
		Иначе
			// Пользователь редактирует настройки варианта
			ДоступностьКнопкиНазад = Истина;
			ДоступностьКнопкиГотово = Истина;
			// Запрещено менять описание предопределенных вариантов
			Если ЭтотОбъект.СоздатьНовыйВариантОтчета = 1 Тогда
				ЗаголовокФормы = НСтр("ru = 'Сохранить новый вариант'");
			Иначе
				ЗаголовокФормы = НСтр("ru = 'Перезаписать вариант'");
				Если Элементы.Варианты.ТекущаяСтрока <> Неопределено Тогда
					Вариант = ЭтотОбъект.Варианты.НайтиПоИдентификатору(Элементы.Варианты.ТекущаяСтрока);
					Предопределенный = ПредопределенноеЗначение("Перечисление.ТипыВариантовОтчетов.Предопределенный");
					Отчет = ПредопределенноеЗначение("Перечисление.ТипыВариантовОтчетов.Отчет");
					Если Вариант.ТипВариантаОтчета = Предопределенный ИЛИ Вариант.ТипВариантаОтчета = Отчет Тогда
						ДоступностьОписания = Ложь;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Элементы.ФормаГотово.Доступность = ДоступностьКнопкиГотово;
		Элементы.ФормаДалее.Доступность  = ДоступностьКнопкиДалее;
		Элементы.ФормаНазад.Доступность  = ДоступностьКнопкиНазад;
		Элементы.Варианты.Доступность    = ДоступностьВариантов;
		Элементы.ВариантОписание.ТолькоПросмотр = НЕ ДоступностьОписания;
		ЭтотОбъект.Заголовок = ЗаголовокФормы;
	КонецЕсли;
	
	Если Изменения = "" ИЛИ Изменения = "БыстрыйДоступ" Тогда
		ЭтотОбъект.БыстрыйДоступ = ПредставлениеНастроекБыстрогоДоступа(ЭтотОбъект);
	КонецЕсли;
	
	Если Изменения = "" ИЛИ Изменения = "Подсистемы" Тогда
		ЭтотОбъект.Разделы = ПредставлениеРазделов(ЭтотОбъект);
	КонецЕсли;
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПодготовитьПараметрыЗаписиВарианта(ЭтотОбъект)
	Элементы = ЭтотОбъект.Элементы;
	
	Если НЕ Элементы.ФормаДалее.Доступность Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ТекущийПользователь = ЭтотОбъект.ТекущийПользователь;
	
	// Создание нового варианта от перезаписи текущего отличается
	//   только процессом получения объекта и заполнением ключей ключа варианта
	Если ЭтотОбъект.СоздатьНовыйВариантОтчета = 1 тогда
		
		ЭтотОбъект.ВариантАвтор = ТекущийПользователь; 
		ЭтотОбъект.ВариантБыстрыйДоступ = Ложь;
		ЭтотОбъект.ВариантИсключенияБыстрогоДоступа.Очистить();
		ЭтотОбъект.ВариантИсключенияБыстрогоДоступа.Добавить().Пользователь = ТекущийПользователь;
		
		ВидимостьДоступность(ЭтотОбъект, "БыстрыйДоступ");
		
	Иначе
		
		Идентификатор = Элементы.Варианты.ТекущаяСтрока;
		Если Идентификатор = Неопределено Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Выберите вариант.'"), , "Варианты");
			Возврат Ложь;
		КонецЕсли;
		
		Вариант = ЭтотОбъект.Варианты.НайтиПоИдентификатору(Идентификатор);
		Если Вариант = Неопределено Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Выберите вариант.'"), , "Варианты");
			Возврат Ложь;
		КонецЕсли;
		Если НЕ ЭтотОбъект.ПравоМенятьЧужиеВарианты И Вариант.Автор <> ТекущийПользователь Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Изменять вариант может только автор.'"), , "Варианты");
			Возврат Ложь;
		КонецЕсли;
		ЭтотОбъект.ВыбранныйВариант = Вариант.Ссылка;
		
		ЭтотОбъект.ВариантНаименование = Вариант.Наименование;
		ЭтотОбъект.ВариантОписание = Вариант.Описание;
		
	КонецЕсли;
	
	Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаПараметровЗаписиВарианта;
	
	ВидимостьДоступность(ЭтотОбъект, "ТекущаяСтраница");
	
	Возврат Истина;
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеНастроекБыстрогоДоступа(ЭтотОбъект)
	ВариантБыстрыйДоступ = ЭтотОбъект.ВариантБыстрыйДоступ;
	ВариантИсключенияБыстрогоДоступа = ЭтотОбъект.ВариантИсключенияБыстрогоДоступа;
	
	Результат = "";
	Для Каждого СтрокаТаблицы Из ВариантИсключенияБыстрогоДоступа Цикл
		ПредставлениеСтроки = СокрЛП(Строка(СтрокаТаблицы.Пользователь));
		Если ПредставлениеСтроки <> "" Тогда
			Если Результат = "" Тогда
				Результат = ?(ВариантБыстрыйДоступ, НСтр("ru = 'Все кроме:'") +" ", "");
				Результат = Результат + ПредставлениеСтроки;
			Иначе
				Результат = Результат +", "+ ПредставлениеСтроки;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если Результат = "" Тогда
		Результат = ?(ВариантБыстрыйДоступ, НСтр("ru = 'Все'"), НСтр("ru = 'Пользователи не выбраны'"));
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеРазделов(ЭтотОбъект)
	ВариантПодсистемы = ЭтотОбъект.ВариантПодсистемы;
	
	Результат = "";
	Для Каждого СтрокаТаблицы Из ВариантПодсистемы Цикл
		ПредставлениеСтроки = СокрЛП(СтрокаТаблицы.Название);
		Если ПредставлениеСтроки <> "" Тогда
			Результат = Результат + ПредставлениеСтроки + ", ";
		КонецЕсли;
	КонецЦикла;
	
	Если Результат = "" Тогда
		Результат = НСтр("ru = 'Разделы не выбраны'");
	Иначе
		Результат = Лев(Результат, СтрДлина(Результат)-2);
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Вызовы клиента
////////////////////////////////////////////////////////////////////////////////

&НаКлиенте
Процедура СохранитьВыполнить()
	
	Если НЕ Элементы.ФормаГотово.Доступность Тогда
		Возврат;
	КонецЕсли;
	
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаВыбораВарианта Тогда
		Если НЕ ПодготовитьПараметрыЗаписиВарианта(ЭтотОбъект) Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если ВариантНаименование = "" тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не задано наименование.'"), , "ВариантНаименование");
		Возврат;
	КонецЕсли;
	
	КлючСохраненногоВарианта = СохранитьВариант(СоздатьНовыйВариантОтчета = 1);
	Оповестить("Запись_ХранилищеВариантовОтчетов", Новый Структура, ЭтотОбъект);
	Закрыть(Новый ВыборНастроек(КлючСохраненногоВарианта));
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// События элементов управления формы
////////////////////////////////////////////////////////////////////////////////

&НаКлиенте
Процедура СписокНастроекПриАктивизацииСтроки(Элемент)
	ВидимостьДоступность(ЭтотОбъект, "Варианты");
КонецПроцедуры

&НаКлиенте
Процедура Далее(Команда)
	ПодготовитьПараметрыЗаписиВарианта(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Назад(Команда)
	Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаВыбораВарианта;
	ВидимостьДоступность(ЭтотОбъект, "ТекущаяСтраница");
КонецПроцедуры

&НаКлиенте
Процедура Готово(Команда)
	СохранитьВыполнить();
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНовыйВариантОтчетаПриИзменении(Элемент)
	ВидимостьДоступность(ЭтотОбъект, "СоздатьНовыйВариантОтчета");
КонецПроцедуры

&НаКлиенте
Процедура БыстрыйДоступНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ИсключенияБыстрогоДоступа = Новый СписокЗначений;
	Для Каждого СтрокаТаблицы Из ВариантИсключенияБыстрогоДоступа Цикл
		ИсключенияБыстрогоДоступа.Добавить(СтрокаТаблицы.Пользователь);
	КонецЦикла;
	ПараметрыФормы = Новый Структура("БыстрыйДоступ, ИсключенияБыстрогоДоступа", ВариантБыстрыйДоступ, ИсключенияБыстрогоДоступа);
	
	Структура = Неопределено;

	
	ОткрытьФорму("Справочник.ВариантыОтчетов.Форма.НастройкаБыстрогоДоступа", ПараметрыФормы, ЭтотОбъект,,,, Новый ОписаниеОповещения("БыстрыйДоступНажатиеЗавершение", ЭтотОбъект), РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
КонецПроцедуры

&НаКлиенте
Процедура БыстрыйДоступНажатиеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Структура = Результат;
	Если Структура = Неопределено тогда
		Возврат;
	КонецЕсли;
	
	ВариантБыстрыйДоступ = Структура.БыстрыйДоступ;
	
	ВариантИсключенияБыстрогоДоступа.Очистить();
	Для Каждого ЭлементСписка Из Структура.ИсключенияБыстрогоДоступа Цикл
		ВариантИсключенияБыстрогоДоступа.Добавить().Пользователь = ЭлементСписка.Значение;
	КонецЦикла;
	
	ВидимостьДоступность(ЭтотОбъект, "БыстрыйДоступ");

КонецПроцедуры

&НаКлиенте
Процедура РазделыНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура("АдресДереваПодсистем, Подсистемы, НовыйЭлементСправочника");
	ПараметрыФормы.Подсистемы              = ВариантПодсистемы;
	ПараметрыФормы.НовыйЭлементСправочника = (СоздатьНовыйВариантОтчета = 1);
	
	ПодготовитьПараметрыНастройкиПодсистем(ПараметрыФормы);
	
	Результат = Неопределено;

	
	ОткрытьФорму("Справочник.ВариантыОтчетов.Форма.НастройкаПодсистем", ПараметрыФормы, ЭтотОбъект,,,, Новый ОписаниеОповещения("РазделыНажатиеЗавершение", ЭтотОбъект), РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
КонецПроцедуры

&НаКлиенте
Процедура РазделыНажатиеЗавершение(Результат1, ДополнительныеПараметры) Экспорт
	
	Результат = Результат1;
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВариантПодсистемы.Очистить();
	Для Каждого СтрокаТаблицы Из Результат.СписокПодсистем Цикл
		ЗаполнитьЗначенияСвойств(ВариантПодсистемы.Добавить(), СтрокаТаблицы);
	КонецЦикла;
	
	ЗафиксироватьРезультатНастройкиПодсистем(Результат);
	
	ВидимостьДоступность(ЭтотОбъект, "Подсистемы");

КонецПроцедуры

&НаКлиенте
Процедура ВариантыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ПодготовитьПараметрыЗаписиВарианта(ЭтотОбъект);
КонецПроцедуры

