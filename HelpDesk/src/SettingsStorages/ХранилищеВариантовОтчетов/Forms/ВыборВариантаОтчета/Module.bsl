&НаКлиенте
Процедура ЗагрузитьВыполнить()
	
	Если Элементы.СписокВариантовОтчета.ТекущаяСтрока <> Неопределено Тогда 
		
		КлючТекущегоВарианта = Элементы.СписокВариантовОтчета.ТекущиеДанные.КлючВариантаОтчета;
		СписокВыбора = Новый СписокЗначений;
		ВыборНастроек = Новый ВыборНастроек(КлючТекущегоВарианта);
		Закрыть(ВыборНастроек);
		
	Иначе
		
		Закрыть();
		
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	КлючОбъекта               = Параметры.КлючОбъекта;
	КлючТекущихНастроек       = Параметры.КлючТекущихНастроек;
	СтандартныеНастройки      = Параметры.СтандартныеНастройки;
	ТекущийПользователь       = Параметры.ТекущийПользователь;
	ПоказыватьТолькоИзбранные = Параметры.ПоказыватьТолькоИзбранные;
		
	Если НЕ ЗначениеЗаполнено(ТекущийПользователь) тогда
		ТекущийПользователь           = Пользователи.ТекущийПользователь();
		Параметры.ТекущийПользователь = ТекущийПользователь;
	КонецЕсли;
	
	ЗаполнитьСписокВариантовОтчета();
	
	СтрокиТекВарианта = СписокВариантовОтчета.НайтиСтроки(Новый Структура("КлючВариантаОтчета", КлючТекущихНастроек));
	Если СтрокиТекВарианта.Количество() > 0 тогда
		Элементы.СписокВариантовОтчета.ТекущаяСтрока = СтрокиТекВарианта[0].ПолучитьИдентификатор();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВариантовОтчета(КлючВарианта = Неопределено)
	
	ВариантыОтчета = РеквизитФормыВЗначение("СписокВариантовОтчета");
	ВариантыОтчета.Очистить();
	ВариантыОтчета.Колонки.Добавить("ДатаИспользования");

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Справочник.Наименование КАК Наименование,
	|	Справочник.Ссылка,
	|	Справочник.КлючВарианта,
	|	Справочник.Администратор,
	|	Справочник.Администратор.Наименование КАК АдминистраторПредставление,
	|	ПОДСТРОКА(Справочник.Описание, 0, 1000) КАК Описание
	|ИЗ
	|	Справочник.ВариантыОтчетов КАК Справочник
	|ГДЕ
	|	Справочник.КлючОбъекта = &КлючОбъекта
	|	И Справочник.ПометкаУдаления = ЛОЖЬ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Наименование";

	Запрос.Параметры.Вставить("КлючОбъекта", КлючОбъекта);
	
 	УстановитьПривилегированныйРежим(истина);
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
 	УстановитьПривилегированныйРежим(ложь);

	Пока Выборка.Следующий() Цикл
		СтрокаСписка = ВариантыОтчета.Добавить();
		СтрокаСписка.Наименование       = Выборка.Наименование;
		СтрокаСписка.КлючВариантаОтчета = Выборка.КлючВарианта;
		СтрокаСписка.Ссылка             = Выборка.Ссылка;
		СтрокаСписка.Автор              = Выборка.Администратор;
		СтрокаСписка.АвторПредставление = Выборка.АдминистраторПредставление;
		СтрокаСписка.Описание           = Выборка.Описание;
	КонецЦикла;
	
	ДобавитьВариантыВнешнегоОтчета(КлючОбъекта, ВариантыОтчета);
	
	ЗначениеВРеквизитФормы(ВариантыОтчета, "СписокВариантовОтчета");
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьВариантыВнешнегоОтчета(КлючОбъекта, ВариантыОтчета)
	СтрокаВнешнийОтчет = "ВнешнийОтчет.";
	ДлинаСтрокиВнешнийОтчет = СтрДлина(СтрокаВнешнийОтчет);
	Если Лев(КлючОбъекта, ДлинаСтрокиВнешнийОтчет) <> СтрокаВнешнийОтчет Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		ВнешнийОтчет = ВнешниеОтчеты.Создать(Сред(КлючОбъекта, ДлинаСтрокиВнешнийОтчет + 1));
	Исключение
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Варианты отчетов. Подключение внешнего отчета'"),
			УровеньЖурналаРегистрации.Ошибка,
			,
			,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ошибка подключения внешнего отчета %1:'"),
				КлючОбъекта
			) + Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())
		);
		Возврат;
	КонецПопытки;
	
	СхемаКД = ВнешнийОтчет.СхемаКомпоновкиДанных;
	Если ТипЗнч(СхемаКД) <> Тип("СхемаКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ВариантНастроекКД Из СхемаКД.ВариантыНастроек Цикл
		Если ВариантыОтчета.Найти(ВариантНастроекКД.Имя, "КлючВариантаОтчета") = Неопределено Тогда
			СтрокаСписка = ВариантыОтчета.Добавить();
			СтрокаСписка.Наименование       = ВариантНастроекКД.Представление;
			СтрокаСписка.КлючВариантаОтчета = ВариантНастроекКД.Имя;
			СтрокаСписка.Ссылка             = Неопределено;
			СтрокаСписка.Автор              = Неопределено;
			СтрокаСписка.АвторПредставление = "";
			СтрокаСписка.Описание           = "";
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура СписокВариантовОтчетаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ЗагрузитьВыполнить();
КонецПроцедуры


