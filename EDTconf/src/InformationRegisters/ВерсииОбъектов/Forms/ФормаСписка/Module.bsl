////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ Список

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	Отказ = Истина;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура УдалитьЗаписи(Команда)
	
	ТекстВопроса = НСтр("ru = 'Удаление записей по версиям объектов может привести к невозможности 
		|выполнения анализа всей цепочки изменений этих объектов. Продолжить?'");
	Ответ = Неопределено;

	ПоказатьВопрос(Новый ОписаниеОповещения("УдалитьЗаписиЗавершение", ЭтотОбъект), ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет, НСтр("ru = 'Предупреждение'"));
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьЗаписиЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Ответ = РезультатВопроса;
	Если Ответ = КодВозвратаДиалога.Да Тогда
		УдалитьВерсииИЗРегистра(Элементы.Список.ВыделенныеСтроки);
	КонецЕсли;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура УдалитьВерсииИЗРегистра(знач МассивКлючейЗаписей)
	
	Для Каждого КлючЗаписи Из МассивКлючейЗаписей Цикл
		НаборЗаписей = РегистрыСведений.ВерсииОбъектов.СоздатьНаборЗаписей();
		
		НаборЗаписей.Отбор.Объект.Значение = КлючЗаписи.Объект;
		НаборЗаписей.Отбор.Объект.ВидСравнения = ВидСравнения.Равно;
		НаборЗаписей.Отбор.Объект.Использование = Истина;
		
		НаборЗаписей.Отбор.НомерВерсии.Значение = КлючЗаписи.НомерВерсии;
		НаборЗаписей.Отбор.НомерВерсии.ВидСравнения = ВидСравнения.Равно;
		НаборЗаписей.Отбор.НомерВерсии.Использование = Истина;
		
		НаборЗаписей.Записать(Истина);
	КонецЦикла;
	
	Элементы.Список.Обновить();
	
КонецПроцедуры
