////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.ОсновнойОбъектАдресации <> Неопределено Тогда
		ОсновнойОбъектАдресации = Параметры.ОсновнойОбъектАдресации;
	Иначе	
		Элементы.ОсновнойОбъектАдресации.Видимость = Ложь;
	КонецЕсли;
	
	ОбновитьДанныеЭлементов();
	ИспользоватьВнешниеЗадачиИБизнесПроцессы = СтандартныеПодсистемыПереопределяемый.ИспользоватьВнешниеЗадачиИБизнесПроцессы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "Запись_РолеваяАдресация" Тогда
		ОбновитьДанныеЭлементов();
 	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ПолеОсновнойОбъектАдресацииПриИзменении(Элемент)
	ОбновитьДанныеЭлементов();
КонецПроцедуры

 ////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ Список

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	НазначитьИсполнителей(Неопределено);
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ВсеНазначенияВыполнить(Команда)
	
	ОткрытьФорму("РегистрСведений.ИсполнителиЗадач.ФормаСписка", 
		Новый Структура("Отбор", Новый Структура("ОсновнойОбъектАдресации", ОсновнойОбъектАдресации)),
		ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьВыполнить(Команда)
	ОбновитьДанныеЭлементов();
КонецПроцедуры

&НаКлиенте
Процедура НазначитьИсполнителей(Команда)
	
	Назначение = Элементы.Список.ТекущиеДанные;
	Если Назначение = Неопределено Тогда
		ПоказатьПредупреждение(Неопределено, НСтр("ru = 'Необходимо выбрать роль в списке.'"));
		Возврат;
	КонецЕсли;
	
	Если ИспользоватьВнешниеЗадачиИБизнесПроцессы Тогда
		Если Назначение.ВнешняяРоль = Истина Тогда
			ПоказатьПредупреждение(Неопределено, НСтр("ru = 'Исполнители внешней роли должны быть определены в другой информационной базе.'"));
			Возврат;
		КонецЕсли;
	КонецЕсли;	

	ОткрытьФорму("РегистрСведений.ИсполнителиЗадач.Форма.АдресацияПоОбъектуИРоли", 
		Новый Структура("ОсновнойОбъектАдресации,Роль", 
			ОсновнойОбъектАдресации, 
			Назначение.РольСсылка),,,,, Неопределено, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
			
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура ОбновитьДанныеЭлементов()
	
	Если ОсновнойОбъектАдресации <> Неопределено И ОсновнойОбъектАдресации <> "" Тогда
		Элементы.ОсновнойОбъектАдресации.Заголовок = ОсновнойОбъектАдресации.Метаданные().ПредставлениеОбъекта;
	КонецЕсли;	
	ВыборкаЗапроса = БизнесПроцессыИЗадачиСервер.ВыбратьРолиСКоличествомИсполнителей(ОсновнойОбъектАдресации);
	СписокОбъект = РеквизитФормыВЗначение("Список");
	СписокОбъект.Очистить();
	Пока ВыборкаЗапроса.Следующий() Цикл
		ТипЗначения = ВыборкаЗапроса.ТипыОсновногоОбъектаАдресации.ТипЗначения;
		ВключаетТип = Истина;
		Если ОсновнойОбъектАдресации <> Неопределено Тогда
			ВключаетТип = ТипЗначения <> Неопределено И ТипЗначения.СодержитТип(ТипЗнч(ОсновнойОбъектАдресации));
		КонецЕсли;
		Если ВключаетТип Тогда
			НоваяСтрока = СписокОбъект.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаЗапроса, "Исполнители,Роль,РольСсылка,ВнешняяРоль"); 
		КонецЕсли;
	КонецЦикла;
	СписокОбъект.Сортировать("Роль");
	Для каждого СтрокаСписка Из СписокОбъект Цикл
		Если СтрокаСписка.Исполнители = 0 Тогда
			СтрокаСписка.ИсполнителиСтрока = ?(СтрокаСписка.ВнешняяРоль, НСтр("ru = 'заданы в другой базе'"), НСтр("ru = 'не заданы'"));
			СтрокаСписка.Картинка = ?(СтрокаСписка.ВнешняяРоль, -1, 1);
		ИначеЕсли СтрокаСписка.Исполнители = 1 Тогда
			СтрокаСписка.ИсполнителиСтрока = Строка(БизнесПроцессыИЗадачиСервер.ВыбратьИсполнителя(ОсновнойОбъектАдресации, СтрокаСписка.РольСсылка));
			СтрокаСписка.Картинка = -1;
		Иначе
			СтрокаСписка.ИсполнителиСтрока =
			  СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			  НСтр("ru = '%1 чел'"), Строка(СтрокаСписка.Исполнители) );
			
			СтрокаСписка.Картинка = -1;
		КонецЕсли;
	КонецЦикла;
	ЗначениеВРеквизитФормы(СписокОбъект, "Список");
	
КонецПроцедуры
