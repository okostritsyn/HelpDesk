&НаСервере
Функция ДобавитьОтборВДинамическомСписке(ОтборСписка, ИмяПоля, ВидСравнения, Значение, Использование = Истина) Экспорт
    
    ЭлементОтбора = ОтборСписка.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
    ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяПоля);
    ЭлементОтбора.ВидСравнения = ВидСравнения;
    ЭлементОтбора.ПравоеЗначение = Значение;
    ЭлементОтбора.Использование = Использование;
    Возврат ЭлементОтбора;
    
КонецФункции // ДобавитьОтборВДинамическомСписке()

&НаСервере
Функция НайтиОтборВДинамическомСписке(ОтборСписка, ИмяПоля) Экспорт
    
    Результат = Неопределено;
    Поле = Новый ПолеКомпоновкиДанных(ИмяПоля);
    Для Каждого ЭлементОтбора Из ОтборСписка.Элементы Цикл
        Если ТипЗнч(ЭлементОтбора) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
            Результат = НайтиОтборВДинамическомСписке(ЭлементОтбора, ИмяПоля);
            Если Результат <> Неопределено Тогда
                Прервать;
            КонецЕсли;
        Иначе
            Если ЭлементОтбора.ЛевоеЗначение = Поле Тогда
                Результат = ЭлементОтбора;
                Прервать;
            КонецЕсли;
        КонецЕсли;
    КонецЦикла;
    
    Возврат Результат;
    
КонецФункции // НайтиОтборВДинамическомСписке()

&НаСервере
Процедура УстановитьОтборПоПолю(Отбор, Поле, ЗначениеПоля) Экспорт    
    
    Использование = ЗначениеЗаполнено(ЗначениеПоля);
    ЭлементОтбораПоПолю = НайтиОтборВДинамическомСписке(Отбор, Поле);
    Если ЭлементОтбораПоПолю = Неопределено Тогда
        ЭлементОтбораПоПолю = ДобавитьОтборВДинамическомСписке(
            Отбор, Поле, ВидСравненияКомпоновкиДанных.Равно, ЗначениеПоля, Использование);
    Иначе
        ЭлементОтбораПоПолю.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
        ЭлементОтбораПоПолю.ПравоеЗначение = ЗначениеПоля;
        ЭлементОтбораПоПолю.Использование = Использование;
    КонецЕсли;
    
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.ЗаявкаЗакрыта <> Неопределено Тогда
		УстановитьОтборПоПолю(Список.Отбор,"ЗаявкаЗакрыта",Параметры.ЗаявкаЗакрыта);
	КонецЕсли;
КонецПроцедуры
