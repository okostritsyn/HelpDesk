////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Запрос = Новый Запрос;
	
	Если Параметры.Свойство("Вариант") И ЗначениеЗаполнено(Параметры.Вариант) Тогда
		БыстрыйДоступ = Параметры.Вариант.БыстрыйДоступ;
		Запрос.УстановитьПараметр("Вариант", Параметры.Вариант);
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ИсключенияБыстрогоДоступа.Пользователь,
		|	(НЕ &БыстрыйДоступ) КАК БыстрыйДоступПользователя
		|ПОМЕСТИТЬ втИсключения
		|ИЗ
		|	Справочник.ВариантыОтчетов.ИсключенияБыстрогоДоступа КАК ИсключенияБыстрогоДоступа
		|ГДЕ
		|	ИсключенияБыстрогоДоступа.Ссылка = &Вариант
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Пользователи.Ссылка КАК Пользователь,
		|	ЕСТЬNULL(Исключения.БыстрыйДоступПользователя, &БыстрыйДоступ) КАК БыстрыйДоступПользователя
		|ИЗ
		|	Справочник.Пользователи КАК Пользователи
		|		ЛЕВОЕ СОЕДИНЕНИЕ втИсключения КАК Исключения
		|		ПО Пользователи.Ссылка = Исключения.Пользователь"
	Иначе
		
		ИсключенияБыстрогоДоступа = Новый ТаблицаЗначений;
		ИсключенияБыстрогоДоступа.Колонки.Добавить("Пользователь", Новый ОписаниеТипов("СправочникСсылка.Пользователи"));
		Для Каждого ЭлементСписка Из Параметры.ИсключенияБыстрогоДоступа Цикл
			ИсключенияБыстрогоДоступа.Добавить().Пользователь = ЭлементСписка.Значение;
		КонецЦикла;
		
		БыстрыйДоступ = Параметры.БыстрыйДоступ;
		Запрос.УстановитьПараметр("ИсключенияБыстрогоДоступа", ИсключенияБыстрогоДоступа);
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ИсключенияБыстрогоДоступа.Пользователь,
		|	(НЕ &БыстрыйДоступ) КАК БыстрыйДоступПользователя
		|ПОМЕСТИТЬ втИсключения
		|ИЗ
		|	&ИсключенияБыстрогоДоступа КАК ИсключенияБыстрогоДоступа
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Пользователи.Ссылка КАК Пользователь,
		|	ЕСТЬNULL(Исключения.БыстрыйДоступПользователя, &БыстрыйДоступ) КАК БыстрыйДоступПользователя
		|ИЗ
		|	Справочник.Пользователи КАК Пользователи
		|		ЛЕВОЕ СОЕДИНЕНИЕ втИсключения КАК Исключения
		|		ПО Пользователи.Ссылка = Исключения.Пользователь"
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("БыстрыйДоступ", БыстрыйДоступ);
	
	УстановитьПривилегированныйРежим(Истина);
	Пользователи.Загрузить(Запрос.Выполнить().Выгрузить());
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
Процедура Принять(Команда)
	
	Результат = Новый Структура("БыстрыйДоступ, ИсключенияБыстрогоДоступа", БыстрыйДоступ, Новый СписокЗначений);
	ИсключенияБыстрогоДоступа = Пользователи.НайтиСтроки(Новый Структура("БыстрыйДоступПользователя", НЕ БыстрыйДоступ));
	Для Каждого СтрокаПользователь Из ИсключенияБыстрогоДоступа Цикл
		Результат.ИсключенияБыстрогоДоступа.Добавить(СтрокаПользователь.Пользователь);
	КонецЦикла;
	Закрыть(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьВсе(Команда)
	
	БыстрыйДоступ = Истина;
	Для Каждого СтрокаПользователь Из Пользователи Цикл
		СтрокаПользователь.БыстрыйДоступПользователя = Истина;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьОтметки(Команда)
	
	БыстрыйДоступ = Ложь;
	Для Каждого СтрокаПользователь Из Пользователи Цикл
		СтрокаПользователь.БыстрыйДоступПользователя = Ложь;
	КонецЦикла;
	
КонецПроцедуры
