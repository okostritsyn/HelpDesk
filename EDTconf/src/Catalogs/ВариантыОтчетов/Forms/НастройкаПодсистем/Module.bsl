////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	// Ожидаемые параметры: ДеревоПодсистем, Подсистемы, НовыйЭлементСправочника
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	АдресДереваПодсистем = Параметры.АдресДереваПодсистем;
	ДеревоЗначений = ПолучитьИзВременногоХранилища(АдресДереваПодсистем);
	УдалитьИзВременногоХранилища(Параметры.АдресДереваПодсистем);
	
	Найденные = ДеревоЗначений.Строки.НайтиСтроки(Новый Структура("Предопределенная", Истина), Истина);
	Для Каждого СтрокаДерева Из Найденные Цикл
		СтрокаДерева.Предопределенная = Ложь;
	КонецЦикла;
	
	Если Параметры.Свойство("Подсистемы") Тогда
		ТаблицаЗначений = Параметры.Подсистемы.Выгрузить();
		
		Для Каждого СтрокаТаблицы Из ТаблицаЗначений Цикл
			Найденные = ДеревоЗначений.Строки.НайтиСтроки(Новый Структура("ПутьКПодсистеме", СтрокаТаблицы.Подсистема), Истина);
			Для Каждого СтрокаДерева Из Найденные Цикл
				СтрокаДерева.Использование = Истина;
				СтрокаДерева.Предопределенная = СтрокаТаблицы.Предопределенная;
			КонецЦикла;
		КонецЦикла;
		
		ЗначениеВРеквизитФормы(ТаблицаЗначений, "Подсистемы");
	КонецЕсли;
	
	ЗначениеВРеквизитФормы(ДеревоЗначений, "ДеревоПодсистем");
	
	Если Параметры.НовыйЭлементСправочника Тогда
		Если УсловноеОформление.Элементы.Количество() > 0 Тогда
			УсловноеОформление.Элементы.Получить(0).Использование = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
Процедура Принять(Команда)
	
	Подсистемы.Очистить();
	
	Для каждого СтрокаПодсистемы из ДеревоПодсистем.ПолучитьЭлементы() Цикл
		Если СтрокаПодсистемы.Использование тогда
			ГруппировкаПодсистемы             	  = Подсистемы.Добавить();
			ГруппировкаПодсистемы.Подсистема      = СтрокаПодсистемы.ПутьКПодсистеме;
			ГруппировкаПодсистемы.Название        = СтрокаПодсистемы.Название;
			ГруппировкаПодсистемы.Предопределенная = СтрокаПодсистемы.Предопределенная;
		КонецЕсли;
		ДобавитьПодсистемы(СтрокаПодсистемы)
	КонецЦикла;
	
	ВозвращаемыеПараметры = Новый Структура("СписокПодсистем, АдресДереваПодсистем");
	ВозвращаемыеПараметры.СписокПодсистем = Подсистемы;
	ВозвращаемыеПараметры.АдресДереваПодсистем = АдресДереваПодсистем;
	
	ПодготовитьРезультатНастройкиПодсистем(ВозвращаемыеПараметры);
	
	Закрыть(ВозвращаемыеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПодсистемы(СтрокаПодсистемы)
	
	Для каждого СтрокаПодсистемы из СтрокаПодсистемы.ПолучитьЭлементы() Цикл
		Если СтрокаПодсистемы.Использование тогда
			ГруппировкаПодсистема                 = Подсистемы.Добавить();
			ГруппировкаПодсистема.Подсистема      = СтрокаПодсистемы.ПутьКПодсистеме;
			ГруппировкаПодсистема.Название        = СтрокаПодсистемы.Название;
			ГруппировкаПодсистема.Предопределенная = СтрокаПодсистемы.Предопределенная;
		КонецЕсли;
		ДобавитьПодсистемы(СтрокаПодсистемы)
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьРезультатНастройкиПодсистем(ВозвращаемыеПараметры)
	
	ДеревоЗначений = ДанныеФормыВЗначение(ДеревоПодсистем, Тип("ДеревоЗначений"));
	ПоместитьВоВременноеХранилище(ДеревоЗначений, ВозвращаемыеПараметры.АдресДереваПодсистем);
	
КонецПроцедуры 
