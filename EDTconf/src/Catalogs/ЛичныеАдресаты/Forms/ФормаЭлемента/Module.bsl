
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Объект.Пользователь = Пользователи.ТекущийПользователь();
	КонецЕсли; 
	
	Если Параметры.Свойство("Родитель") Тогда
		Объект.Группа = Параметры.Родитель;
	КонецЕсли;	
	
	Если Параметры.Свойство("Адрес") Тогда
		
		// EmailАдресата
		Если ЗначениеЗаполнено(Параметры.Адрес) И Найти(Параметры.Адрес, "@") <> 0 Тогда
			НовСтр = Объект.КонтактнаяИнформация.Добавить();
			НовСтр.ЗначенияПолей = Параметры.Адрес;
			НовСтр.АдресЭП = Параметры.Адрес;
			НовСтр.Представление = Параметры.Адрес;
			НовСтр.Вид = Справочники.ВидыКонтактнойИнформации.EmailАдресата;
			НовСтр.Тип = Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты;
		КонецЕсли;
		
	КонецЕсли;	
	
	Если Параметры.Свойство("Представление") Тогда
		Объект.Наименование = Параметры.Представление;
	КонецЕсли;	
	
	УправлениеКонтактнойИнформацией.ПриСозданииНаСервере(ЭтотОбъект, Объект, "ГруппаКонтактнаяИнформация");
	
	Для каждого Эл Из Элементы.ГруппаКонтактнаяИнформация.ПодчиненныеЭлементы Цикл
		Эл.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Лево;
	КонецЦикла;	
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
		// Обработчик подсистемы "Контактная информация"
	УправлениеКонтактнойИнформацией.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ПОДСИСТЕМЫ "КОНТАКТНАЯ ИНФОРМАЦИЯ"

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияПриИзменении(Элемент)	
	
	УправлениеКонтактнойИнформациейКлиент.ПредставлениеПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	УправлениеКонтактнойИнформациейКлиент.ПредставлениеНачалоВыбора(ЭтотОбъект, Элемент, Модифицированность, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("ЛичныйАдресатИзменен", Объект.Ссылка);
	Оповестить("Запись_ЛичныйАдресат", , Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПреобразоватьВКорреспондента(Команда)
	
	ЛичныйАдресат = Объект.Ссылка;
	ПараметрыФормы = Новый Структура("ЛичныйАдресат", ЛичныйАдресат);
	КодВозврата = Неопределено;

	ОткрытьФорму("Справочник.ЛичныеАдресаты.Форма.ВыборКорреспондентаКонтактноеЛицо", ПараметрыФормы,,,,, Новый ОписаниеОповещения("ПреобразоватьВКорреспондентаЗавершение", ЭтотОбъект, Новый Структура("ЛичныйАдресат", ЛичныйАдресат)), РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);	
	
КонецПроцедуры

&НаКлиенте
Процедура ПреобразоватьВКорреспондентаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ЛичныйАдресат = ДополнительныеПараметры.ЛичныйАдресат;
	
	
	КодВозврата = Результат;
	
	Если ТипЗнч(КодВозврата) = Тип("СправочникСсылка.Контрагенты") 
		Или ТипЗнч(КодВозврата) = Тип("СправочникСсылка.КонтактныеЛица") Тогда
		
		РаботаСЛичнымиАдресатами.ЗаполнитьКонтактИЗаменитьСсылки(ЛичныйАдресат, КодВозврата, УникальныйИдентификатор);
		Прочитать();
		
	КонецЕсли;
	
КонецПроцедуры

