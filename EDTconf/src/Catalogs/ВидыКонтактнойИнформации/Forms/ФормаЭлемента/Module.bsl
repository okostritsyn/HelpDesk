////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ТипКИАдрес   = Перечисления.ТипыКонтактнойИнформации.Адрес;
	ТипКИТелефон = Перечисления.ТипыКонтактнойИнформации.Телефон;
	ТипКИФакс    = Перечисления.ТипыКонтактнойИнформации.Факс;
	ПроверитьЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи()
	
	ПроверитьЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ТипПриИзменении(Элемент)
	
	ПроверитьЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура РедактированиеТолькоВДиалогеПриИзменении(Элемент)
	
	ПроверитьЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиентеНаСервереБезКонтекста
Процедура ПроверитьЭлементыФормы(Форма)
	
	ЭтоНовый = Не ЗначениеЗаполнено(Форма.Объект.Ссылка);
	
	Если ЭтоНовый Тогда
		// для новых видов КИ установим признак "Можно изменять способ редактирования"
		Форма.Объект.МожноИзменятьСпособРедактирования = Истина;
		
	Иначе
		// для уже записанных запретим редактировать группу и тип
		Форма.Элементы.Родитель.ТолькоПросмотр = Истина;
		Форма.Элементы.Тип.ТолькоПросмотр      = Не Форма.РазрешитьРедактированиеТипа;
		
		// для предопределенных запретим и наименование редактировать
		Если Форма.Объект.Предопределенный Тогда
			Форма.Элементы.Наименование.ТолькоПросмотр = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не Форма.Объект.МожноИзменятьСпособРедактирования Тогда
		// Если у объекта нельзя менять способ редактирования, то сделаем недоступными оставшиеся элементы
		Форма.Элементы.РедактированиеТолькоВДиалоге.Доступность = Ложь;
		Форма.Элементы.АдресТолькоРоссийский.Доступность = Ложь;
		Возврат;
	
	КонецЕсли;
	
	ЭтоАдрес   = (Форма.Объект.Тип = Форма.ТипКИАдрес);
	ЭтоТелефон = (Форма.Объект.Тип = Форма.ТипКИТелефон);
	ЭтоФакс    = (Форма.Объект.Тип = Форма.ТипКИФакс);
	НеЗаполненТип = Не ЗначениеЗаполнено(Форма.Объект.Тип);
	
	// Установим доступность и проверим значение поля РедактированиеТолькоВДиалоге
	Если ЭтоАдрес ИЛИ ЭтоТелефон ИЛИ ЭтоФакс ИЛИ НеЗаполненТип Тогда
		Форма.Элементы.РедактированиеТолькоВДиалоге.Доступность = Истина;
	Иначе
		Форма.Элементы.РедактированиеТолькоВДиалоге.Доступность = Ложь;
		Если Форма.Объект.РедактированиеТолькоВДиалоге Тогда
			Форма.Объект.РедактированиеТолькоВДиалоге = Ложь;
		КонецЕсли;
	КонецЕсли;
		
	// Установим доступность и проверим значение поля РедактированиеТолькоВДиалоге
	Если ЭтоАдрес И Форма.Объект.РедактированиеТолькоВДиалоге Тогда
		Форма.Элементы.АдресТолькоРоссийский.Доступность = Истина;
	Иначе
		Форма.Элементы.АдресТолькоРоссийский.Доступность = Ложь;
		Если Форма.Объект.АдресТолькоРоссийский Тогда
			Форма.Объект.АдресТолькоРоссийский = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура РазрешитьРедактированиеТипаПриИзменении(Элемент)
	ПроверитьЭлементыФормы(ЭтотОбъект);
КонецПроцедуры

