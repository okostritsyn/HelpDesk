
// Предотвращает недопустимое изменение идентификаторов объектов метаданных.
Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ДополнительныеСвойства.Свойство("ВыполняетсяАвтоматическоеОбновлениеДанныхСправочника") Тогда
		
		Если ЭтоНовый() Тогда
		
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ошибка при работе со справочником ""Идентификаторы объектов метаданных"".
				           |
				           |Создание нового идентификатора объекта метаданных
				           |возможно только автоматически при обновлении данных справочника.'"),
				ПолноеИмя);
				
		ИначеЕсли Справочники.ИдентификаторыОбъектовМетаданных.ЗапрещеноИзменятьПолноеИмя(ЭтотОбъект) Тогда
			
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ошибка при работе со справочником ""Идентификаторы объектов метаданных"".
				           |
				           |При изменении идентификатора объекта метаданных указано
				           |полное имя ""%1"", которое может быть
				           |установлено только автоматически при обновлении данных справочника.'"),
				ПолноеИмя);
		
		ИначеЕсли Справочники.ИдентификаторыОбъектовМетаданных.ПолноеИмяИспользуется(ПолноеИмя, Ссылка) Тогда
			
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ошибка при работе со справочником ""Идентификаторы объектов метаданных"".
				           |
				           |При изменении идентификатора объекта метаданных указано
				           |полное имя ""%1"",
				           |которое уже используется в справочнике.'"),
				ПолноеИмя);
		
		КонецЕсли;
		
		Справочники.ИдентификаторыОбъектовМетаданных.ОбновитьСвойстваИдентификатора(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

// Предотвращает удаление идентификаторов объектов метаданных не помеченных на удаление.
Процедура ПередУдалением(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ПометкаУдаления Тогда
		ВызватьИсключение(НСтр("ru = 'Ошибка при работе со справочником ""Идентификаторы объектов метаданных"".
		                             |
		                             |Удаление идентификаторов объектов метаданных, у которых значение
		                             |реквизита ""ПометкаУдаления"" установлено Ложь недопустимо.'"));
	КонецЕсли;
	
КонецПроцедуры

