#Область ЗаполнениеОтдельныхРеквизитов

Функция ПолучитьПользователяПоСотруднику(ФизЛицо = Неопределено) Экспорт
	Пользователь = Неопределено;
	
	Если ФизЛицо <> Неопределено Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
			|	Пользователи.Ссылка КАК Пользователь
			|ИЗ
			|	Справочник.Пользователи КАК Пользователи
			|ГДЕ
			|	Пользователи.ФизическоеЛицо = &ФизЛицо";
		
		Запрос.УстановитьПараметр("ФизЛицо", ФизЛицо);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Если Не РезультатЗапроса.Пустой() Тогда
			
			Выборка = РезультатЗапроса.Выбрать();
			Выборка.Следующий();
			Пользователь = Выборка.Пользователь;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если Пользователь = Неопределено Тогда
		Пользователь = Пользователи.АвторизованныйПользователь();
	КонецЕсли;

	Возврат Пользователь;
КонецФункции

Функция ПолучитьКалендарьСотрудника(ФизЛицо = Неопределено) Экспорт
	
	Пользователь = ПолучитьПользователяПоСотруднику(ФизЛицо);
	
	Календарь = РаботаССистемойCRMПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователь, "ОсновнойКалендарь");
	
	Возврат Календарь;
	
КонецФункции

#КонецОбласти

// Функция возвращает связанных Сотрудников пользователя для переданной записи
//
// Пользователь - (Справочник.Пользователи) Пользователь, для которого получаем таблицы значений с записями
//
Функция ПолучитьСотрудниковПользователя(Пользователь = Неопределено) Экспорт
	
	Если Пользователь = Неопределено Тогда
		Пользователь = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Пользователи.ФизическоеЛицо КАК Сотрудник
		|ИЗ
		|	Справочник.Пользователи КАК Пользователи
		|ГДЕ
		|	Пользователи.Ссылка = &Пользователь
		|
		|УПОРЯДОЧИТЬ ПО
		|	Пользователи.ФизическоеЛицо.Наименование";
	
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	
	Результат = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Сотрудник");
	
	Возврат Результат;
	
КонецФункции // ПолучитьСотрудниковПользователя()
