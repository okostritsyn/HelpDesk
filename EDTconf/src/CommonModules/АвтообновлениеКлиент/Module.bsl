// Открывает модальную форму настройки автообновления списка. При завершении диалога
// устанавливает выбранные параметры автообновления списка.
//
// Параметры:
// Форма - уникальный идентификатор открытой формы
// ИмяСписка - Строка - имя реквизита списка на форме
//
Процедура УстановитьПараметрыАвтообновленияСписка(Форма, ИмяСписка) Экспорт
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("АвтоОбновление", Форма.Элементы[ИмяСписка].АвтоОбновление);
	ПараметрыФормы.Вставить("ПериодАвтоОбновления", Форма.Элементы[ИмяСписка].ПериодАвтоОбновления);
	Результат = Неопределено;

	ОткрытьФорму("ОбщаяФорма.НастройкаАвтообновления",
		ПараметрыФормы,
		Форма,,,, Новый ОписаниеОповещения("УстановитьПараметрыАвтообновленияСпискаЗавершение", ЭтотОбъект, Новый Структура("ИмяСписка, Форма", ИмяСписка, Форма)), РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

Процедура УстановитьПараметрыАвтообновленияСпискаЗавершение(Результат1, ДополнительныеПараметры) Экспорт
	
	ИмяСписка = ДополнительныеПараметры.ИмяСписка;
	Форма = ДополнительныеПараметры.Форма;
	
	
	Результат = Результат1;
	
	Если ЗначениеЗаполнено(Результат) Тогда
		Автообновление.СохранитьНастройкиАвтообновленияСписка(Форма.ИмяФормы, ИмяСписка, Результат);
		
		Форма.Элементы[ИмяСписка].АвтоОбновление = Результат.АвтоОбновление;
		Форма.Элементы[ИмяСписка].ПериодАвтоОбновления = Результат.ПериодАвтоОбновления;
		Если Результат.АвтоОбновление Тогда
			Форма.Элементы[ИмяСписка].Обновить();
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры	

// Открывает модальную форму настройки автообновления формы. При завершении диалога
// возвращает выбранные параметры автообновления.
//
// Параметры:
// Форма - уникальный идентификатор открытой формы
//
Процедура УстановитьПараметрыАвтообновленияФормы(Знач Оповещение, Форма, НастройкиАвтообновления) Экспорт
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("АвтоОбновление", НастройкиАвтообновления.АвтоОбновление);
	ПараметрыФормы.Вставить("ПериодАвтоОбновления", НастройкиАвтообновления.ПериодАвтоОбновления);

	ОткрытьФорму("ОбщаяФорма.НастройкаАвтообновления",
		ПараметрыФормы,
		Форма,,,, Новый ОписаниеОповещения("УстановитьПараметрыАвтообновленияФормыЗавершение", ЭтотОбъект, Новый Структура("Оповещение, Форма", Оповещение, Форма)), РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

Процедура УстановитьПараметрыАвтообновленияФормыЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Оповещение = ДополнительныеПараметры.Оповещение;
	Форма = ДополнительныеПараметры.Форма;
		
	Если Не ЗначениеЗаполнено(Результат) Тогда
		ВыполнитьОбработкуОповещения(Оповещение, Неопределено);
		Возврат;
	КонецЕсли;
	
	Автообновление.СохранитьНастройкиАвтообновленияФормы(Форма.ИмяФормы, Результат);
	
	ВыполнитьОбработкуОповещения(Оповещение, Результат);
	
	Возврат;
	
КонецПроцедуры


