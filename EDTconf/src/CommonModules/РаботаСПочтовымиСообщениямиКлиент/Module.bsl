////////////////////////////////////////////////////////////////////////////////
// Подсистема "Работа с почтовыми сообщениями".
//
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Интерфейсная клиентская функция, поддерживающая упрощенный вызов формы редактирования
// нового письма.
// Параметры
// Отправитель*  - СписокЗначений, СправочникСсылка.УчетныеЗаписиЭлектроннойПочты - 
//                 учетная запись (список) с которой(ых) может быть отправлено
//                 почтовое сообщение. Если тип список значений, тогда
//                   представление - наименование учетной записи,
//                   значение - ссылка на учетную запись
//
// Получатель      - СписокЗначений, Строка:
//                   если список значений, то представление - имя получателя
//                                            значение      - почтовый адрес
//                   если строка то список почтовых адресов,
//                   в формате правильного e-mail адреса*
//
// Тема            - Строка - тема письма
// Текст           - Строка - тело письма
//
// СписокФайлов    - СписокЗначений, где
//                   представление - строка - наименование вложения
//                   значение      - ДвоичныеДанные - двоичные данные вложения
//                                 - Строка - адрес файла во временном хранилище
//                                 - Строка - путь к файлу на клиенте
//
// УдалятьФайлыПослеОтправки - булево - удалять временные файлы после отправки сообщения
// ПисьмоДолжноСохраняться   - булево - должно ли письмо сохраняться (используется только
//                                      если встроена подсистема Взаимодействия)
//
Процедура ОткрытьФормуОтправкиПочтовогоСообщения(знач Отправитель = Неопределено,
												знач Получатель = Неопределено,
												знач Тема = "",
												знач Текст = "",
												знач СписокФайлов = Неопределено,
												знач УдалятьФайлыПослеОтправки = Ложь,
												знач ПисьмоДолжноСохраняться = Истина) Экспорт
	
	СтандартнаяОбработка = Истина;
	
	ЭлектроннаяПочтаКлиентПереопределяемый.ОткрытьФормуОтправкиПочтовогоСообщения(СтандартнаяОбработка,
	      Отправитель,Получатель,Тема,Текст,СписокФайлов,УдалятьФайлыПослеОтправки);
	
	Если СтандартнаяОбработка Тогда
		ОткрытьПростуюФормуОтправкиПочтовогоСообщения(Отправитель,
		Получатель, Тема,Текст, СписокФайлов, УдалятьФайлыПослеОтправки);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Интерфейсная клиентская функция, поддерживающая упрощенный вызов простой
// формы редактирования нового письма. При отправке письма через простую
// форму сообщения не сохраняются в информационной базе.
//
// Параметры см. к функции ОткрытьФормуОтправкиПочтовогоСообщения
//
Процедура ОткрытьПростуюФормуОтправкиПочтовогоСообщения(Отправитель,
			Получатель, Тема,Текст, СписокФайлов, УдалятьФайлыПослеОтправки) Экспорт
	
	ПараметрыПисьма = Новый Структура;
	
	ПараметрыПисьма.Вставить("УчетнаяЗапись", Отправитель);
	ПараметрыПисьма.Вставить("Кому", Получатель);
	ПараметрыПисьма.Вставить("Тема", Тема);
	ПараметрыПисьма.Вставить("Тело", Текст);
	ПараметрыПисьма.Вставить("Вложения", СписокФайлов);
	ПараметрыПисьма.Вставить("УдалятьФайлыПослеОтправки", УдалятьФайлыПослеОтправки);
	
	ОткрытьФорму("ОбщаяФорма.РедактированиеНовогоПисьма", ПараметрыПисьма);
	
КонецПроцедуры

// Выполняет проверку учетной записи.
//
// Параметры
// УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиЭлектроннойПочты - учетная запись,
//					которую нужно проверить.
//
Процедура ПроверитьУчетнуюЗапись(знач УчетнаяЗапись) Экспорт
	
	ОчиститьСообщения();
	
	Состояние(НСтр("ru = 'Проверка учетной записи'"),,НСтр("ru = 'Выполняется проверка учетной записи. Подождите...'"));
	
	Если ЭлектроннаяПочтаВызовСервера.ПарольЗадан(УчетнаяЗапись) Тогда
		ПарольПараметр = Неопределено;
	Иначе
		ПараметрУчетнаяЗапись = Новый Структура("УчетнаяЗапись", УчетнаяЗапись);
		ОткрытьФорму("ОбщаяФорма.ПодтверждениеПароляУчетнойЗаписи", ПараметрУчетнаяЗапись,,,,, Новый ОписаниеОповещения("ПроверитьУчетнуюЗаписьЗавершение", ЭтотОбъект, Новый Структура("УчетнаяЗапись", УчетнаяЗапись)), РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
        Возврат;
	КонецЕсли;
	
	ПроверитьУчетнуюЗаписьФрагмент(ПарольПараметр, УчетнаяЗапись);
КонецПроцедуры

Процедура ПроверитьУчетнуюЗаписьЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	УчетнаяЗапись = ДополнительныеПараметры.УчетнаяЗапись;
	
	
	ПарольПараметр = Результат;
	Если ТипЗнч(ПарольПараметр) <> Тип("Строка") Тогда
		Возврат
	КонецЕсли;
	
	ПроверитьУчетнуюЗаписьФрагмент(ПарольПараметр, УчетнаяЗапись);

КонецПроцедуры

Процедура ПроверитьУчетнуюЗаписьФрагмент(Знач ПарольПараметр, Знач УчетнаяЗапись)
	
	Перем ДополнительноеСообщение, СообщениеОбОшибке;
	
	СообщениеОбОшибке = "";
	ДополнительноеСообщение = "";
	ЭлектроннаяПочтаВызовСервера.ПроверитьВозможностьОтправкиИПолученияЭлектроннойПочты(УчетнаяЗапись, ПарольПараметр, СообщениеОбОшибке, ДополнительноеСообщение);
	
	Если ЗначениеЗаполнено(СообщениеОбОшибке) Тогда
		ПоказатьПредупреждение(Неопределено, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Проверка параметров учетной записи завершилась с ошибками:
		|%1'"), СообщениеОбОшибке ),,
		НСтр("ru = 'Проверка учетной записи'"));
	Иначе
		ПоказатьПредупреждение(Неопределено, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Проверка параметров учетной записи завершилась успешно. %1'"),
		ДополнительноеСообщение ),,
		НСтр("ru = 'Проверка учетной записи'"));
	КонецЕсли;
	
КонецПроцедуры
 // ПроверитьУчетнуюЗапись()

