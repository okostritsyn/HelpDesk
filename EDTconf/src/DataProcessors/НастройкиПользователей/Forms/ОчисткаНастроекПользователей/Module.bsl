///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИспользоватьВнешнихПользователей = ПолучитьФункциональнуюОпцию("ИспользоватьВнешнихПользователей");
	
	Источник = "ВыбраннымПользователям";
	ОчищаемыеНастройки = "ОчиститьВсе";
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ВРег(ИмяСобытия) = ВРег("ВыборПользователя") Тогда
		
		Если ПользователиИсточник <> Неопределено Тогда
			Элементы.ВыбратьНастройки.Заголовок = НСтр("ru='Выбрать'");
			ВыбранныеНастройки = Неопределено;
			КоличествоНастроек = Неопределено;
		КонецЕсли;
			
		ПользователиИсточник = Новый Структура("МассивПользователей", Параметр.ПользователиПриемник);
		
		КоличествоПользователей = Параметр.ПользователиПриемник.Количество();
		Если КоличествоПользователей = 1 Тогда
			Элементы.ВыбратьПользователей.Заголовок = Строка(Параметр.ПользователиПриемник[0]);
			Элементы.ГруппаОчищаемыеНастройки.Доступность = Истина;
		ИначеЕсли КоличествоПользователей > 1 Тогда
			
			ПрописьЧисла = ЧислоПрописью(
				КоличествоПользователей,
				"Л = ru_RU",
				НСтр("ru = ',,,,,,,,0'"));
			ПрописьЧислаИПредмета = ЧислоПрописью(
				КоличествоПользователей,
				"Л = ru_RU",
				НСтр("ru = 'пользователь,пользователя,пользователей,,,,,,0'"));
			ЧислоИПредмет = СтрЗаменить(
				ПрописьЧислаИПредмета,
				ПрописьЧисла,
				Формат(КоличествоПользователей, "ЧДЦ=0") + " ");
				
			Элементы.ВыбратьПользователей.Заголовок = ЧислоИПредмет;
			ОчищаемыеНастройки = "ОчиститьВсе";
		КонецЕсли;
		Элементы.ВыбратьПользователей.Подсказка = "";
		
	ИначеЕсли ВРег(ИмяСобытия) = ВРег("ВыборНастроек") Тогда
		ВыбранныеНастройки = Новый Структура("НастройкиОтчетов, ВнешнийВид, ПрочиеНастройки, ПерсональныеНастройки",
			Параметр.НастройкиОтчетов, Параметр.ВнешнийВид, Параметр.ПрочиеНастройки, Параметр.ПерсональныеНастройки);
		
		КоличествоНастроек = Параметр.КоличествоНастроек;
		
		Если КоличествоНастроек = 0 Тогда
			ТекстЗаголовка = НСтр("ru='Выбрать'");
		ИначеЕсли КоличествоНастроек = 1 Тогда
			ТекстЗаголовка = Параметр.ПредставленияНастроек[0];
		Иначе
			ПрописьЧисла = ЧислоПрописью(
				КоличествоНастроек,
				"Л = ru_RU",
				НСтр("ru = ',,,,,,,,0'"));
			ПрописьЧислаИПредмета = ЧислоПрописью(
				КоличествоНастроек,
				"Л = ru_RU",
				НСтр("ru = 'настройка,настройки,настроек,,,,,,0'"));
			ТекстЗаголовка = СтрЗаменить(
				ПрописьЧислаИПредмета,
				ПрописьЧисла,
				Формат(КоличествоНастроек, "ЧДЦ=0") + " ");
		КонецЕсли;
		
		Элементы.ВыбратьНастройки.Заголовок = ТекстЗаголовка;
		Элементы.ВыбратьНастройки.Подсказка = "";
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ИсточникПриИзменении(Элемент)
	
	Если Источник = "ВыбраннымПользователям"
		И КоличествоПользователей > 1
		Или Источник = "ВсемПользователям" Тогда
		ОчищаемыеНастройки = "ОчиститьВсе";
	КонецЕсли;
	
	Если Источник = "ВыбраннымПользователям"
		И КоличествоПользователей = 1
		Или Источник = "ВсемПользователям" Тогда
		Элементы.ГруппаОчищаемыеНастройки.Доступность = Истина;
	Иначе
		Элементы.ГруппаОчищаемыеНастройки.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчищаемыеНастройкиПриИзменении(Элемент)
	
	Если Источник = "ВыбраннымПользователям"
		И КоличествоПользователей > 1 
		Или Источник = "ВсемПользователям" Тогда
		ПоказатьПредупреждение(Новый ОписаниеОповещения("ОчищаемыеНастройкиПриИзмененииЗавершение", ЭтотОбъект), НСтр("ru = 'Очистка отдельных настроек доступна только при выборе одного пользователя.'"));
	ИначеЕсли ОчищаемыеНастройки = "ОчиститьВсе" Тогда
		Элементы.ВыбратьНастройки.Доступность = Ложь;
	Иначе
		Элементы.ВыбратьНастройки.Доступность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчищаемыеНастройкиПриИзмененииЗавершение(ДополнительныеПараметры) Экспорт
	
	ОчищаемыеНастройки = "ОчиститьВсе";
	Элементы.ВыбратьНастройки.Доступность = Ложь;

КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПользователейНажатие(Элемент)
	
	Если ИспользоватьВнешнихПользователей Тогда
		ВыборТипаПользователей = Новый СписокЗначений;
		ВыборТипаПользователей.Добавить("ВнешниеПользователи", "Внешние пользователи");
		ВыборТипаПользователей.Добавить("Пользователи", "Пользователи");
		
		ВыбранныйВариант = Неопределено;

		
		ВыборТипаПользователей.ПоказатьВыборЭлемента(Новый ОписаниеОповещения("ВыбратьПользователейНажатиеЗавершение", ЭтотОбъект));
        Возврат;
		
	Иначе
		ТипПользователя = Тип("СправочникСсылка.Пользователи");
	КонецЕсли;
	
	ВыбратьПользователейНажатиеФрагмент(ТипПользователя);
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПользователейНажатиеЗавершение(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	ВыбранныйВариант = ВыбранныйЭлемент;
	
	Если ВыбранныйВариант = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ВыбранныйВариант.Значение = "Пользователи" Тогда
		ТипПользователя = Тип("СправочникСсылка.Пользователи");
	ИначеЕсли ВыбранныйВариант.Значение = "ВнешниеПользователи" Тогда
		ТипПользователя = Тип("СправочникСсылка.ВнешниеПользователи");
	КонецЕсли;
	
	
	ВыбратьПользователейНажатиеФрагмент(ТипПользователя);

КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПользователейНажатиеФрагмент(Знач ТипПользователя)
	
	Перем ПараметрыФормы;
	
	ПараметрыФормы = Новый Структура("Пользователь, ТипПользователя, КопироватьВсе, ОчисткаНастроек",
	"", ТипПользователя, Ложь, Истина);
	ОткрытьФорму("Обработка.НастройкиПользователей.Форма.ВыборПользователей", ПараметрыФормы);

КонецПроцедуры

&НаКлиенте
Процедура ВыбратьНастройки(Элемент)
	
	Если КоличествоПользователей = 1 Тогда
		ПользовательСсылка = ПользователиИсточник.МассивПользователей[0];
		ПараметрыФормы = Новый Структура("Пользователь", ПользовательСсылка);
		ОткрытьФорму("Обработка.НастройкиПользователей.Форма.ВыборНастроек", ПараметрыФормы);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура Очистить(Команда)
	
	ОчиститьСообщения();
	ОчисткаНастроек();
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьИЗакрыть(Команда)
	
	ОчиститьСообщения();
	НастройкиОчищены = ОчисткаНастроек();
	
	Если НастройкиОчищены Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Функция ОчисткаНастроек()
	
	Если Источник = "ВыбраннымПользователям"
		И КоличествоПользователей = 0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Выберите пользователя или пользователей,
				|которым необходимо очистить настройки.'"), , "Источник");
		Возврат Ложь;
	КонецЕсли;
	
	Если ОчищаемыеНастройки = "ОтдельныеНастройки"
		И КоличествоНастроек = 0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Выберите настройки, которые необходимо очистить.'"), , "ОчищаемыеНастройки");
		Возврат Ложь;
	КонецЕсли;
	
	Если ОчищаемыеНастройки = "ОтдельныеНастройки" Тогда
		ОчиститьВыбранныеНастройки();
		ТекстПояснения = НСтр("ru = 'Очищено настроек: %1'");
		ТекстПояснения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстПояснения, КоличествоНастроек);
		ПоказатьОповещениеПользователя(НСтр("ru = 'Очистка настроек'"), , ТекстПояснения);
	ИначеЕсли ОчищаемыеНастройки = "ОчиститьВсе" Тогда
		ОчиститьВсеНастройки();
		
		ТекстПояснения = НСтр("ru = 'Очищены все настройки'");
		ПоказатьОповещениеПользователя(НСтр("ru = 'Очистка настроек'"), , ТекстПояснения);
	КонецЕсли;
	
	КоличествоНастроек = 0;
	Элементы.ВыбратьНастройки.Заголовок = НСтр("ru='Выбрать'");
	Возврат Истина;
	
КонецФункции

&НаСервере
Процедура ОчиститьВыбранныеНастройки()
	
	Обработка = РеквизитФормыВЗначение("Объект");
	
	Источники = ПользователиИсточник.МассивПользователей[0];
	Пользователь = Обработки.НастройкиПользователей.ИмяПользователяИБ(Источники);
	
	Если ВыбранныеНастройки.НастройкиОтчетов.Количество() > 0 Тогда
		Обработка.УдалитьНастройки(Пользователь, ВыбранныеНастройки.НастройкиОтчетов, "ХранилищеПользовательскихНастроекОтчетов");
	КонецЕсли;
		
	Если ВыбранныеНастройки.ВнешнийВид.Количество() > 0 Тогда
		Обработка.УдалитьНастройки(Пользователь, ВыбранныеНастройки.ВнешнийВид, "ХранилищеСистемныхНастроек");
	КонецЕсли;
	
	Если ВыбранныеНастройки.ПрочиеНастройки.Количество() > 0 Тогда
		Обработка.УдалитьНастройки(Пользователь, ВыбранныеНастройки.ПрочиеНастройки, "ХранилищеСистемныхНастроек");
	КонецЕсли;
	
	Если ВыбранныеНастройки.ПерсональныеНастройки.Количество() > 0 Тогда
		Обработка.УдалитьНастройки(Пользователь, ВыбранныеНастройки.ПерсональныеНастройки, "ХранилищеОбщихНастроек");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьВсеНастройки()
	
	Обработка = РеквизитФормыВЗначение("Объект");
	
	МассивНастроек = Новый Массив;
	МассивНастроек.Добавить("НастройкиОтчетов");
	МассивНастроек.Добавить("НастройкиВнешнегоВида");
	МассивНастроек.Добавить("ПерсональныеНастройки");
	МассивНастроек.Добавить("ДанныеФорм");
	МассивНастроек.Добавить("Избранное");
	МассивНастроек.Добавить("НастройкиПечати");
	
	Если Источник = "ВыбраннымПользователям" Тогда
		Источники = ПользователиИсточник.МассивПользователей;
	Иначе
		Источники = Новый Массив;
		ТаблицаПользователей = Новый ТаблицаЗначений;
		ТаблицаПользователей.Колонки.Добавить("Пользователь");
		ТаблицаПользователей = Обработка.ПользователиДляКопирования("", ТаблицаПользователей, ИспользоватьВнешнихПользователей);
		
		Для Каждого СтрокаТаблицы Из ТаблицаПользователей Цикл
			Источники.Добавить(СтрокаТаблицы.Пользователь);
		КонецЦикла;
		
	КонецЕсли;
	
	Обработки.НастройкиПользователей.УдалитьНастройкиПользователей(МассивНастроек, Источники);
	
КонецПроцедуры




