

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не РольДоступна("ПолныеПрава") Тогда
		ВызватьИсключение НСтр("ru = 'Использование обработки в интерактивном режиме доступно только пользователю с полными правами.'");
	КонецЕсли;
		
	СписокРазрешенныхТипов.Добавить(Тип("Строка"));
	СписокРазрешенныхТипов.Добавить(Тип("Число"));
	СписокРазрешенныхТипов.Добавить(Тип("Булево"));
	СписокРазрешенныхТипов.Добавить(Тип("Неопределено"));
	СписокРазрешенныхТипов.Добавить(Тип("Дата"));
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	//ИзменитьРежимОбработки(ЭтоКлиент);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьТекущийСписок(Команда)
	
	ЗаполнитьТекущийСписокНаСервере(Элементы.ХранимыеНастройки.ТекущаяСтраница.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНастройкиВФайл(Команда)
	
	ТекущееХранилище = Элементы.ХранимыеНастройки.ТекущаяСтраница.Имя;
	ИмяДерева = "Дерево" + ТекущееХранилище;
	
	ТекущаяСтрока = Элементы[ИмяДерева].ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда
		Сообщить("Не указана строка настроек");
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы[ИмяДерева].ТекущиеДанные;
	ИмяПользователя = "";
	
	Если ПустаяСтрока(ТекущиеДанные.Пользователь) Тогда
		ИмяПользователя = ИмяПользователяИБ;
	Иначе
		ИмяПользователя = ТекущиеДанные.Пользователь; 
	КонецЕсли;
		
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ТекущееХранилище",ТекущееХранилище);
	СтруктураПараметров.Вставить("Пользователь",ИмяПользователя);
	СтруктураПараметров.Вставить("КлючНастроек",ТекущиеДанные.КлючНастроек);
	СтруктураПараметров.Вставить("РежимВыгрузки",Истина);
	Если ПустаяСтрока(ТекущиеДанные.КлючОбъекта) Тогда
		СтруктураПараметров.Вставить("КлючОбъекта",ТекущиеДанные.СкрытыйКлючОбъекта);
	Иначе
		СтруктураПараметров.Вставить("КлючОбъекта",ТекущиеДанные.КлючОбъекта);		
	КонецЕсли;
	
	ОткрытьФорму("Обработка.СпискиХранилищНастроек.Форма.ФормаВыгрузки",СтруктураПараметров,ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура НаКлиенте(Команда)
	
	Если Не ЭтоКлиент Тогда
		
		ЭтоКлиент = Истина;
		
		ИзменитьРежимОбработки(ЭтоКлиент);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НаСервере(Команда)
	
	Если ЭтоКлиент Тогда
		
		ЭтоКлиент = Ложь;
		
		ИзменитьРежимОбработки(ЭтоКлиент);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередатьНастройки(Команда)
	
	ТекущееХранилище = Элементы.ХранимыеНастройки.ТекущаяСтраница.Имя;

	ИмяДерева = "Дерево" + ТекущееХранилище;
	
	ТекущаяСтрока = Элементы[ИмяДерева].ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда
		Сообщить("Неуказана строка настроек");
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы[ИмяДерева].ТекущиеДанные;
	
	Если НЕ ПустаяСтрока(ТекущиеДанные.КлючОбъекта) Тогда
		Сообщить("Неуказана строка настроек");
		Возврат;
	КонецЕсли;
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("КлючОбъекта",ТекущиеДанные.СкрытыйКлючОбъекта);
	СтруктураПараметров.Вставить("ИмяПользователя",ТекущиеДанные.Пользователь);
	СтруктураПараметров.Вставить("КлючНастроек",ТекущиеДанные.КлючНастроек);
	СтруктураПараметров.Вставить("ТекущееХранилище",ТекущееХранилище);
	
	ВыбЗнач = Пользователь;
	
	Оповещение = Новый ОписаниеОповещения("ПередатьНастройкиНаСервере",ЭтаФорма,СтруктураПараметров);
	ПоказатьВводЗначения(Оповещение, ВыбЗнач, "Укажите пользователя - получателя настроек");
	
КонецПроцедуры

&НаКлиенте
Процедура ПередатьВсемПользователям(Команда)
	
	ТекущееХранилище = Элементы.ХранимыеНастройки.ТекущаяСтраница.Имя;

	ИмяДерева = "Дерево" + ТекущееХранилище;
	
	ТекущаяСтрока = Элементы[ИмяДерева].ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда
		Сообщить("Неуказана строка настроек");
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы[ИмяДерева].ТекущиеДанные;
	
	Если НЕ ПустаяСтрока(ТекущиеДанные.КлючОбъекта) Тогда
		Сообщить("Неуказана строка настроек");
		Возврат;
	КонецЕсли;
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("КлючОбъекта",ТекущиеДанные.СкрытыйКлючОбъекта);
	СтруктураПараметров.Вставить("ИмяПользователя",ТекущиеДанные.Пользователь);
	СтруктураПараметров.Вставить("КлючНастроек",ТекущиеДанные.КлючНастроек);
	СтруктураПараметров.Вставить("ТекущееХранилище",ТекущееХранилище);
	
	СтрокаОшибки = "";
	СтрокаОшибки = ПередатьВсемПользователямНаСервере(СтруктураПараметров);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьТекущиеДляВсехПользователей(Команда)
	
	ТекущееХранилище = Элементы.ХранимыеНастройки.ТекущаяСтраница.Имя;
	ИмяДерева = "Дерево" + ТекущееХранилище;
	
	ТекущаяСтрока = Элементы[ИмяДерева].ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда
		Сообщить("Не указана строка настроек");
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы[ИмяДерева].ТекущиеДанные;
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ТекущееХранилище",ТекущееХранилище);
	
	Если ПустаяСтрока(ТекущиеДанные.КлючОбъекта) Тогда
		СтруктураПараметров.Вставить("КлючОбъекта",ТекущиеДанные.СкрытыйКлючОбъекта);
		СтруктураПараметров.Вставить("ИмяПользователя",Неопределено);
		СтруктураПараметров.Вставить("КлючНастроек",?(ПустаяСтрока(Текущиеданные.КлючНастроек),Неопределено,Текущиеданные.КлючНастроек));
	Иначе
		СтруктураПараметров.Вставить("КлючОбъекта",ТекущиеДанные.КлючОбъекта);
		СтруктураПараметров.Вставить("ИмяПользователя",Неопределено);
		СтруктураПараметров.Вставить("КлючНастроек",Неопределено);
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПослеВопросаУдаления",ЭтаФорма,СтруктураПараметров);
	ТекстВопроса = "Удалить настройки для всех пользователей?";
	ТекстВопроса = ТекстВопроса + "
	|" + СтруктураПараметров.КлючОбъекта;
	Если НЕ СтруктураПараметров.КлючНастроек = Неопределено Тогда
	ТекстВопроса = ТекстВопроса + "
	|" + СтруктураПараметров.КлючНастроек;	
	КонецЕсли;	
	Кнопки = РежимДиалогаВопрос.ДаНет;
	ТекстЗаголовка = "Удаление настроек";
	
	ПоказатьВопрос(Оповещение,ТекстВопроса,Кнопки,,, ТекстЗаголовка);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьТекущиеНастройки(Команда)
	
	ТекущееХранилище = Элементы.ХранимыеНастройки.ТекущаяСтраница.Имя;
	ИмяДерева = "Дерево" + ТекущееХранилище;
	
	ТекущаяСтрока = Элементы[ИмяДерева].ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда
		Сообщить("Не указана строка настроек");
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы[ИмяДерева].ТекущиеДанные;
	ИмяПользователя = "";
	
	Если ПустаяСтрока(ТекущиеДанные.Пользователь) Тогда
		ИмяПользователя = ИмяПользователяИБ;
	Иначе
		ИмяПользователя = ТекущиеДанные.Пользователь; 
	КонецЕсли;
	
	Если ПустаяСтрока(ИмяПользователя) Тогда
		Сообщить("Не выбран пользователь");
		Возврат;
	КонецЕсли;
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ТекущееХранилище",ТекущееХранилище);
	
	Если ПустаяСтрока(ТекущиеДанные.КлючОбъекта) Тогда
		СтруктураПараметров.Вставить("КлючОбъекта",ТекущиеДанные.СкрытыйКлючОбъекта);
		СтруктураПараметров.Вставить("ИмяПользователя",ИмяПользователя);
		СтруктураПараметров.Вставить("КлючНастроек",?(ПустаяСтрока(Текущиеданные.КлючНастроек),Неопределено,Текущиеданные.КлючНастроек));
	Иначе
		СтруктураПараметров.Вставить("КлючОбъекта",ТекущиеДанные.КлючОбъекта);
		СтруктураПараметров.Вставить("ИмяПользователя",ИмяПользователя);
		СтруктураПараметров.Вставить("КлючНастроек",Неопределено);
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПослеВопросаУдаления",ЭтаФорма,СтруктураПараметров);
	ТекстВопроса = "Удалить настройки для "+ ИмяПользователя +" ?";
	ТекстВопроса = ТекстВопроса + "
	|" + СтруктураПараметров.КлючОбъекта;
	Если НЕ СтруктураПараметров.КлючНастроек = Неопределено Тогда
	ТекстВопроса = ТекстВопроса + "
	|" + СтруктураПараметров.КлючНастроек;	
	КонецЕсли;	
	Кнопки = РежимДиалогаВопрос.ДаНет;
	ТекстЗаголовка = "Удаление настроек";
	
	ПоказатьВопрос(Оповещение,ТекстВопроса,Кнопки,,, ТекстЗаголовка);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьНастройки(Команда)
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ТекущееХранилище",Неопределено);
	СтруктураПараметров.Вставить("Пользователь",Неопределено);
	СтруктураПараметров.Вставить("КлючНастроек",Неопределено);
	СтруктураПараметров.Вставить("РежимВыгрузки",Ложь);
	СтруктураПараметров.Вставить("КлючОбъекта",Неопределено);		
	
	ОткрытьФорму("Обработка.СпискиХранилищНастроек.Форма.ФормаВыгрузки",СтруктураПараметров,ЭтаФорма);
	

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементов

&НаКлиенте
Процедура ДеревоХранилищеСистемныхНастроекПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
	ТекущийЭлементДерева = Элементы.ДеревоХранилищеСистемныхНастроек.ТекущаяСтрока;
	Если ТекущийЭлементДерева = Неопределено Тогда
		Сообщить("Не указан ключ объекта");
		Возврат;
	КонецЕсли;
	ТекущиеДанныеДерева = Элементы.ДеревоХранилищеСистемныхНастроек.ТекущиеДанные;
	КлючОбъекта = ТекущиеДанныеДерева.СкрытыйКлючОбъекта;
	ТекущийПользователь = ТекущиеДанныеДерева.Пользователь;
	Если ПустаяСтрока(КлючОбъекта) Тогда
		Сообщить("Не указан ключ объекта");
		Возврат;
	КонецЕсли;
	
	ТекстНастроек = ПолучитьТекстНастроек(КлючОбъекта,ТекущийПользователь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПользовательПриИзменении(Элемент)

	ОтборПоПользователю = ЗначениеЗаполнено(Пользователь);
	ОтборПоПользователюПриИзменении(Неопределено);
	
	УстановитьИмяПользователяНаСервере();
		
КонецПроцедуры

&НаКлиенте
Процедура ОтборПоПользователюПриИзменении(Элемент)
	
	Элементы.ДеревоХранилищеОбщихНастроекПользователь.Видимость      = (НЕ ОтборПоПользователю);
	Элементы.ДеревоХранилищеСистемныхНастроекПользователь.Видимость  = (НЕ ОтборПоПользователю);
	Элементы.ДеревоХранилищеНастроекДанныхФормПользователь.Видимость = (НЕ ОтборПоПользователю);
	ОчиститьТаблицы();
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоХранилищеОбщихНастроекПередНачаломИзменения(Элемент, Отказ)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если НЕ ПустаяСтрока(ТекущиеДанные.КлючОбъекта) Тогда
		Отказ = Истина;
		Возврат;
	ИначеЕсли НЕ ТекущиеДанные.РазрешеноРедактирование Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ДеревоХранилищеОбщихНастроекЗначениеПриИзменении(Элемент)
	
	ТекущиеДанные = Элемент.Родитель.ТекущиеДанные;
	ТекущееХранилище = Элементы.ХранимыеНастройки.ТекущаяСтраница.Имя;
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Настройки",ТекущиеДанные.Значение);
	СтруктураПараметров.Вставить("КлючОбъекта",ТекущиеДанные.СкрытыйКлючОбъекта);
	СтруктураПараметров.Вставить("ИмяПользователя",ТекущиеДанные.Пользователь);
	СтруктураПараметров.Вставить("КлючНастроек",ТекущиеДанные.КлючНастроек);
	СтруктураПараметров.Вставить("ТекущееХранилище",ТекущееХранилище);
	СтрокаОшибки = "";
	СтрокаОшибки = ДеревоХранилищеНастроекЗначениеПриИзмененииНаСервере(СтруктураПараметров);
	Если НЕ ПустаяСтрока(СтрокаОшибки) Тогда
		Сообщить(СтрокаОшибки);
		ЗаполнитьТекущийСписок(Неопределено);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеДляОбработчиковСобытийЭлементов

&НаСервереБезКонтекста
Функция ДеревоХранилищеНастроекЗначениеПриИзмененииНаСервере(СтруктураПараметров) Экспорт
	
	СтрокаОшибки = "";
	
	МенеджерХранилища = Вычислить(СтруктураПараметров.ТекущееХранилище);
	Попытка
	ОписаниеНастроек = МенеджерХранилища.ПолучитьОписание(СтруктураПараметров.КлючОбъекта, 
	СтруктураПараметров.КлючНастроек,
	СтруктураПараметров.ИмяПользователя); 
	
	МенеджерХранилища.Сохранить(СтруктураПараметров.КлючОбъекта, 
	СтруктураПараметров.КлючНастроек, 
	СтруктураПараметров.Настройки, 
	ОписаниеНастроек, 
	СтруктураПараметров.ИмяПользователя);
	Исключение
	    СтрокаОшибки = ОписаниеОшибки();
	КонецПопытки;
	
	Возврат СтрокаОшибки;
	
КонецФункции

&НаСервере
Функция РазрешенныйДляРедактирования(ТекущееЗначение)
		
	Возврат (НЕ СписокРазрешенныхТипов.НайтиПоЗначению(ТипЗнч(ТекущееЗначение)) = Неопределено);
		
КонецФункции

&НаСервереБезКонтекста
Функция  ПолучитьТекстНастроек(КлючОбъекта,ТекущийПользователь)
	
	Возврат ЗначениеВСтрокуВнутр(ХранилищеСистемныхНастроек.Загрузить(КлючОбъекта,,ТекущийПользователь));	
	
КонецФункции

&НаСервере
Процедура УстановитьИмяПользователяНаСервере()
	
	ИмяПользователяИБ = "";
	Если ЗначениеЗаполнено(Пользователь) Тогда
		
		ПользовательОтбора = Пользователь;
	Иначе
		ПользовательОтбора = Пользователи.ТекущийПользователь();
	КонецЕсли;
	ПрочитанныеСвойства = Неопределено;
	Пользователи.ПрочитатьПользователяИБ(ПользовательОтбора.ИдентификаторПользователяИБ, ПрочитанныеСвойства);
	Если ТипЗнч(ПрочитанныеСвойства) = Тип("Структура") Тогда
		ИмяПользователяИБ = ПрочитанныеСвойства.Имя;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ОчиститьТаблицы()
	
	РеквизитФормы = РеквизитФормыВЗначение("ДеревоХранилищеНастроекДанныхФорм");
	РеквизитФормы.Строки.Очистить();
	ЗначениеВРеквизитФормы(РеквизитФормы,"ДеревоХранилищеНастроекДанныхФорм");
	
	РеквизитФормы = РеквизитФормыВЗначение("ДеревоХранилищеОбщихНастроек");
	РеквизитФормы.Строки.Очистить();
	ЗначениеВРеквизитФормы(РеквизитФормы,"ДеревоХранилищеОбщихНастроек");
	
	РеквизитФормы = РеквизитФормыВЗначение("ДеревоХранилищеСистемныхНастроек");
	РеквизитФормы.Строки.Очистить();
	ЗначениеВРеквизитФормы(РеквизитФормы,"ДеревоХранилищеСистемныхНастроек");
	
	ТекстНастроек = "";
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьТекущийСписокНаСервере(ИмяТекущегоХранилища)
	
	Перем МенеджерХранилища;
	
	ИзменятьДанные = (ИмяТекущегоХранилища = "ХранилищеОбщихНастроек" ИЛИ ИмяТекущегоХранилища = "ХранилищеНастроекДанныхФорм");
	
	ИмяДерева = "Дерево" + ИмяТекущегоХранилища;
	
	ДеревоЗначений = РеквизитФормыВЗначение(ИмяДерева);
	ДеревоЗначений.Строки.Очистить();
		
	МенеджерХранилища = Вычислить(ИмяТекущегоХранилища);
		
	Если ОтборПоПользователю Тогда	
		Выборка = МенеджерХранилища.Выбрать(Новый Структура("Пользователь",ИмяПользователяИБ));
	Иначе
		Выборка = МенеджерХранилища.Выбрать();
	КонецЕсли;
	
	ТаблицаНастроек = Новый ТаблицаЗначений;
	ТаблицаНастроек.Колонки.Добавить("КлючОбъекта",Новый ОписаниеТипов("Строка"));
	ТаблицаНастроек.Колонки.Добавить("Настройки");
	ТаблицаНастроек.Колонки.Добавить("КлючНастроек",Новый ОписаниеТипов("Строка"));
	ТаблицаНастроек.Колонки.Добавить("Значение");
	ТаблицаНастроек.Колонки.Добавить("Пользователь",Новый ОписаниеТипов("Строка"));
	ТаблицаНастроек.Колонки.Добавить("СкрытыйКлючОбъекта",Новый ОписаниеТипов("Строка"));
	ТаблицаНастроек.Колонки.Добавить("РазрешеноРедактирование",Новый ОписаниеТипов("Булево"));
	
	Пока Выборка.Следующий() Цикл
		СтрокаТаблицы  = ТаблицаНастроек.Добавить();
		СтрокаТаблицы.КлючОбъекта = Выборка.КлючОбъекта;
		СтрокаТаблицы.КлючНастроек = Выборка.КлючНастроек;
		СтрокаТаблицы.Настройки = Строка(ТипЗнч(Выборка.Настройки));
		СтрокаТаблицы.Пользователь = Выборка.Пользователь;
		СтрокаТаблицы.СкрытыйКлючОбъекта = Выборка.КлючОбъекта;
		Если ИзменятьДанные Тогда
			Если РазрешенныйДляРедактирования(Выборка.Настройки) Тогда
				СтрокаТаблицы.Значение = Выборка.Настройки;
				СтрокаТаблицы.РазрешеноРедактирование = Истина;
			Иначе
				СтрокаТаблицы.Значение = СтрокаТаблицы.Настройки;
			КонецЕсли;	
		КонецЕсли;
		
	КонецЦикла;
	ТаблицаНастроек.Сортировать("КлючОбъекта Возр, Пользователь Возр, КлючНастроек Возр");
	
	ИмяКлюча = "";
	Для Каждого СтрокаТаблицы Из ТаблицаНастроек Цикл
		Если Не ИмяКлюча = СтрокаТаблицы.КлючОбъекта Тогда
			СтрокаКлючОбъекта = ДеревоЗначений.Строки.Добавить();
			СтрокаКлючОбъекта.КлючОбъекта = СтрокаТаблицы.КлючОбъекта;
		КонецЕсли;
		СтрокаНастроек = СтрокаКлючОбъекта.Строки.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаНастроек,СтрокаТаблицы);
		СтрокаНастроек.КлючОбъекта = "";
		ИмяКлюча = СтрокаТаблицы.КлючОбъекта;
	КонецЦикла;	

	
	ЗначениеВРеквизитФормы(ДеревоЗначений,ИмяДерева);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьРежимОбработки(РежимРаботы)
	
	ГруппаРежима = КоманднаяПанель.ПодчиненныеЭлементы.РежимОбработки.ПодчиненныеЭлементы;
	
	ГруппаРежима.ФормаНаКлиенте.Пометка = РежимРаботы;
	ГруппаРежима.ФормаНаСервере.Пометка = Не РежимРаботы;
	
	КоманднаяПанель.ПодчиненныеЭлементы.РежимОбработки.Заголовок = 
	?(РежимРаботы, НСтр("ru = 'Режим работы (на клиенте)'"), НСтр("ru = 'Режим работы (на сервере)'"));
		
	//ИзменитьРасположениеФайла();
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиВФайлНаСервере(Команда)
	
		
КонецПроцедуры

&НаСервере
Процедура ПередатьНастройкиНаСервере(Получатель,СтруктураПараметров) Экспорт
	
	Если Получатель = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ИмяПользователя = "";
	ПрочитанныеСвойства = Неопределено;
	Пользователи.ПрочитатьПользователяИБ(Получатель.ИдентификаторПользователяИБ, ПрочитанныеСвойства);
	Если ТипЗнч(ПрочитанныеСвойства) = Тип("Структура") Тогда
		ИмяПользователя = ПрочитанныеСвойства.Имя;
	КонецЕсли;
	
	Если ПустаяСтрока(ИмяПользователя) Тогда
		Сообщить("У выбранного пользователя не указанн пользователь ИБ");
		Возврат;
	КонецЕсли;

	СтруктураПараметров.Вставить("Получатель",ИмяПользователя); 	
	 
	
	СтрокаОшибки = "";	
	СтрокаОшибки = УстановитьНастройкиДругомуПользователю(СтруктураПараметров);
	
	Если Не ПустаяСтрока(СтрокаОшибки) Тогда
		Сообщить(СтрокаОшибки);
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция УстановитьНастройкиДругомуПользователю(СтруктураПараметров)
	
	СтрокаОшибки = "";
	
	ТекущееХранилище = СтруктураПараметров.ТекущееХранилище;
	
	МенеджерХранилища = Вычислить(ТекущееХранилище);
	Попытка
		ОписаниеНастроек = МенеджерХранилища.ПолучитьОписание(СтруктураПараметров.КлючОбъекта, 
		СтруктураПараметров.КлючНастроек,
		СтруктураПараметров.ИмяПользователя);
		
		Настройки = МенеджерХранилища.Загрузить(СтруктураПараметров.КлючОбъекта,
		СтруктураПараметров.КлючНастроек,
		ОписаниеНастроек,
		СтруктураПараметров.ИмяПользователя);
		
		ОписаниеНастроек.Пользователь = СтруктураПараметров.Получатель;
		
		МенеджерХранилища.Сохранить(СтруктураПараметров.КлючОбъекта, 
		СтруктураПараметров.КлючНастроек, 
		Настройки, 
		ОписаниеНастроек, 
		СтруктураПараметров.Получатель);
	Исключение
		СтрокаОшибки = ОписаниеОшибки();
	КонецПопытки;
	
	Возврат СтрокаОшибки;
КонецФункции

&НаСервереБезКонтекста
Функция  ПередатьВсемПользователямНаСервере(СтруктураПараметров)
	
	СтрокаОшибки = "";
	МассивПользователей = ПользователиИнформационнойБазы.ПолучитьПользователей();
						  
	Для Каждого ПользовательИБ Из МассивПользователей Цикл
		ИмяПользователяИБ = ПользовательИБ.Имя;
		Если ИмяПользователяИБ = СтруктураПараметров.ИмяПользователя Тогда
			Продолжить;
		КонецЕсли;
		СтруктураПараметров.Вставить("Получатель",ИмяПользователяИБ);
		СтрокаОшибки = УстановитьНастройкиДругомуПользователю(СтруктураПараметров);
		
		Если Не ПустаяСтрока(СтрокаОшибки) Тогда
			Возврат СтрокаОшибки;
		КонецЕсли;
	КонецЦикла;
	
	Возврат СтрокаОшибки;
	
КонецФункции

&НаСервереБезКонтекста
Функция УдалитьНастройкиНаСервере(СтруктураПараметров)
	
	СтрокаОшибки = "";
	
	ТекущееХранилище = СтруктураПараметров.ТекущееХранилище;
	
	МенеджерХранилища = Вычислить(ТекущееХранилище);
	
	Попытка
		МенеджерХранилища.Удалить(СтруктураПараметров.КлючОбъекта, СтруктураПараметров.КлючНастроек, СтруктураПараметров.ИмяПользователя);
	Исключение
		СтрокаОшибки = ОписаниеОшибки();
	КонецПопытки;
	
	Возврат СтрокаОшибки;
	
КонецФункции

&НаКлиенте
Функция ПослеВопросаУдаления(РезультатВопроса,СтруктураПараметров) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		СтрокаОшибки = "";
		СтрокаОшибки = УдалитьНастройкиНаСервере(СтруктураПараметров);
		
		Если Не ПустаяСтрока(СтрокаОшибки) Тогда
			Сообщить(СтрокаОшибки);
		КонецЕсли; 
	КонецЕсли;
	
КонецФункции
	
#КонецОбласти






