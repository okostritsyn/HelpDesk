&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СписокЭлементов = ДеревоМетаданных.ПолучитьЭлементы();
	СписокЭлементов.Очистить();
	
	МетаданныеСтр = "Метаданные";
	ДобавитьСправочники(СписокЭлементов, МетаданныеСтр);
	ДобавитьДокументы(СписокЭлементов, МетаданныеСтр);
	ДобавитьБизнесПроцессы(СписокЭлементов, МетаданныеСтр);
	ДобавитьЗадачи(СписокЭлементов, МетаданныеСтр);
	
	Если Параметры.Свойство("Наименование") Тогда
		Наименование = Параметры.Наименование;
	КонецЕсли;
	Если Параметры.Свойство("Раздел") Тогда
		Дерево = РеквизитФормыВЗначение("ДеревоМетаданных");
		Для каждого ИмяМетаданных Из Параметры.Раздел Цикл
			СтрокаДерева = Дерево.Строки.Найти(ИмяМетаданных, "Путь", Истина);
			Если СтрокаДерева <> Неопределено Тогда
				СтрокаДерева.Пометка = 1;
				ДеревоПометкаПриИзмененииНаСервере(СтрокаДерева);
			КонецЕсли;
		КонецЦикла;
		ЗначениеВРеквизитФормы(Дерево, "ДеревоМетаданных");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьСправочники(СписокЭлементов, Знач МетаданныеСтр)
	МетаданныеСправочникиСтр = МетаданныеСтр + ".Справочники";
	СписокСправочников = НовыйЭлементДерева(СписокЭлементов, НСтр("ru = 'Справочники'"), МетаданныеСправочникиСтр, Истина, БиблиотекаКартинок.Справочник).ПолучитьЭлементы();
	Для каждого Мета Из Метаданные.Справочники Цикл
		Если ПравоДоступа("Просмотр", Мета) Тогда
			Если Мета.ПолнотекстовыйПоиск = Метаданные.СвойстваОбъектов.ИспользованиеПолнотекстовогоПоиска.Использовать Тогда
				МетаданныеСправочникСтр = МетаданныеСправочникиСтр + "." + Мета.Имя;
				ЭлементыДерева = НовыйЭлементДерева(СписокСправочников, Мета.Имя, МетаданныеСправочникСтр, Ложь, БиблиотекаКартинок.Справочник).ПолучитьЭлементы();
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ДобавитьДокументы(СписокЭлементов, Знач МетаданныеСтр)
	МетаданныеДокументыСтр = МетаданныеСтр + ".Документы";
	СписокДокументов = НовыйЭлементДерева(СписокЭлементов, НСтр("ru = 'Документы'"), МетаданныеДокументыСтр, Истина, БиблиотекаКартинок.Документ).ПолучитьЭлементы();
	Для каждого Мета Из Метаданные.Документы Цикл
		Если ПравоДоступа("Просмотр", Мета) Тогда
			Если Мета.ПолнотекстовыйПоиск = Метаданные.СвойстваОбъектов.ИспользованиеПолнотекстовогоПоиска.Использовать Тогда
				МетаданныеДокументСтр = МетаданныеДокументыСтр + "." + Мета.Имя;
				ЭлементыДерева = НовыйЭлементДерева(СписокДокументов, Мета.Имя, МетаданныеДокументСтр, Ложь, БиблиотекаКартинок.Документ).ПолучитьЭлементы();
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ДобавитьБизнесПроцессы(СписокЭлементов, Знач МетаданныеСтр)
	МетаданныеБизнесПроцессыСтр = МетаданныеСтр + ".БизнесПроцессы";
	СписокБизнесПроцессов = НовыйЭлементДерева(СписокЭлементов, НСтр("ru = 'Процессы'"), МетаданныеБизнесПроцессыСтр, Истина, БиблиотекаКартинок.БизнесПроцесс).ПолучитьЭлементы();
	Для каждого Мета Из Метаданные.БизнесПроцессы Цикл
		Если ПравоДоступа("Просмотр", Мета) Тогда
			Если Мета.ПолнотекстовыйПоиск = Метаданные.СвойстваОбъектов.ИспользованиеПолнотекстовогоПоиска.Использовать Тогда
				МетаданныеБизнесПроцессСтр = МетаданныеБизнесПроцессыСтр + "." + Мета.Имя;
				ЭлементыДерева = НовыйЭлементДерева(СписокБизнесПроцессов, Мета.Имя, МетаданныеБизнесПроцессСтр, Ложь, БиблиотекаКартинок.БизнесПроцесс).ПолучитьЭлементы();
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ДобавитьЗадачи(СписокЭлементов, Знач МетаданныеСтр)
	МетаданныеЗадачиСтр = МетаданныеСтр + ".Задачи";
	СписокЗадач = НовыйЭлементДерева(СписокЭлементов, НСтр("ru = 'Задачи'"), МетаданныеЗадачиСтр, Истина, БиблиотекаКартинок.Задача).ПолучитьЭлементы();
	Для каждого Мета Из Метаданные.Задачи Цикл
		Если ПравоДоступа("Просмотр", Мета) Тогда
			Если Мета.ПолнотекстовыйПоиск = Метаданные.СвойстваОбъектов.ИспользованиеПолнотекстовогоПоиска.Использовать Тогда
				МетаданныеЗадачаСтр = МетаданныеЗадачиСтр + "." + Мета.Имя;
				ЭлементыДерева = НовыйЭлементДерева(СписокЗадач, Мета.Имя, МетаданныеЗадачаСтр, Ложь, БиблиотекаКартинок.Задача).ПолучитьЭлементы();
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Функция НовыйЭлементДерева(СписокЭлементов, Имя, Путь, ЭтоГруппа, Картинка)
	
	ЭлементДерева = СписокЭлементов.Добавить();
	ЭлементДерева.Имя = Имя;
	ЭлементДерева.Пометка = 0;
	ЭлементДерева.Путь = Путь;
	ЭлементДерева.ЭтоГруппа = ЭтоГруппа;
	ЭлементДерева.Картинка = Картинка;
	
	Возврат ЭлементДерева;
	
КонецФункции

&НаКлиенте
Процедура ДеревоМетаданныхПометкаПриИзменении(Элемент)
	
	ТекСтрока = Элементы.ДеревоМетаданных.ТекущаяСтрока;
	ТекДанные = Элементы.ДеревоМетаданных.ТекущиеДанные;
	Пометка = ТекДанные.Пометка % 2;
	ДеревоМетаданных.НайтиПоИдентификатору(ТекСтрока).Пометка = Пометка;
	
	// Установим пометки подчиненных элементов
	УстановитьПометкиПодчиненнымСтрокам(ТекДанные, Пометка);
	
	// Установим пометки родителей
	УстановитьПометкиРодителям(ТекДанные.ПолучитьРодителя());
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПометкиПодчиненнымСтрокам(Знач Родитель, Знач Пометка)
	Для каждого СтрокаДерева Из Родитель.ПолучитьЭлементы() Цикл
		СтрокаДерева.Пометка = Пометка;
		УстановитьПометкиПодчиненнымСтрокам(СтрокаДерева, Пометка);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПометкиРодителям(Знач Родитель)
	Пока Родитель <> Неопределено Цикл
		Пометка = Неопределено;
		Для каждого Элемент Из Родитель.ПолучитьЭлементы() Цикл
			Если Пометка = Неопределено Тогда
				Пометка = Элемент.Пометка;
				Продолжить;
			КонецЕсли;
			Если Пометка = 2 Тогда
				Прервать;
			КонецЕсли;
			Если ((Элемент.Пометка = 1) И (Пометка = 0)) Или ((Элемент.Пометка = 0) И (Пометка = 1)) Тогда
				Пометка = 2;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		ТекСтрока = Родитель.ПолучитьИдентификатор();
		ДеревоМетаданных.НайтиПоИдентификатору(ТекСтрока).Пометка = Пометка;
		Родитель = Родитель.ПолучитьРодителя();
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ДеревоПометкаПриИзмененииНаСервере(Элемент)
	УстановитьПометкиПодчиненнымСтрокамНаСервере(Элемент, Элемент.Пометка);
	УстановитьПометкиРодителямНаСервере(Элемент.Родитель);
КонецПроцедуры

&НаСервере
Процедура УстановитьПометкиПодчиненнымСтрокамНаСервере(Знач Родитель, Знач Пометка)
	Для каждого СтрокаДерева Из Родитель.Строки Цикл
		СтрокаДерева.Пометка = Пометка;
		УстановитьПометкиПодчиненнымСтрокамНаСервере(СтрокаДерева, Пометка);
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура УстановитьПометкиРодителямНаСервере(Знач Родитель)
	Пока Родитель <> Неопределено Цикл
		Пометка = Неопределено;
		Для каждого СтрокаДерева Из Родитель.Строки Цикл
			Если Пометка = Неопределено Тогда
				Пометка = СтрокаДерева.Пометка;
				Продолжить;
			КонецЕсли;
			Если Пометка = 2 Тогда
				Прервать;
			КонецЕсли;
			Если ((СтрокаДерева.Пометка = 1) И (Пометка = 0)) ИЛИ ((СтрокаДерева.Пометка = 0) И (Пометка = 1)) Тогда
				Пометка = 2;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Родитель.Пометка = Пометка;
		Родитель = Родитель.Родитель;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	ПараметрРаздел = ПолучитьМассивМетаданных();
	Если ПараметрРаздел.Количество() = 0 Тогда
		Закрыть();
		Возврат;
	КонецЕсли;
	
	Если ПустаяСтрока(Наименование) Тогда
		Наименование = "Раздел " + ТекущаяДата();
	КонецЕсли;
	
	Если ПолнотекстовыйПоискСерверПереопределяемый.ЭтоСтандартныйРазделПоиска(Наименование) Тогда
		ПоказатьПредупреждение(Неопределено, НСтр("ru = 'Наименование раздела совпадает со стандартным разделом поиска. Переименуйте раздел.'"));
		Возврат;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("Раздел", ПараметрРаздел);
	Результат.Вставить("Наименование", Наименование);
	Закрыть(Результат);
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьМассивМетаданных()
	МассивРезультат = Новый Массив;
	ДобавитьМетаданные(МассивРезультат, ДеревоМетаданных);
	Возврат МассивРезультат;
КонецФункции

&НаКлиенте
Процедура ДобавитьМетаданные(МассивРезультат, Знач Владелец)
	
	Для каждого ЭлементДерева Из Владелец.ПолучитьЭлементы() Цикл
		Если ЭлементДерева.Пометка = 0 Тогда
			Продолжить;
		ИначеЕсли (ЭлементДерева.Пометка = 1) И НЕ ЭлементДерева.ЭтоГруппа Тогда
			МассивРезультат.Добавить(ЭлементДерева.Путь);
		Иначе
			ДобавитьМетаданные(МассивРезультат, ЭлементДерева);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть(Неопределено);
КонецПроцедуры
